TMCx(1342, 101, aiscript):
#Nuke Script, cheating
    start_town()
    transports_off()
    defaultbuild_off()
#if DEBUG
    debug(debugmsg, Miracle Invocation 1.2 by Archon_Wing)
    --debugmsg--
#endif
    default_min(0)
    define_max(42, marine)
    define_max(15, scv)
    define_max(19, medic)
    define_max(20, siege_tank)
    define_max(20, goliath)
    define_max(7, wraith)
    define_max(4, dropship)
    define_max(6, battlecruiser)
    define_max(12, science_vessel)
    define_max(2, valkyrie)
    define_max(16, ghost)
    define_max(5, nuclear_missile)
    define_max(4, firebat)
    define_max(30, vulture)
    build(1, command_center, 180)
    wait_buildstart(1, command_center)
    multirun(money)
    multirun(ebay_up)
	multirun(scvs)
    build(1, barracks, 80)
    wait_buildstart(1, barracks)
    build(1, supply_depot, 80)
    wait_buildstart(1, supply_depot)

    multirun(depots)
    build(2, barracks, 80)
    wait_buildstart(2, barracks)
    build(3, barracks, 80)
    multirun(mass rax)
	wait(1)
	multirun(marines)
    wait_build(1, barracks)
	defenseuse_gg(1, marine)
    defenseuse_ga(1, marine)
    defenseuse_aa(1, marine)
    defenseuse_ag(1, marine)
    defensebuild_gg(1, marine)
    defensebuild_ga(1, marine)
    defensebuild_aa(1, marine)
    defensebuild_ag(1, marine)

	wait(1)
	multirun(bunker)
	player_need(5, scv)
	wait(1)

	wait(1)
    multirun(comsat)
	expand(99, expand)
	wait(1)
    multirun(meds)
	wait(1)
    multirun(tech asap)
    multirun(ghosts)
	wait(1)
	expand(99, expand)
	wait(1)
    multirun(vessels)
	wait(1)
    multirun(stim range)
	wait(1)
    multirun(turrets)
    wait(100)
    multirun(place stuff)
	wait(5)
	multirun(get bcs)
	wait(5)
	multirun(train bcs)
	wait(100)
	multirun(air up)
	wait(1)
	multirun(air armor)
	train(25, marine)
	wait_build(1, physics_lab)
	train(6, battlecruiser)
    --loop--
	expand(99, expand)
	attack_add(39, marine)
	attack_add(6, battlecruiser)
	attack_add(2, science_vessel)
	attack_add(10, ghost)
	attack_add(15, medic)
	attack_add(4, wraith)
	wait(6500)
	attack_do()
	attack_clear()
	clear_combatdata()
    goto(loop)
	
	--expand--
	start_town()
	build(1, command_center, 80)
	wait_build(1, command_center)
	build(2, command_center, 80)
	get_oldpeons(2)
	build(2, barracks, 80)
	multirun(turrets)
	--wait for ops--
	if_owned(covert_ops, expo nukes)
	wait(300)
	goto(wait for ops)
	--expo nukes--
	wait_build(2, command_center)
	build(2, nuclear_silo, 80)
	build(3, barracks, 80)
	stop()
	
    
    --marines--
	wait_build(1, barracks)
	train(42, marine)
	wait_force(42, marine)
    wait(530)
    goto(marines)
    
    --ghosts--
	wait_build(1, covert_ops)
	train(16, ghost)
    wait_force(16, ghost)
	wait(2000)
    goto(ghosts)
    
    --meds--
	wait_build(1, academy)
	train(11, medic)
	wait_force(12, medic)
    stop()
    
    --place stuff--
    wait_build(1, control_tower)
	defenseuse_gg(1, wraith)
	defenseuse_ga(1, wraith)
	defenseuse_aa(1, wraith)
	defenseuse_ag(1, wraith)
	defensebuild_gg(1, wraith)
	defensebuild_ga(1, wraith)
	defensebuild_aa(1, wraith)
	defensebuild_ag(1, wraith)
	tech(cloaking_field, 80)
	wait(3000)
	upgrade(1, wraith_mana, 80)
    wait(30000)
    check_transports()
    stop()
    
    --vessels--
	wait_build(1, science_facility)
    train(3, science_vessel)
    wait(1900)
    goto(vessels)
    

    
    --money--
    give_money()
    wait(100)

    goto(money)
    
    --tech asap--
    build(1, factory, 85)
    wait_buildstart(1, factory)
    build(1, academy, 85)
    wait_buildstart(1, academy)
    build(1, engineering_bay, 85)

    wait_build(1, factory)
    build(1, starport, 85)
    wait_build(1, starport)
    build(1, control_tower, 85)
    build(1, science_facility, 85)
    wait_build(1, science_facility)
    build(1, covert_ops, 85)

	wait_build(1, covert_ops)
	defenseuse_gg(1, ghost)
    defenseuse_ga(1, ghost)
    defenseuse_aa(1, ghost)
    defenseuse_ag(1, ghost)
    defensebuild_gg(1, ghost)
    defensebuild_ga(1, ghost)
    defensebuild_aa(1, ghost)
    defensebuild_ag(1, ghost)
    tech(personnel_cloaking, 85)
	tech(irradiate, 85)
    wait(3000)
	enemyowns_jump(overlord, skip lockdown)
    tech(lockdown, 85)
	--skip lockdown--
	upgrade(1, science_vessel_mana, 85)
    wait(3000)

	upgrade(1, ghost_sight, 85)
	enemyowns_jump(overlord, skip emp)
	tech(emp_shockwave, 85)
	--skip emp--

    wait(1250)
	upgrade(1, ghost_mana, 85)


    
    stop()
    
    --comsat--
    wait(300)
    notowns_jump(academy, comsat)
    build(1, comsat_station, 80)
	defensebuild_gg(1, firebat)
	wait(2000)
    build(2, command_center, 80)
    wait_build(2, command_center)
    build(2, comsat_station, 80)
	wait(4000)
    build(3, command_center, 80)
	wait(500)
	build(3, command_center, 80)
	wait_build(1, covert_ops)
	wait_build(3, command_center)
	build(1, nuclear_silo, 80)
	stop()
    
 

    
    
    
    --scvs--
    build(6, scv, 80)
    wait_build(6, scv)
    build(9, scv, 80)
    wait_build(9, scv)
    build(10, scv, 80)
	wait_build(10, scv)
	build(15, scv, 80)
	wait_build(15, scv)
	stop()
    

    
    --turrets--
    wait(300)
    notowns_jump(engineering_bay, turrets)
    player_need(1, engineering_bay)
    build(3, missile_turret, 80)
    wait_build(3, missile_turret)
    build(6, missile_turret, 80)
	wait_build(6, missile_turret)
    stop()
    
    --stim range--
    wait_build(1, academy)
    tech(stim_packs, 100)
    wait(1250)

    upgrade(1, marine_range, 100)
    wait(1250)
    tech(optical_flare, 90)
    wait(1500)
    upgrade(1, medic_mana, 90)
	wait(2000)
	tech(restoration, 90)
    stop()
    

    
    --mass rax--
    wait_buildstart(3, barracks)
    build(4, barracks, 80)
    wait_buildstart(4, barracks)
    build(5, barracks, 80)
    wait_buildstart(5, barracks)
    build(6, barracks, 80)
    wait_buildstart(6, barracks)
    build(7, barracks, 80)
    wait(100)
    build(8, barracks, 80)
	wait_buildstart(8, barracks)
    wait(500)
	build(9, barracks, 80)
	wait_buildstart(9, barracks)
    build(10, barracks, 80)
	wait(100)
    build(11, barracks, 80)
	wait(100)
    build(12, barracks, 80)
	wait_build(12, barracks)
	build(15, barracks, 80)
	wait_build(15, barracks)
    stop()
    

    
    --depots--
	wait(370)
    build(2, supply_depot, 80)
    wait_buildstart(2, supply_depot)
    build(3, supply_depot, 80)
    wait_buildstart(3, supply_depot)
    build(4, supply_depot, 80)
    wait_buildstart(4, supply_depot)
    build(5, supply_depot, 80)
    wait_buildstart(5, supply_depot)
    build(6, supply_depot, 80)
	wait_buildstart(6, supply_depot)
    build(7, supply_depot, 80)
    wait_buildstart(7, supply_depot)
    build(8, supply_depot, 80)
    wait_build(8, supply_depot)
    build(9, supply_depot, 120)
    wait_buildstart(9, supply_depot)
    build(10, supply_depot, 120)
    wait_buildstart(10, supply_depot)
    build(11, supply_depot, 120)
	wait_build(11, supply_depot)
	build(12, supply_depot, 120)
	wait_build(12, supply_depot)
    build(13, supply_depot, 120)
    wait_buildstart(13, supply_depot)
    build(14, supply_depot, 120)
    wait_build(14, supply_depot)
    build(15, supply_depot, 120)
    wait_buildstart(15, supply_depot)
    build(16, supply_depot, 120)
    wait_buildstart(16, supply_depot)
    build(17, supply_depot, 120)
	wait_buildstart(17, supply_depot)
	build(19, supply_depot, 120)
	wait_buildstart(19, supply_depot)
	build(21, supply_depot, 80)
    stop()
	
	--bunker--
	build(2, bunker, 80)
	wait(4500)
	build(4, bunker, 80)
	stop()
	
	--get bcs--
	wait_build(1, covert_ops)
	build(2, science_facility, 120)
	wait_build(2, science_facility)
	build(1, physics_lab, 120)
	wait(100)
	wait_build(1, physics_lab)
	defenseuse_gg(1, battlecruiser)
	defenseuse_ga(1, battlecruiser)
	defenseuse_aa(1, battlecruiser)
	defenseuse_ag(1, battlecruiser)
	defensebuild_gg(1, battlecruiser)
	defensebuild_ga(1, battlecruiser)
	defensebuild_aa(1, battlecruiser)
	defensebuild_ag(1, battlecruiser)
	build(3, starport, 120)
	wait(100)
	build(4, starport, 120)
	wait_build(4, starport)
	build(4, control_tower, 120)
	build(7, starport, 120)
	wait_build(7, starport)
	build(7, control_tower, 120)
	build(10, starport, 80)
	wait_build(10, starport)
	build(10, control_tower, 80)
	build(12, starport, 80)
	wait_build(12, starport)
	build(12, control_tower, 80)
	stop()

	--train bcs--
	wait_build(1, physics_lab)
	train(6, battlecruiser)
	wait_force(6, battlecruiser)
	wait(1000)
	goto(train bcs)
	
	--air up--
	wait_build(1, starport)
	wait(3000)
	build(2, armory, 120)
	wait_build(1, armory)
    upgrade(1, t_ship_weapon, 120)
    wait(4500)
	wait_build(1, science_facility)

    upgrade(2, t_ship_weapon, 120)
	wait(4500)

    upgrade(3, t_ship_weapon, 120)
    stop()
	
	--air armor--
	wait_build(2, armory)
	upgrade(1, t_ship_plating, 120)
    wait(4500)
	wait_build(1, science_facility)
	upgrade(2, t_ship_plating, 120)
	 wait(4900)
    upgrade(3, t_ship_plating, 120)
	stop()
	
	            --ebay_up--
	wait(4000)
    build(1, engineering_bay, 120)
	wait_buildstart(1, engineering_bay)
	build(2, engineering_bay, 120)
	wait_build(2, engineering_bay)
    upgrade(1, t_infantry_weapon, 120)

    upgrade(1, t_infantry_armor, 120)
	wait(4500)

    wait_build(1, science_facility)
    upgrade(2, t_infantry_weapon, 120)


    upgrade(2, t_infantry_armor, 120)
    wait(4900)

    upgrade(3, t_infantry_weapon, 120)

    upgrade(3, t_infantry_armor, 120)
    stop()
	
