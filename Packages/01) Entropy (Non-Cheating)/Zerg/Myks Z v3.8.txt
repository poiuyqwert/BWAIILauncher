#Myk's Zerg AI version 3.8
#
# stat_txt.tbl entry 1344: Zerg Expansion Custom Level<0>
ZMCx(1344, 101, aiscript):
	start_town()
	transports_off()
	farms_notiming()
#if DEBUG
	debug(null, Myk's Zerg AI 3.8)
--null--
#endif
	race_jump(ZvT, ZvT, ZvP)
	
#=========ZvT=========:
--ZvT--
	define_max(50, Zerg Drone)
	build(6, Zerg Drone, 80)
	wait_buildstart(6, Zerg Drone)
	random_jump(32, 6poolrush)
	goto(no6pool)
		--6poolrush--
	build(1, Zerg Spawning Pool, 100)
	wait_build(1, Zerg Spawning Pool)
	train(6, Zerg Zergling)
		--6poolrushgo--
	attack_add(6, Zerg Zergling)
	attack_prepare()
	clear_combatdata()
	enemyowns_jump(Terran Bunker, laterpool)
	enemyowns_jump(Terran Academy, laterpool)
	enemyowns_jump(Terran Factory, laterpool)
	attack_do()
	attack_clear()
	wait(25)
	goto(6poolrushgo)
		--no6pool--
	build(9, Zerg Drone, 80)
	wait_buildstart(9, Zerg Drone)
	#random_jump(32, 6poolrush)
	rush(0, overpool)
	goto(laterpool)
		--overpool--
	build(2, Zerg Overlord, 80)
	wait_buildstart(2, Zerg Overlord)
	build(1, Zerg Spawning Pool, 100)
	wait_buildstart(1, Zerg Spawning Pool)
	build(11, Zerg Drone, 80)
	wait_buildstart(11, Zerg Drone)
	defensebuild_gg(1, Zerg Zergling)
	defenseuse_gg(1, Zerg Zergling)
	defenseuse_ga(1, Zerg Zergling)
	expand(1, zvt_nat_expo_build)
	goto(zvt_3hatch)
		--laterpool--
	attack_clear()
	build(2, Zerg Overlord, 80)
	wait_buildstart(2, Zerg Overlord)
	build(12, Zerg Drone, 80)
	wait_buildstart(12, Zerg Drone)
	expand(1, zvt_nat_expo_build)
	wait(200)
	build(1, Zerg Spawning Pool, 100)
	wait_buildstart(1, Zerg Spawning Pool)
	defensebuild_gg(1, Zerg Zergling)
	defenseuse_gg(1, Zerg Zergling)
	defenseuse_ga(1, Zerg Zergling)
	goto(zvt_3hatch)
		--zvt_3hatch--
	build(13, Zerg Drone, 80)
	wait_buildstart(13, Zerg Drone)
	build(2, Zerg Hatchery, 80)
	wait_buildstart(2, Zerg Hatchery)
	build(14, Zerg Drone, 80)
	wait_buildstart(14, Zerg Drone)
	build(1, Zerg Extractor, 80)
	wait_buildstart(1, Zerg Extractor)
	farms_timing()
	multirun(b_drones_a)
	wait_build(1, Zerg Extractor)
	multirun(give_the_money)
	multirun(main_spore)
	random_jump(64, ZvT_D)
	build(1, Zerg Lair, 80)
	wait_buildstart(1, Zerg Lair)
	random_jump(64, ZvT_A)
	upgrade(1, Metabolic Boost, 80)
	random_jump(64, ZvT_B)
	goto(ZvT_C)
	
#ZvT_A - Ling/Ultra:

--ZvT_A--
	define_max(200, Zerg Zergling)
	define_max(100, Zerg Ultralisk)
	define_max(255, Zerg Lurker)
	define_max(255, Zerg Hydralisk)
	define_max(255, Zerg Scourge)
	define_max(255, Zerg Queen)
	define_max(255, Zerg Mutalisk)
	define_max(255, Zerg Defiler)
	define_max(255, Zerg Guardian)
	build(1, Zerg Evolution Chamber, 80)
	wait_build(1, Zerg Lair)
	build(1, Zerg Queen's Nest, 80)
	multirun(r_evo_carapace)
	wait_buildstart(1, Zerg Queen's Nest)
	upgrade(1, Metabolic Boost, 80)
	wait_build(1, Zerg Queen's Nest)
	build(1, Zerg Hive, 80)
	wait_buildstart(1, Zerg Hive)
	wait_build(1, Zerg Hive)
	multirun(t_ling)
	build(1, Zerg Ultralisk Cavern, 80)
	wait_buildstart(1, Zerg Ultralisk Cavern)
	build(2, Zerg Evolution Chamber, 80)
	multirun(r_evo_melee)
	upgrade(1, Adrenal Glands, 80)
	defensebuild_gg(1, Zerg Ultralisk)
	defenseuse_gg(1, Zerg Ultralisk)
	multirun(r_ultraups)
	multirun(t_ultra)
	wait_force(6, Zerg Ultralisk)
	multirun(fast_expo_loop)
	--zvta_endloop--
	multirun(kill)
	wait(50)
	multirun(t_ling)
	multirun(t_ultra)
	wait(3000)
	goto(zvta_endloop)
	
#ZvT_B - Ling/Lurker/Scourge/Defiler/Ultra:

--ZvT_B--
	define_max(200, Zerg Zergling)
	define_max(24, Zerg Hydralisk)
	define_max(24, Zerg Lurker)
	define_max(6, Zerg Scourge)
	define_max(12, Zerg Defiler)
	define_max(24, Zerg Ultralisk)
	define_max(255, Zerg Queen)
	define_max(255, Zerg Mutalisk)
	build(1, Zerg Hydralisk Den, 80)
	wait_buildstart(1, Zerg Hydralisk Den)
	wait_build(1, Zerg Hydralisk Den)
	tech(Lurker Aspect, 80)
	defensebuild_gg(1, Zerg Lurker)
	defenseuse_gg(1, Zerg Lurker)
	train(6, Zerg Hydralisk)
	multirun(t_ling)
	multirun(t_lurker)
	train(3, Zerg Lurker)
	multirun(expo_loop)
	train(9, Zerg Lurker)
	attack_add(9, Zerg Lurker)
	wait(100)
	multirun(t_scourge)
	multirun(r_evo_carapace)
	build(1, Zerg Spire, 80)
	build(1, Zerg Queen's Nest, 80)
	multirun(r_evo_melee)
	multirun(r_overlordups)
	wait_build(1, Zerg Queen's Nest)
	wait(1000)
	multirun(kill)
	wait(100)
	build(1, Zerg Hive, 80)
	multirun(r_cracklingup)
	multirun(t_ling)
	multirun(t_lurker)
	multirun(t_scourge)
	wait_build(1, Zerg Hive)
	multirun(kill)
	wait(100)
	build(1, Zerg Defiler Mound, 80)
	multirun(r_defilerups)
	multirun(t_ling)
	multirun(t_lurker)
	multirun(t_scourge)
	wait_build(1, Zerg Defiler Mound)
	multirun(t_defiler)
	multirun(kill)
	wait(100)
	build(1, Zerg Ultralisk Cavern, 80)
	multirun(r_ultraups)
	multirun(t_ling)
	multirun(t_lurker)
	multirun(t_scourge)
	multirun(t_defiler)
	wait_build(1, Zerg Ultralisk Cavern)
		--zvtb_endloop--
	multirun(kill)
	wait(25)
	multirun(t_ling)
	multirun(t_lurker)
	multirun(t_scourge)
	multirun(t_defiler)
	multirun(t_ultra)
	wait(3000)
	goto(zvtb_endloop)
	
#ZvT_C - Ling/Muta/Ultra/Scourge:

--ZvT_C--
	define_max(100, Zerg Zergling)
	define_max(100, Zerg Mutalisk)
	define_max(255, Zerg Hydralisk)
	define_max(24, Zerg Ultralisk)
	define_max(6, Zerg Scourge)
	define_max(255, Zerg Lurker)
	define_max(255, Zerg Queen)
	define_max(255, Zerg Defiler)
	build(1, Zerg Evolution Chamber, 80)
	multirun(r_evo_carapace)
	multirun(t_ling)
	wait_build(1, Zerg Lair)
	build(1, Zerg Spire, 90)
	wait_buildstart(1, Zerg Spire)
	multirun(fast_expo_loop)
	wait_build(1, Zerg Spire)
	defenseuse_gg(1, Zerg Mutalisk)
	train(12, Zerg Mutalisk)
	multirun(t_muta)
	wait(25)
	multirun(kill)
	wait(25)
	build(1, Zerg Queen's Nest, 80)
	multirun(t_ling)
	multirun(t_muta)
	wait_build(1, Zerg Queen's Nest)
	multirun(kill)
	build(1, Zerg Hive, 80)
	multirun(t_ling)
	multirun(t_muta)
	wait_build(1, Zerg Hive)
	multirun(kill)
	multirun(r_cracklingup)
	build(1, Zerg Ultralisk Cavern, 80)
	multirun(t_ling)
	multirun(t_muta)
	wait_build(1, Zerg Ultralisk Cavern)
	build(2, Zerg Evolution Chamber, 80)
	multirun(t_ultra)
	multirun(r_evo_melee)
	multirun(r_ultraups)
		--ZvT_C_endloop--
	multirun(kill)
	wait(25)
	multirun(t_scourge)
	multirun(t_ling)
	multirun(t_ultra)
	wait(3000)
	goto(ZvT_C_endloop)
	
#ZvT_D - Hydra/Ling/Muta/Guardian:

--ZvT_D--
	define_max(100, Zerg Zergling)
	define_max(100, Zerg Mutalisk)
	define_max(100, Zerg Hydralisk)
	define_max(100, Zerg Guardian)
	define_max(255, Zerg Ultralisk)
	define_max(255, Zerg Scourge)
	define_max(255, Zerg Lurker)
	define_max(255, Zerg Queen)
	define_max(255, Zerg Defiler)
	defenseuse_gg(1, Zerg Zergling)
	defensebuild_gg(1, Zerg Zergling)
	build(1, Zerg Hydralisk Den, 80)
	wait_buildstart(1, Zerg Hydralisk Den)
	upgrade(1, Metabolic Boost, 80)
	multirun(t_ling)
	build(1, Zerg Evolution Chamber, 80)
	wait_buildstart(1, Zerg Evolution Chamber)
	multirun(r_evo_carapace)
	wait_build(1, Zerg Hydralisk Den)
	train(12, Zerg Hydralisk)
	multirun(t_hydra)
	multirun(r_hydradentech)
	wait_train(12, Zerg Hydralisk)
	wait(3000)
	multirun(fast_expo_loop)
	multirun(kill)
	build(1, Zerg Lair, 80)
	multirun(t_hydra)
	multirun(t_ling)
	wait_build(1, Zerg Lair)
	multirun(kill)
	build(1, Zerg Queen's Nest, 80)
	build(2, Zerg Evolution Chamber, 80)
	multirun(r_evo_missile)
	multirun(t_hydra)
	multirun(t_ling)
	wait_build(1, Zerg Queen's Nest)
	multirun(kill)
	build(1, Zerg Hive, 80)
	build(1, Zerg Spire, 80)
	multirun(t_hydra)
	multirun(t_ling)
	multirun(t_muta)
	wait_build(1, Zerg Hive)
	multirun(kill)
	multirun(r_cracklingup)
	build(1, Zerg Greater Spire, 80)
	multirun(t_hydra)
	multirun(t_ling)
	multirun(t_muta)
	wait_build(1, Zerg Greater Spire)
	multirun(r_spire_armor)
		--ZvT_D_endloop--
	multirun(kill)
	wait(25)
	multirun(t_hydra)
	multirun(t_ling)
	multirun(t_muta)
	multirun(t_guardian)
	wait(3000)
	goto(ZvT_D_endloop)
	
#=========ZvP=========:

--ZvP--
	define_max(6, Zerg Scourge)
	define_max(13, Zerg Lurker)
	define_max(50, Zerg Drone)
	define_max(200, Zerg Zergling)
	define_max(100, Zerg Defiler)
	define_max(100, Zerg Ultralisk)
	define_max(200, Zerg Hydralisk)
	define_max(255, Zerg Queen)
	define_max(255, Zerg Mutalisk)
	build(1, Zerg Hatchery, 80)
	wait_build(1, Zerg Hatchery)
	build(9, Zerg Drone, 80)
	wait_buildstart(9, Zerg Drone)
	build(2, Zerg Overlord, 80)
	wait_buildstart(2, Zerg Overlord)
	build(12, Zerg Drone, 100)
	wait_buildstart(12, Zerg Drone)
	expand(99, zvp_nat_expo_build)
	build(1, Zerg Spawning Pool, 150)
	wait_buildstart(1, Zerg Spawning Pool)
	defenseuse_gg(1, Zerg Zergling)
	defensebuild_gg(1, Zerg Zergling)
	build(13, Zerg Drone, 100)
	wait_buildstart(13, Zerg Drone)
	expand(99, zvp_nat_expo_build)
	multirun(give_the_money)
	multirun(b_drones_a)
	farms_timing()
	build(1, Zerg Extractor, 80)
	wait_build(1, Zerg Extractor)
	build(1, Zerg Lair, 80)
	wait_buildstart(1, Zerg Lair)
	upgrade(1, Metabolic Boost, 80)
	wait_build(1, Zerg Lair)
	build(1, Zerg Spire, 100)
	wait_buildstart(1, Zerg Spire)
	wait(200)
	build(2, Zerg Hatchery, 80)
	wait_buildstart(2, Zerg Hatchery)
	wait(200)
	build(1, Zerg Hydralisk Den, 80)
	wait_buildstart(1, Zerg Hydralisk Den)
	wait(200)
	build(3, Zerg Hatchery, 80)
	wait_buildstart(3, Zerg Hatchery)
	wait(200)
	multirun(t_scourge)
	multirun(r_hydradentech)
	multirun(main_spore)
	build(1, Zerg Evolution Chamber, 80)
	multirun(r_evo_missile)
	multirun(r_overlordups)
	multirun(t_hydra)
	train(12, Zerg Hydralisk)
	multirun(t_ling)
	wait(2500)
	multirun(kill)
	multirun(zvp_expo_loop)
	wait(50)
	build(1, Zerg Queen's Nest, 80)
	guard_resources(Zerg Zergling)
	multirun(t_hydra)
	multirun(t_scourge)
	multirun(t_lurker)
	wait_build(1, Zerg Queen's Nest)
	multirun(kill)
	wait(50)
	build(1, Zerg Hive, 80)
	build(3, Zerg Evolution Chamber, 80)
	multirun(r_evo_carapace)
	multirun(r_evo_melee)
	multirun(t_hydra)
	multirun(t_lurker)
	multirun(t_scourge)
	multirun(t_ling)
	wait_build(1, Zerg Hive)
	multirun(kill)
	wait(50)
	build(1, Zerg Ultralisk Cavern, 80)
	multirun(r_cracklingup)
	multirun(t_hydra)
	multirun(t_ling)
	multirun(t_lurker)
	multirun(t_scourge)
	wait_build(1, Zerg Ultralisk Cavern)
	multirun(r_ultraups)
		--zvp_endloop--
	multirun(kill)
	wait(50)
	multirun(t_ling)
	multirun(t_ultra)
	multirun(t_scourge)
	wait(3000)
	goto(zvp_endloop)

#Unit Training:
--t_ling--
	wait(25)
	notowns_jump(Zerg Spawning Pool, t_ling)
	defenseuse_gg(2, Zerg Zergling)
	defenseuse_ga(2, Zerg Zergling)
	defensebuild_gg(2, Zerg Zergling)
	train(6, Zerg Zergling)
	attack_add(6, Zerg Zergling)
	train(12, Zerg Zergling)
	attack_add(6, Zerg Zergling)
	train(18, Zerg Zergling)
	attack_add(6, Zerg Zergling)
	train(24, Zerg Zergling)
	attack_add(6, Zerg Zergling)
	train(30, Zerg Zergling)
	attack_add(6, Zerg Zergling)
	train(36, Zerg Zergling)
	attack_add(6, Zerg Zergling)
	train(42, Zerg Zergling)
	attack_add(6, Zerg Zergling)
	train(48, Zerg Zergling)
	attack_add(6, Zerg Zergling)
	stop()
	
--t_hydra--
	wait(25)
	notowns_jump(Zerg Hydralisk Den, t_hydra)
	defenseuse_gg(1, Zerg Hydralisk)
	defenseuse_ag(1, Zerg Hydralisk)
	defenseuse_ga(1, Zerg Hydralisk)
	defenseuse_aa(1, Zerg Hydralisk)
	defensebuild_gg(1, Zerg Hydralisk)
	defensebuild_ga(1, Zerg Hydralisk)
	defensebuild_ag(1, Zerg Hydralisk)
	defensebuild_aa(1, Zerg Hydralisk)
	train(1, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(2, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(3, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(4, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(5, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(6, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(7, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(8, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(9, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(10, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(11, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(12, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(13, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(14, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(15, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(16, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(17, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(18, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(19, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(20, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(21, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(22, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(23, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(24, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(25, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(26, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(27, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(28, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(29, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(30, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(31, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(32, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(33, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(34, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(35, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(36, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(37, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(38, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(39, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(40, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(41, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(42, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(43, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(44, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(45, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(46, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(47, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	train(48, Zerg Hydralisk)
	attack_add(1, Zerg Hydralisk)
	stop()
	
		--t_lurker--
	wait(25)
	notowns_jump(Zerg Hydralisk Den, t_lurker)
	notowns_jump(Zerg Ultralisk Cavern, t_lurker)
	defenseuse_gg(1, Zerg Lurker)
	defensebuild_gg(1, Zerg Lurker)
	train(1, Zerg Lurker)
	attack_add(1, Zerg Lurker)
	train(2, Zerg Lurker)
	attack_add(1, Zerg Lurker)
	train(3, Zerg Lurker)
	attack_add(1, Zerg Lurker)
	train(4, Zerg Lurker)
	attack_add(1, Zerg Lurker)
	train(5, Zerg Lurker)
	attack_add(1, Zerg Lurker)
	train(6, Zerg Lurker)
	attack_add(1, Zerg Lurker)
	train(7, Zerg Lurker)
	attack_add(1, Zerg Lurker)
	train(8, Zerg Lurker)
	attack_add(1, Zerg Lurker)
	train(9, Zerg Lurker)
	attack_add(1, Zerg Lurker)
	train(10, Zerg Lurker)
	attack_add(1, Zerg Lurker)
	train(11, Zerg Lurker)
	attack_add(1, Zerg Lurker)
	train(12, Zerg Lurker)
	attack_add(1, Zerg Lurker)
	stop()
	
		--t_defiler--
	wait(25)
	notowns_jump(Zerg Defiler Mound, t_defiler)
	place_guard(Zerg Defiler, 0)
	place_guard(Zerg Defiler, 1)
	place_guard(Zerg Defiler, 2)
	train(1, Zerg Defiler)
	attack_add(1, Zerg Defiler)
	wait(25)
	wait_force(2, Zerg Defiler)
	attack_add(1, Zerg Defiler)
	wait(25)
	wait_force(3, Zerg Defiler)
	attack_add(1, Zerg Defiler)
	stop()
	
--t_ultra--
	wait(25)
	notowns_jump(Zerg Ultralisk Cavern, t_ultra)
	defenseuse_gg(1, Zerg Ultralisk)
	defenseuse_ga(1, Zerg Ultralisk)
	defensebuild_gg(1, Zerg Ultralisk)
	defensebuild_ga(1, Zerg Ultralisk)
	train(1, Zerg Ultralisk)
	attack_add(1, Zerg Ultralisk)
	train(2, Zerg Ultralisk)
	attack_add(1, Zerg Ultralisk)
	train(3, Zerg Ultralisk)
	attack_add(1, Zerg Ultralisk)
	train(4, Zerg Ultralisk)
	attack_add(1, Zerg Ultralisk)
	train(5, Zerg Ultralisk)
	attack_add(1, Zerg Ultralisk)
	train(6, Zerg Ultralisk)
	attack_add(1, Zerg Ultralisk)
	train(7, Zerg Ultralisk)
	attack_add(1, Zerg Ultralisk)
	train(8, Zerg Ultralisk)
	attack_add(1, Zerg Ultralisk)
	train(9, Zerg Ultralisk)
	attack_add(1, Zerg Ultralisk)
	train(10, Zerg Ultralisk)
	attack_add(1, Zerg Ultralisk)
	train(11, Zerg Ultralisk)
	attack_add(1, Zerg Ultralisk)
	train(12, Zerg Ultralisk)
	attack_add(1, Zerg Ultralisk)
	train(13, Zerg Ultralisk)
	attack_add(1, Zerg Ultralisk)
	stop()	

	
		--t_scourge--
	if_owned(Zerg Greater Spire, t_scourge_a)
	if_owned(Zerg Spire, t_scourge_a)
	wait(25)
	goto(t_scourge)
		--t_scourge_a--
	place_guard(Zerg Scourge, 0)
	place_guard(Zerg Scourge, 1)
	place_guard(Zerg Scourge, 2)
	defenseuse_aa(1, Zerg Scourge)
	defenseuse_ag(1, Zerg Scourge)
	attack_add(2, Zerg Scourge)
	train(2, Zerg Scourge)
	attack_add(2, Zerg Scourge)
	stop()

		--t_muta--
	if_owned(Zerg Greater Spire, t_muta_a)
	if_owned(Zerg Spire, t_muta_a)
	wait(25)
	goto(t_muta)
		--t_muta_a--
	defensebuild_gg(1, Zerg Mutalisk)
	defensebuild_ga(1, Zerg Mutalisk)
	defenseuse_gg(1, Zerg Mutalisk)
	defenseuse_ga(1, Zerg Mutalisk)
	defenseuse_aa(1, Zerg Mutalisk)
	defenseuse_ag(1, Zerg Mutalisk)
	train(3, Zerg Mutalisk)
	attack_add(3, Zerg Mutalisk)
	train(6, Zerg Mutalisk)
	attack_add(3, Zerg Mutalisk)
	train(12, Zerg Mutalisk)
	attack_add(3, Zerg Mutalisk)
	train(18, Zerg Mutalisk)
	attack_add(3, Zerg Mutalisk)
	train(21, Zerg Mutalisk)
	attack_add(3, Zerg Mutalisk)
	train(24, Zerg Mutalisk)
	attack_add(3, Zerg Mutalisk)
	stop()
		--t_guardian--
	if_owned(Zerg Greater Spire, t_guardian_a)
	if_owned(Zerg Spire, t_guardian_a)
	wait(25)
	goto(t_guardian)
		--t_guardian_a--
	defensebuild_gg(1, Zerg Guardian)
	defensebuild_ga(1, Zerg Guardian)
	defenseuse_ga(1, Zerg Guardian)
	defenseuse_gg(1, Zerg Guardian)
	train(1, Zerg Guardian)
	attack_add(1, Zerg Guardian)
	train(2, Zerg Guardian)
	attack_add(1, Zerg Guardian)
	train(3, Zerg Guardian)
	attack_add(1, Zerg Guardian)
	train(4, Zerg Guardian)
	attack_add(1, Zerg Guardian)
	train(5, Zerg Guardian)
	attack_add(1, Zerg Guardian)
	train(6, Zerg Guardian)
	attack_add(1, Zerg Guardian)
	train(7, Zerg Guardian)
	attack_add(1, Zerg Guardian)
	train(8, Zerg Guardian)
	attack_add(1, Zerg Guardian)
	train(9, Zerg Guardian)
	attack_add(1, Zerg Guardian)
	train(10, Zerg Guardian)
	attack_add(1, Zerg Guardian)
	train(11, Zerg Guardian)
	attack_add(1, Zerg Guardian)
	train(12, Zerg Guardian)
	attack_add(1, Zerg Guardian)
	stop()
	
#Upgrades:
--r_evo_carapace--
	wait_build(1, Zerg Evolution Chamber)
	upgrade(1, Zerg Carapace, 80)
	wait(4500)
	wait_build(1, Zerg Lair)
	upgrade(2, Zerg Carapace, 80)
	wait(5000)
	wait_build(1, Zerg Hive)
	upgrade(3, Zerg Carapace, 80)
	stop()
	
--r_evo_melee--
	wait_build(2, Zerg Evolution Chamber)
	upgrade(1, Zerg Melee Attacks, 80)
	wait(4500)
	wait_build(1, Zerg Lair)
	upgrade(2, Zerg Melee Attacks, 80)
	wait(5000)
	wait_build(1, Zerg Hive)
	upgrade(3, Zerg Melee Attacks, 80)
	stop()
	
--r_evo_missile--
	wait_build(1, Zerg Evolution Chamber)
	upgrade(1, Zerg Missile Attacks, 80)
	wait(4500)
	wait_build(1, Zerg Lair)
	upgrade(2, Zerg Missile Attacks, 80)
	wait(5000)
	wait_build(1, Zerg Hive)
	upgrade(3, Zerg Missile Attacks, 80)
	stop()	

--r_spire_armor--
	wait_build(1, Zerg Spire)
	upgrade(1, Zerg Flyer Carapace, 80)
	wait(4500)
	upgrade(2, Zerg Flyer Carapace, 80)
	wait(5500)
	wait_build(1, Zerg Hive)
	upgrade(3, Zerg Flyer Carapace, 80)
	stop()
	
--r_ultraups--
	wait_build(1, Zerg Ultralisk Cavern)
	race_jump(ultraupsa, ultraupsb, ultraupsa)
		--ultraupsa--
	upgrade(1, Chitinous Plating, 80)
	wait(3000)
	upgrade(1, Anabolic Synthesis, 80)
	stop()
		--ultraupsb--
	upgrade(1, Anabolic Synthesis, 80)
	wait(3000)
	upgrade(1, Chitinous Plating, 80)
	stop()
	
--r_defilerups--
	wait_build(1, Zerg Defiler Mound)
	upgrade(1, Metasynaptic Node, 80)
	wait(3000)
	tech(Burrowing, 80)
	tech(Plague, 80)
	stop()
	
--r_cracklingup--
	wait_build(1, Zerg Hive)
	upgrade(1, Adrenal Glands, 80)
	stop()
		
--r_hydradentech--
	wait_build(1, Zerg Hydralisk Den)
	upgrade(1, Muscular Augments, 80)
	wait(2000)
	upgrade(1, Grooved Spines, 80)
	enemyowns_jump(Terran Command Center, halt)
	wait(2000)
	tech(Lurker Aspect, 50)
	stop()
		
--r_overlordups--
	wait_build(1, Zerg Lair)
	upgrade(1, Pneumatized Carapace, 70)
	wait_build(1, Zerg Hive)
	upgrade(1, Ventral Sacs, 70)
	wait(2400)
	check_transports()
	stop()
	
#Drone production & Extractor:

--b_drones_a--
	wait_build(1, Zerg Hatchery)
	place_guard(Zerg Drone, 0)
	build(1, Zerg Drone, 80)
	wait_buildstart(1, Zerg Drone)
	build(2, Zerg Drone, 80)
	wait_buildstart(2, Zerg Drone)
	build(3, Zerg Drone, 80)
	wait_buildstart(3, Zerg Drone)
	build(4, Zerg Drone, 80)
	wait_buildstart(4, Zerg Drone)
	build(5, Zerg Drone, 80)
	wait_buildstart(5, Zerg Drone)
	build(6, Zerg Drone, 80)
	wait_buildstart(6, Zerg Drone)
	build(7, Zerg Drone, 80)
	wait_buildstart(7, Zerg Drone)
	build(8, Zerg Drone, 80)
	wait_buildstart(8, Zerg Drone)
	build(9, Zerg Drone, 80)
	wait_buildstart(9, Zerg Drone)
	build(10, Zerg Drone, 80)
	wait_buildstart(10, Zerg Drone)
	build(11, Zerg Drone, 80)
	wait_buildstart(11, Zerg Drone)
	build(12, Zerg Drone, 80)
	wait_buildstart(12, Zerg Drone)
	stop()
	
--b_extractor--
	wait_build(1, Zerg Hatchery)
	build(1, Zerg Extractor, 80)
	stop()	
#======================================
--fast_expo_loop--
	expand(99, zvt_expo_build)	
	wait(4000)
	goto(fast_expo_loop)
	
--expo_loop--
	expand(99, zvt_expo_build)
	wait(7000)
	goto(expo_loop)
	
--zvp_expo_loop--
	expand(99, zvp_expo_build)
	wait(6000)
	goto(zvp_expo_loop)
	
#ZvT Expansion:
--zvt_nat_expo_build--
	start_town()
	build(1, Zerg Hatchery, 90)
	wait_build(1, Zerg Hatchery)
	get_oldpeons(7)
	multirun(b_drones_a)
	multirun(heavy_expo_defense)
	wait_buildstart(1, Zerg Sunken Colony)
	multirun(b_extractor)
	stop()
	
--zvt_expo_build--
	start_town()
	build(1, Zerg Hatchery, 90)
	wait_build(1, Zerg Hatchery)
	multirun(b_drones_a)
	multirun(b_extractor)
	multirun(light_expo_defense)
	stop()
	
#ZvP Expansion:	
--zvp_nat_expo_build--
	start_town()
	build(1, Zerg Hatchery, 90)
	wait_build(1, Zerg Hatchery)
	multirun(light_expo_defense)
	get_oldpeons(6)
	multirun(b_drones_a)
	wait_build(6, Zerg Drone)
	multirun(b_extractor)
	stop()
	
--zvp_expo_build--
	start_town()
	build(1, Zerg Hatchery, 90)
	wait_build(1, Zerg Hatchery)
	multirun(light_expo_defense)
	multirun(b_drones_a)
	multirun(b_extractor)
	--b_hatchery--
	resources_jump(900, 0, b_hatchery_a)
	wait(300)
	goto(b_hatchery)
		--b_hatchery_a--
	build(2, Zerg Hatchery, 80)
	wait_build(2, Zerg Hatchery)
	stop()
	
#Expansion Defense:

--heavy_expo_defense--
	enemyowns_jump(Terran Starport, spore_first)
	enemyowns_jump(Protoss Stargate, spore_first)
	enemyowns_jump(Terran Academy, sunken_first)
	enemyowns_jump(Terran Vulture, sunken_first)
	enemyowns_jump(Protoss Citadel of Adun, sunken_first)
	wait(300)
	goto(heavy_expo_defense)
		--spore_first--
	player_need(1, Zerg Evolution Chamber)
	creep(4)
	build(1, Zerg Creep Colony, 90)
	wait_build(1, Zerg Creep Colony)
	build(1, Zerg Spore Colony, 90)
	wait(200)
	build(2, Zerg Creep Colony, 80)
	wait_build(2, Zerg Creep Colony)
	build(1, Zerg Sunken Colony, 80)
	wait_buildstart(1, Zerg Sunken Colony)
	wait(200)
	build(3, Zerg Creep Colony, 80)
	wait_build(3, Zerg Creep Colony)
	build(2, Zerg Sunken Colony, 80)
	wait_buildstart(1, Zerg Sunken Colony)
	wait(200)
	build(4, Zerg Creep Colony, 80)
	wait_build(4, Zerg Creep Colony)
	build(3, Zerg Sunken Colony, 80)
	wait_buildstart(1, Zerg Sunken Colony)
	wait(200)
	build(5, Zerg Creep Colony, 80)
	wait_build(5, Zerg Creep Colony)
	build(4, Zerg Sunken Colony, 80)
	wait_buildstart(1, Zerg Sunken Colony)
	wait(200)
	build(6, Zerg Creep Colony, 80)
	wait_build(6, Zerg Creep Colony)
	build(5, Zerg Sunken Colony, 80)
	wait_buildstart(1, Zerg Sunken Colony)
	stop()
		--sunken_first--
	creep(3)
	build(3, Zerg Creep Colony, 90)
	wait_build(1, Zerg Creep Colony)
	build(3, Zerg Sunken Colony, 90)
	wait_buildstart(3, Zerg Sunken Colony)
	wait(200)
	build(4, Zerg Creep Colony, 80)
	wait_build(4, Zerg Creep Colony)
	build(4, Zerg Sunken Colony, 80)
	wait_buildstart(4, Zerg Sunken Colony)
	wait(200)
	build(5, Zerg Creep Colony, 80)
	wait_build(5, Zerg Creep Colony)
	build(5, Zerg Sunken Colony, 80)
	wait_buildstart(5, Zerg Sunken Colony)
		--late_spores--
	enemyowns_jump(Terran Starport, late_spores_build)
	enemyowns_jump(Protoss Stargate, late_spores_build)
	wait(300)
	goto(late_spores)
		--late_spores_build--
	creep(4)
	build(6, Zerg Creep Colony, 80)
	wait_build(6, Zerg Creep Colony)
	build(1, Zerg Spore Colony, 80)
	wait_buildstart(1, Zerg Spore Colony)
	wait(200)
	creep(4)
	build(7, Zerg Creep Colony, 80)
	wait_build(7, Zerg Creep Colony)
	build(2, Zerg Spore Colony, 80)
	wait_buildstart(2, Zerg Spore Colony)
	stop()
	
--light_expo_defense--
	enemyowns_jump(Terran Starport, light_spore_first)
	enemyowns_jump(Protoss Stargate, light_spore_first)
	enemyowns_jump(Terran Academy, light_sunken_first)
	enemyowns_jump(Terran Vulture, light_sunken_first)
	enemyowns_jump(Protoss Citadel of Adun, light_sunken_first)
	enemyowns_jump(Protoss Gateway, light_sunken_first)
	wait(300)
	goto(light_expo_defense)
		--light_spore_first--
	build(1, Zerg Creep Colony, 80)
	wait_build(1, Zerg Creep Colony)
	build(1, Zerg Spore Colony, 80)
	wait_build(1, Zerg Spore Colony)
	wait(200)
	build(2, Zerg Creep Colony, 80)
	wait_build(2, Zerg Creep Colony)
	build(1, Zerg Sunken Colony, 80)
	wait(200)
	build(3, Zerg Creep Colony, 80)
	wait_build(3, Zerg Creep Colony)
	build(2, Zerg Sunken Colony, 80)
	stop()
		--light_sunken_first--
	build(1, Zerg Creep Colony, 80)
	wait_build(1, Zerg Creep Colony)
	build(1, Zerg Sunken Colony, 80)
	wait_build(1, Zerg Sunken Colony)
	wait(200)
	build(2, Zerg Creep Colony, 80)
	wait_build(2, Zerg Creep Colony)
	build(2, Zerg Sunken Colony, 80)
	wait_build(2, Zerg Sunken Colony)
	wait(200)
	build(3, Zerg Creep Colony, 80)
	wait_build(3, Zerg Creep Colony)
	build(1, Zerg Spore Colony, 80)
	stop()

#Main base Spore Colony:
--main_spore--
	enemyowns_jump(Terran Starport, main_spore_a)
	enemyowns_jump(Protoss Stargate, main_spore_a)
	wait(300)
	goto(main_spore)
		--main_spore_a--
	build(1, Zerg Evolution Chamber, 80)
	wait_build(1, Zerg Evolution Chamber)
	creep(4)
	build(1, Zerg Creep Colony, 80)
	wait_build(1, Zerg Creep Colony)
	build(1, Zerg Spore Colony, 80)
	wait_build(1, Zerg Spore Colony)
	wait(200)
	build(2, Zerg Creep Colony, 70)
	wait_build(2, Zerg Creep Colony)
	build(2, Zerg Spore Colony, 70)
	wait_build(2, Zerg Spore Colony)
	stop()
#Misc:

--kill--
	clear_combatdata()
	attack_prepare()
	wait(25)
	clear_combatdata()
	attack_do()
	wait(25)
	attack_clear()
	stop()
	
--give_the_money--
	wait(100)
	#resources_jump(400, 400, give_the_money)
	#give_money()
	wait(100)
	goto(give_the_money)
	
--halt--
	stop()