# Unleaded Zerg 3.3
# Courtesy of Broodwarai.com
# Island Scripts belong to Myk
ZMCx(1344, 101, aiscript):
	start_town()
	transports_off()
	farms_notiming()
	groundmap_jump(ground)
	goto(myki:start)
		--ground--
#if DEBUG
	debug(null, Unleaded Z 3.3 by Archon_Wing)
	--null--
#endif
	define_max(40, zergling)
	define_max(40, hydralisk)
	define_max(18, mutalisk)
	define_max(10, ultralisk)
	define_max(2, defiler)
	define_max(4, scourge)
	build(1, hatchery, 150)
	wait_build(1, hatchery)
	build(1, overlord, 130)
	build(5, drone, 80)
	wait_buildstart(5, drone)
	multirun(transport)
	random_jump(32, 5_pool)
	build(9, drone, 80)
	wait_buildstart(9, drone)
	
	race_jump(zvt, zvz, zvp)
	
#==================Zerg vs Protoss===========================
	--zvp--
	rush(0, zvp overpool)
	#random_jump(168, zvp overpool)
	define_max(12, drone)
	#12 hatch
	build(12, drone, 80)
	wait_buildstart(12, drone)
	wait(100)
	multirun(antiair)
	multirun(ling_speed)
	rush(0, 12 hatch vs non fe)
	multirun(ling harass)
	#dbl expo
	expand(1, low defense expo)
	define_max(11, drone)
	wait(100)
	build(1, spawning_pool, 80)
	wait_buildstart(1, spawning_pool)
	define_max(60, drone)
	wait_build(1, spawning_pool)
	define_max(6, zergling)
	defenseuse_gg(1, zergling)
	defenseuse_ga(1, zergling)
	train(4, zergling)
	goto(vs fe 3)
	
	--12 hatch vs non fe--
	expand(1, natural)
	define_max(11, drone)
	build(1, spawning_pool, 80)
	wait_buildstart(1, spawning_pool)
	define_max(60, drone)
	wait_build(1, spawning_pool)
	defenseuse_gg(1, zergling)
	defenseuse_ga(1, zergling)
	defensebuild_gg(1, zergling)
	train(10, zergling)
	farms_timing()
	multirun(gas)
	multirun(evo_ups)
	goto(vs non fe 2)
	

	
	
	--zvp overpool--
	build(2, overlord, 80)
	wait_buildstart(2, overlord)
	build(1, spawning_pool, 80)
	place_guard(drone, 0)

	#5 pool links here
	--zvp start--
	multirun(ling_speed)
	define_max(10, drone)
	build(10, drone, 80)
	wait_build(1, spawning_pool)
	defenseuse_gg(1, zergling)
	defenseuse_ga(1, zergling)
	defensebuild_gg(1, zergling)

	multirun(antiair)
	rush(0, vs non fe)
	goto(vs fe)
	--vs non fe--

	multirun(evo_ups)
	train(6, zergling)
	expand(1, natural)
	train(10, zergling)
	--vs non fe 2--
	farms_timing()
	define_max(60, drone)

	multirun(early rush)

	build(2, hatchery, 80)
	wait_buildstart(2, hatchery)

	multirun(tech)
	multirun(more hatch)
	train(12, zergling)
	enemyowns_jump(assimilator, continue)
	train(16, zergling)
	--continue--	
	build(1, extractor, 95)
	multirun(drone)

	wait_build(1, extractor)
	wait(150)
	build(1, hydralisk_den, 90)
	wait_build(1, hydralisk_den)

	defenseuse_gg(1, hydralisk)
	defenseuse_ga(1, hydralisk)
	defenseuse_ag(1, hydralisk)
	defenseuse_aa(1, hydralisk)
	defensebuild_gg(1, hydralisk)
	defensebuild_ga(1, hydralisk)



	train(5, hydralisk)
	multirun(zvp hydra up)
	expand(2, expo)



	train(10, hydralisk)
	multirun(train hydra)


	--continue 2--
	build(1, lair, 90)
	wait_build(1, lair)
	define_max(40, zergling)
	train(24, hydralisk)
	multirun(more expo)
	multirun(spire)

	
	--loop--
	multirun(train lings)
	multirun(train hydra)
	attack_add(24, zergling)
	attack_add(30, hydralisk)
	multirun(mutas)
	wait(5)
	multirun(add lurker)

	wait(2100)
	attack_prepare()
	attack_do()
	multirun(train lings)
	multirun(train hydra)
	attack_clear()
	clear_combatdata()
	notowns_jump(hive, loop)
	--final--
	multirun(train lings)
	multirun(train hydra)
	attack_add(30, zergling)
	attack_add(40, hydralisk)
	multirun(mutas)
	wait(5)
	kill_thread()
	multirun(add lurker)
	multirun(add scourge)
	wait(5)
	multirun(add ultra)
	wait(3000)
	attack_prepare()
	attack_do()
	multirun(train lings)
	multirun(train hydra)
	attack_clear()
	clear_combatdata()
	goto(final)
	
	--train lings--
	train(30, zergling)
	wait(3000)
	wait_force(30, zergling)
	train(40, zergling)
	wait_force(40, zergling)

	stop()
	
	--train hydra--
	notowns_jump(hydralisk_den, terminate)
	train(40, hydralisk)
	wait(3000)
	wait_force(40, hydralisk)
	stop()
	
	--add scourge--
	notowns_jump(scourge, terminate)
	attack_add(2, scourge)
	stop()
	
	
	--add lurker--
	notowns_jump(lurker, terminate)
	attack_add(2, lurker)
	--terminate--
	stop()
	
	--mutas--
	notowns_jump(spire, terminate)
	train(12, mutalisk)
	wait(2000)
	wait_force(12, mutalisk)
	stop()
	
	--add ultra--
	notowns_jump(ultralisk_cavern, terminate)
	attack_add(7, ultralisk)
	train(3, ultralisk)
	stop()
	
	

	
	--early rush--
	wait_train(12, zergling)
	wait(400)
	rush(5, terminate)
	send_suicide(0)
	wait(500)
	clear_combatdata()
	rush(5, terminate)
	train(12, zergling)
	wait_force(12, zergling)
	send_suicide(0)
	stop()
#Expansion crap	
			--natural--
	start_town()
	defenseuse_gg(2, zergling)
	defenseuse_ga(2, zergling)
	build(1, hatchery, 105)
	wait_build(1, hatchery)
	get_oldpeons(2)
	multirun(natural defenses)
	multirun(expo stuff)
	


	wait(5000)
	multirun(gas)


	multirun(replace)

	stop()
	

	
			--expo--
	start_town()

	build(1, hatchery, 120)
	wait_build(1, hatchery)
	build(1, drone, 65)
	multirun(transfer)
	multirun(defenses)
	multirun(gas)
	build(3, drone, 55)
	wait_buildstart(3, drone)
	build(6, drone, 55)
	wait_buildstart(6, drone)
	build(9, drone, 55)
	wait_buildstart(9, drone)
	build(12, drone, 35)
	wait_buildstart(12, drone)
	wait(8300)
	build(2, hatchery, 15)
	stop()
	
	--transfer--
	get_oldpeons(3)
	stop()
	


	
	--gas--
	build(1, extractor, 35)
	stop()
	
		--expo stuff--
	wait_build(1, hatchery)
	build(3, drone, 80)


	wait_buildstart(3, drone)
	build(6, drone, 70)
	wait_buildstart(6, drone)
	build(9, drone, 70)
	wait_buildstart(9, drone)

	build(12, drone, 55)
	wait_buildstart(12, drone)

	build(14, drone, 55)
	wait_buildstart(14, drone)
	build(17, drone, 35)
	wait_buildstart(17, drone)
	wait(2000)
	build(2, hatchery, 25)

	stop()
	
		--replace--
	wait_train(1, hydralisk)
	player_need(1, hydralisk_den)
	stop()
	#====Standard Defense for natural expos
	--wait nat defenses--
	wait(300)
	goto(mass sunk t 2)
	
	
			--natural defenses--
	
	race_jump(mass sunk t, defenses, mass sunk p)
	--mass sunk t--
	creep(3)
	build(1, creep_colony, 90)
	wait_build(1, creep_colony)
	--mass sunk t 2--
	notowns_jump(spawning_pool, wait nat defenses)
	player_need(1, spawning_pool)
	creep(3)
	build(2, creep_colony, 90)
	defenseuse_gg(1, zergling)
	build(1, sunken_colony, 90)
	creep(3)
	build(3, creep_colony, 90)

	wait_build(2, creep_colony)
	build(2, sunken_colony, 90)
	creep(3)
	enemyowns_jump(factory, slow sunkens)
	--mass sunk t 3--
	wait_buildstart(3, creep_colony)
	build(4, creep_colony, 90)
	wait_build(3, creep_colony)


	enemyowns_jump(starport, early spore)
	build(3, sunken_colony, 90)
	wait_build(4, creep_colony)
	build(4, sunken_colony, 90)
	build(5, creep_colony, 90)
	wait_build(5, creep_colony)

	build(5, sunken_colony, 90)
	#enemyowns_jump(starport, expo spore)
	build(6, creep_colony, 50)
	wait_build(6, creep_colony)
	build(6, sunken_colony, 50)
	--zvt spore--
	enemyowns_jump(starport, zvt spore 2)
	wait(500)
	goto(zvt spore)
	
	--zvt spore 2--
	build(7, creep_colony, 60)
	wait_build(7, creep_colony)
	notowns_jump(evolution_chamber, zvt spore wait)
	build(1, spore_colony, 60)
	stop()
	
	--zvt spore wait--
	wait(200)
	goto(zvt spore 2)
	
	--slow sunkens--
	wait(3000)
	goto(mass sunk t 3)
	
	
	

	
	
	--mass sunk p--
	creep(3)
	build(1, creep_colony, 90)
	wait_buildstart(1, creep_colony)
	creep(3)
	build(2, creep_colony, 90)
	player_need(1, spawning_pool)
	defenseuse_gg(1, zergling)
	wait_build(1, creep_colony)
	build(1, sunken_colony, 90)
	wait_build(2, creep_colony)
	build(2, sunken_colony, 90)
	creep(3)
	build(3, creep_colony, 90)
	wait_build(3, creep_colony)
	build(3, sunken_colony, 90)
	wait_buildstart(3, sunken_colony)
	enemyowns_jump(stargate, early spore)

	build(4, creep_colony, 80)
	wait_build(4, creep_colony)

	build(4, sunken_colony, 80)
	wait_build(4, sunken_colony)
	wait(1000)
	#build(5, creep_colony, 50)
	#wait_build(5, creep_colony)
	#build(5, sunken_colony, 50)
	
	
	--natural antiair--
	enemyowns_jump(starport, expo spore)
	enemyowns_jump(stargate, expo spore)
	wait(1000)
	goto(natural antiair)
	
	--expo spore--
	notowns_jump(evolution_chamber, wait expo spore)
	build(5, creep_colony, 70)
	wait_build(5, creep_colony)
	build(1, spore_colony, 70)
	stop()
	
	--wait expo spore--
	wait(300)
	goto(expo spore)
	
	--early spore--
	notowns_jump(evolution_chamber, wait early spore 2)
	build(1, spore_colony, 80)
	wait_build(1, spore_colony)
	wait(1000)
	build(3, sunken_colony, 70)
	build(5, creep_colony, 40)
	wait_build(5, creep_colony)
	build(4, sunken_colony, 40)
	stop()
	
	--wait early spore 2--
	wait(300)
	goto(early spore)
	
	#===Outer expo D
			--defenses--
	player_need(1, spawning_pool)
	wait_build(1, drone)
	build(1, creep_colony, 60)
	wait_build(1, creep_colony)
	build(1, sunken_colony, 60)
	race_jump(spore, zvz d, spore)


	


#spores at expo



		--spore--

	notowns_jump(evolution_chamber, wait evo)
	player_need(1, evolution_chamber)
	
	build(2, creep_colony, 30)
	wait_build(2, creep_colony)
	build(1, spore_colony, 30)
	build(3, creep_colony, 30)
	wait_build(3, creep_colony)
	build(2, sunken_colony, 30)
	stop()
	
	--wait evo--
	wait(600)
	goto(spore)


		--zvz d--
	wait_build(1, creep_colony)
	build(1, sunken_colony, 80)
	wait_build(1, sunken_colony)

		--zvz antiair--
	enemyowns_jump(spire, zvz spore)
	wait(400)

	goto(zvz antiair)


		--zvz spore--
	multirun(scourge)
	player_need(1, evolution_chamber)
	creep(4)
	build(3, creep_colony, 80)
	wait_build(3, creep_colony)
	build(2, spore_colony, 80)
	stop()
	

	
	--ling_speed--
	wait_build(1, extractor)
	wait(550)
	enemyowns_jump(overlord, faster ling speed)
	wait(550)
	wait_train(12, zergling)
	--faster ling speed--
	upgrade(1, zergling_speed, 80)
	wait(2000)
	wait_build(1, hive)
	upgrade(1, zergling_attack, 80)
	stop()
	
		--transport--
	wait_build(1, hive)
	wait(1000)
	groundmap_jump(transport)
	upgrade(1, overlord_capacity, 80)
	wait(2000)
	check_transports()
	stop()
	--zvp hydra up--
	wait_train(6, hydralisk)
	upgrade(1, hydralisk_range, 80)
	wait(2000)
	upgrade(1, hydralisk_speed, 80)
	wait_build(1, lair)
	wait(3000)
	tech(lurker_aspect, 80)
	wait(2000)
	defenseuse_gg(1, lurker)
	defenseuse_ga(1, lurker)
	place_guard(lurker, 0)
	wait(450)
	place_guard(lurker, 1)
	stop()
	
	--antiair--
	enemyowns_jump(stargate, need den )
	enemyowns_jump(starport, need den )
	enemyowns_jump(spire, need den )
	wait(600)
	goto(antiair)
	--need den --
	multirun(scourge)
	build(1, extractor, 80)
	multirun(main spore)
	wait_build(1, extractor)
	build(1, hydralisk_den, 80)
	wait_build(1, hydralisk_den)
	defenseuse_aa(1, hydralisk)
	defenseuse_ag(1, hydralisk)
	defenseuse_gg(1, hydralisk)
	defenseuse_ga(1, hydralisk)
	train(3, hydralisk)
	train(5, hydralisk)
	wait(2000)
	train(6, hydralisk)
	defensebuild_ag(1, hydralisk)
	defensebuild_aa(1, hydralisk)
	stop()
	--scourge--
	wait_build(1, spire)
	place_guard(scourge, 0)
	defenseuse_aa(1, scourge)
	defenseuse_ag(1, scourge)
	wait(100)
	place_guard(scourge, 1)
	stop()
	
	--main spore--
	build(1, creep_colony, 80)
	build(1, evolution_chamber, 80)
	wait_build(1, creep_colony)
	wait_build(1, evolution_chamber)
	build(1, spore_colony, 80)
	stop()
	
			--evo_ups--
	wait_buildstart(1, lair)
	race_jump(t up, z up, p up)
	
	--t up--
	wait_build(1, evolution_chamber)

	upgrade(1, z_carapace, 80)
	multirun(t 2nd evo)
	wait(4500)
	upgrade(2, z_carapace, 80)
	wait(4800)
	wait_build(1, hive)
	upgrade(3, z_carapace, 80)
	stop()
	--t 2nd evo--
	wait(1000)
	build(2, evolution_chamber, 80)
	wait_build(2, evolution_chamber)
	upgrade(1, z_missile_attack, 80)
	wait(4500)
	upgrade(1, z_melee_attack, 80)
	wait(4500)
	upgrade(2, z_missile_attack, 80)
	wait(4800)
	wait_build(1, hive)
	upgrade(3, z_missile_attack, 80)
	wait(5000)
	upgrade(2, z_melee_attack, 80)

	wait(5000)
	upgrade(3, z_melee_attack, 60)
	stop()

	









	


	--p up--
	build(1, evolution_chamber, 70)
	wait_build(1, evolution_chamber)

	upgrade(1, z_carapace, 80)
	wait(4500)
	upgrade(2, z_carapace, 80)
	wait(4500)
	build(2, evolution_chamber, 80)
	wait_build(2, evolution_chamber)
	upgrade(1, z_melee_attack, 80)
	upgrade(1, z_missile_attack, 80)
	wait(5000)
	wait_build(1, hive)
	upgrade(3, z_carapace, 80)
	upgrade(2, z_melee_attack, 80)



	wait(5200)
	upgrade(3, z_melee_attack, 80)

	upgrade(2, z_missile_attack, 45)
	wait(5000)
	upgrade(3, z_missile_attack, 40)
	stop()
	
	--z up--
	wait(4000)
	build(1, evolution_chamber, 80)
	wait_build(1, evolution_chamber)

	upgrade(1, z_carapace, 60)
	wait(4800)
	wait_build(1, lair)

	upgrade(2, z_carapace, 60)
	wait(5000)
	wait_build(1, hive)
	upgrade(3, z_carapace, 60)
	wait(5000)
	upgrade(1, z_melee_attack, 60)
	wait(5000)
	upgrade(2, z_melee_attack, 60)
	wait(5000)
	upgrade(3, z_melee_attack, 60)	
	stop()
	
	--5 hatch hydra evo ups--
	build(1, evolution_chamber, 70)
	wait_build(1, evolution_chamber)
	upgrade(1, z_missile_attack, 70)
	wait(4500)
	upgrade(2, z_missile_attack, 70)
	wait(4200)
	build(2, evolution_chamber, 70)
	wait_build(2, evolution_chamber)

	upgrade(1, z_carapace, 70)
	upgrade(1, z_melee_attack, 70)
	wait(4500)
	wait_build(1, hive)
	upgrade(3, z_missile_attack, 70)
	upgrade(2, z_carapace, 70)
	wait(5000)
	upgrade(3, z_carapace, 60)
	upgrade(2, z_melee_attack, 60)



	wait(5200)
	upgrade(3, z_melee_attack, 45)


	stop()
	--tech--
	wait_build(1, extractor)
	wait(2500)
	build(1, lair, 80)
	wait_build(1, lair)
	--tech 2--
	enemyowns_jump(dark_templar, ol speed)
	enemyowns_jump(control_tower, ol speed)
	wait(2000)
	enemyowns_jump(dark_templar, ol speed)
	enemyowns_jump(control_tower, ol speed)
	enemyowns_jump(lurker, ol speed)
	wait(2000)
	--ol speed--
	upgrade(1, overlord_speed, 80)

	wait(2700)
		--need hive--
	time_jump(30, get hive)
	resources_jump(1000, 800, get hive)
	wait(1000)
	goto(need hive)
	
	--get hive--
	build(1, queen_nest, 80)
	wait_build(1, queen_nest)
	build(1, hive, 80)
	wait_build(1, hive)

	race_jump(defiler, zvz tech, defiler)	
			--defiler--
	build(1, defiler_mound, 90)
	wait_build(1, defiler_mound)
	place_guard(defiler, 0)
	upgrade(1, defiler_mana, 80)
	wait(3000)
	tech(plague, 80)
	race_jump(ultra tech, terminate, ultra tech)
	--ultra tech--
	resources_jump(1200, 1200, get ultra tech)
	wait(1000)
	goto(ultra tech)
	--get ultra tech--
	
	build(1, ultralisk_cavern, 100)
	wait_build(1, ultralisk_cavern)
	defensebuild_gg(1, ultralisk)
	defenseuse_gg(1, ultralisk)
	defenseuse_ga(1, ultralisk)
	upgrade(1, ultralisk_armor, 80)
	wait(3000)
	upgrade(1, ultralisk_speed, 80)
	stop()
	--zvz tech--
	build(1, greater_spire, 80)
	wait(10000)
	goto(defiler)
	
	
	
	--more expo--
	wait(4000)
	expand(99, expo)
	wait(5100)
	goto(more expo)
	
	--spire--
	wait_build(1, lair)
	build(1, spire, 90)
	wait_build(1, spire)
	defenseuse_gg(1, mutalisk)
	defenseuse_ag(1, mutalisk)
	defenseuse_ga(1, mutalisk)
	defenseuse_aa(1, mutalisk)
	wait_train(10, mutalisk)
	upgrade(1, z_flyer_carapace, 70)
    wait(4500)
	upgrade(2, z_flyer_carapace, 60)
	wait(4800)
	wait_build(1, greater_spire)

    upgrade(3, z_flyer_carapace, 60)
	wait(5000)

    upgrade(1, z_flyer_attack, 30)
    wait(4500)



    upgrade(2, z_flyer_attack, 30)
    wait(4800)

	wait(5000)
	upgrade(3, z_flyer_carapace, 30)
	stop()
	
	--drone--
	build(12, drone, 70)
	wait_buildstart(12, drone)
	build(14, drone, 70)
	wait_build(14, drone)
	build(16, drone, 70)
	stop()
	
	--more hatch--
	resources_jump(1000, 0, build more hatch)
	wait(1000)
	goto(more hatch)
	
	--build more hatch--
	build(3, hatchery, 60)
	build(1, extractor, 60)
	wait(3500)
	
	--more hatch 2--
	resources_jump(2000, 0, build more hatch 2)
	wait(2400)
	goto(more hatch 2)
	
	--build more hatch 2--
	build(4, hatchery, 75)
	wait_build(4, hatchery)
	
		--more hatch 3--
	resources_jump(2400, 0, build more hatch 3)
	wait(2400)
	goto(more hatch 3)
	
	--build more hatch 3--
	build(5, hatchery, 70)
	stop()
	
	
	



###--- Vs Protoss FE
--vs fe--
enemyowns_jump(forge, vs fe 2)
attack_add(6, zergling)
attack_do()
attack_clear()
attack_add(6, zergling)
attack_do()
attack_clear()

--vs fe 2--
define_max(8, zergling)
multirun(ling pressure)
train(2, zergling)

expand(1, low defense expo)

define_max(60, drone)
build(13, drone, 80)
--vs fe 3--
random_jump(32, ling all in)
random_jump(32, 2 base break)
#Double Expand Builds
expand(2, low defense expo)
build(1, extractor, 80)

train(6, zergling)
defensebuild_gg(1, zergling)

farms_timing()
wait_build(1, extractor)
wait(1)
build(1, lair, 90)
multirun(tech)
multirun(more hatch)
define_max(12, zergling)
enemyowns_jump(stargate, spire first)
goto(5 hatch hydra)
--spire first--
wait_build(1, lair)
build(1, spire, 80)
wait_buildstart(1, spire)
--5 hatch hydra--

build(1, hydralisk_den, 100)
wait_buildstart(1, hydralisk_den)
defenseuse_gg(1, hydralisk)
defenseuse_ga(1, hydralisk)
defensebuild_gg(1, hydralisk)
defensebuild_ga(1, hydralisk)

train(12, hydralisk)

multirun(5 hatch hydra evo ups)
multirun(more hatch)
multirun(zvp hydra up)
train(24, hydralisk)
define_max(40, zergling)
random_jump(45, 5 hatch push)
goto(5 hatch hydra 2)
--5 hatch push--
wait_force(24, hydralisk)
attack_add(24, hydralisk)
wait(300)
attack_prepare()
attack_do()
attack_clear()
clear_combatdata()
--5 hatch hydra 2--


train(36, hydralisk)
define_max(40, zergling)
multirun(spire)

random_jump(45, 5 hatch push 2)
--5 hatch push 2--
multirun(more expo)
attack_add(36, hydralisk)
wait_force(36, hydralisk)
attack_prepare()
attack_do()

attack_clear()
clear_combatdata()
goto(loop)













#2 base shit
--2 base break--
farms_timing()
build(2, hatchery, 90)
wait_buildstart(2, hatchery)
build(1, extractor, 90)
wait_buildstart(1, extractor)
#multirun(drone)
define_max(20, drone)
random_jump(224, 3 hatch hydra)
multirun(tech)
multirun(spire)
define_max(30, drone)
#3 hatch muta
build(1, lair, 80)
wait(300)

define_max(12, zergling)
#also anti mech
--3 hatch muta--
wait_buildstart(1, spire)
multirun(olz)

wait_build(1, spire)
multirun(more expo)
multirun(mutas)
wait(5)

random_jump(128, no muta attack)
attack_add(12, mutalisk)
attack_add(12, zergling)

wait(500)
attack_prepare()
define_max(60, drone)
attack_do()

--no muta attack--
attack_clear()
clear_combatdata()
build(1, hydralisk_den, 80)
define_max(40, zergling)
multirun(zvp hydra up)


wait_build(1, hydralisk_den)
defensebuild_gg(1, hydralisk)
defensebuild_ga(1, hydralisk)
defenseuse_gg(1, hydralisk)
defenseuse_ga(1, hydralisk)
multirun(5 hatch hydra evo ups)
wait(5)
multirun(more hatch)
goto(loop)



#3 hatch hydra
--3 hatch hydra--
build(1, hydralisk_den, 100)
wait_buildstart(1, hydralisk_den)


wait_build(1, hydralisk_den)

defenseuse_gg(1, hydralisk)
defenseuse_ga(1, hydralisk)
defenseuse_ag(1, hydralisk)
defenseuse_aa(1, hydralisk)
train(1, hydralisk)

	multirun(zvp hydra up)
	train(12, hydralisk)
	multirun(tech)
	wait(1)
	multirun(more expo)
	wait_force(15, hydralisk)
	send_suicide(0)
	


	build(2, hatchery, 80)
	multirun(more hatch)
	wait(1)
	multirun(spire)

	train(16, hydralisk)
	multirun(5 hatch hydra evo ups)
	defensebuild_gg(1, hydralisk)
	define_max(40, zergling)
	define_max(60, drone)
	goto(continue 2)
	

--ling all in--

build(2, hatchery, 90)
wait_buildstart(2, hatchery)
farms_timing()
define_max(40, zergling)
build(1, extractor, 90)
wait_buildstart(1, extractor)
multirun(tech)
train(16, zergling)
multirun(more hatch)
attack_add(16, zergling)
attack_do()
attack_clear()
define_max(60, drone)
multirun(evo_ups)
goto(continue)

--ling pressure--
wait_train(4, zergling)
rush(0, terminate)
enemyowns_jump(photon_cannon, terminate)
send_suicide(0)
wait_force(6, zergling)
send_suicide(0)
stop()


		--low defense expo--
	start_town()
	build(1, hatchery, 100)
	wait_build(1, hatchery)
	multirun(need 2nd gas)
	multirun(hatch 2)
	build(3, drone, 80)
	wait_buildstart(3, drone)
	multirun(low defenses)
	build(6, drone, 80)
	wait_build(6, drone)
	build(9, drone, 70)
	wait_buildstart(9, drone)

	build(12, drone, 50)
	wait_build(12, drone)
	build(14, drone, 60)
	wait_buildstart(14, drone)
	multirun(replace)
	stop()
		--hatch 2--
	wait(2600)
	build(2, hatchery, 70)
	stop()
		--low defenses--
	wait_build(1, drone)
	build(1, creep_colony, 75)
	wait_build(1, creep_colony)
	player_need(1, spawning_pool)
	build(1, sunken_colony, 75)
	enemyowns_jump(stargate, low defenses 3)

	build(2, creep_colony, 75)
	wait_build(2, creep_colony)
	build(2, sunken_colony, 75)
	enemyowns_jump(stargate, low defenses 4)

	build(3, creep_colony, 50)
	wait_build(3, creep_colony)
	build(3, sunken_colony, 50)
			--low defenses 3--
	notowns_jump(evolution_chamber, wait low defenses 3)
	build(2, creep_colony, 70)
	wait_build(2, creep_colony)
	build(1, spore_colony, 70)
	wait(500)
	build(3, creep_colony, 60)
	wait_build(3, creep_colony)
	build(2, sunken_colony, 60)
	build(4, creep_colony, 50)
	wait_build(4, creep_colony)
	build(3, sunken_colony, 50)
	stop()
	
	--wait low defenses 3--
	wait(300)
	goto(low defenses 3)
	
	--low defenses 4--
	notowns_jump(evolution_chamber, wait low defenses 4)
	build(3, creep_colony, 60)
	wait_build(3, creep_colony)
	build(1, spore_colony, 60)
	wait(1000)
	build(4, creep_colony, 40)
	wait_build(4, creep_colony)
	build(3, sunken_colony, 40)
	stop()
	
	--wait low defenses 4--
	wait(300)
	goto(low defenses 3)
	
	--need 2nd gas--
	wait(800)
	if_owned(lair, get 2nd gas)
	goto(need 2nd gas)
	
	--get 2nd gas--
	build(1, extractor, 70)
	stop()
	
	--olz--
	build(3, overlord, 80)
	wait_buildstart(3, overlord)
	build(4, overlord, 80)
	wait_buildstart(4, overlord)
	build(5, overlord, 80)
	wait_build(5, overlord)
	build(6, overlord, 80)
	stop()

	
#==================Zerg vs Terran===========================
--zvt--
build(2, overlord, 80)
wait_buildstart(2, overlord)
wait(400)
rush(0, zvt antirush)
multirun(ling pressure)

define_max(13, drone)

#12 hatch
build(12, drone, 80)
wait_buildstart(12, drone)
random_jump(24, zvt ling attack)
expand(1, zvt natural)
wait(100)
build(1, spawning_pool, 90)
wait_buildstart(1, spawning_pool)
build(13, drone, 80)
define_max(60, drone)
#3 hatch
wait_build(1, spawning_pool)
defenseuse_gg(1, zergling)
defenseuse_ga(1, zergling)
train(6, zergling)
--zvt2--
build(2, hatchery, 90)
wait_buildstart(2, hatchery)
farms_timing()
build(1, extractor, 90)
wait_build(1, extractor)
build(13, drone, 80)
wait_buildstart(13, drone)
goto(normal)

--zvt antirush--
build(12, drone, 80)
wait_buildstart(12, drone)
build(1, spawning_pool, 80)
wait_buildstart(1, spawning_pool)
wait(500)
define_max(10, drone)
expand(1, zvt natural)
define_max(11, drone)
wait_build(1, spawning_pool)
defenseuse_gg(1, zergling)
defenseuse_ga(1, zergling)

train(8, zergling)
build(2, hatchery, 80)
wait_buildstart(2, hatchery)
farms_timing()
define_max(60, drone)

goto(zvt2)



--zvt ling attack--
expand(1, low defense expo)
build(1, spawning_pool, 90)
build(13, drone, 80)
#3 hatch

define_max(13, drone)
wait_build(1, spawning_pool)
defenseuse_gg(1, zergling)
defenseuse_ga(1, zergling)

multirun(train lings)
farms_timing()
wait(1500)
send_suicide(0)
build(2, hatchery, 90)
build(1, extractor, 100)
define_max(60, drone)
goto(normal)




--12 lings--
train(12, zergling)
wait(2000)
wait_force(12, zergling)

wait_force(18, zergling)
stop()







--normal--
multirun(12 lings)
enemyowns_jump(factory, anti mech)
build(1, lair, 90)
wait_buildstart(1, lair)



wait(700)
build(1, hydralisk_den, 90)
wait_buildstart(1, hydralisk_den)
multirun(antiair)

wait_build(1, lair)
wait_build(1, hydralisk_den)


tech(lurker_aspect, 110)
multirun(t_lurker)
defenseuse_gg(2, lurker)
defenseuse_ga(2, lurker)
build(1, evolution_chamber, 95)
wait_build(1, evolution_chamber)
multirun(olz)


multirun(evo_ups)
train(1, lurker)
defenseuse_gg(1, hydralisk)
defenseuse_ga(1, hydralisk)
multirun(ling_speed)
train(3, lurker)

multirun(mass lurkers)
wait(15)
multirun(more hatch)
wait(15)

multirun(tech 2)
wait(15)
expand(99, expo)
train(24, zergling)
train(7, lurker)
attack_add(24, zergling)

attack_add(7, lurker)
attack_add(2, hydralisk)
multirun(zvt hydra up)
wait(5)
multirun(zvt spire)

wait(1000)
multirun(add scourge)

attack_prepare()
attack_do()
multirun(train lings)
attack_clear()
clear_combatdata()
train(11, lurker)
multirun(more expo)





--lurkerling loop--
multirun(train hydra)
attack_add(14, lurker)
attack_add(40, zergling)
attack_add(10, hydralisk)
wait(1800)
kill_thread()
multirun(add scourge)

attack_prepare()
attack_do()
multirun(train lings)
attack_clear()
clear_combatdata()
if_owned(hive, hive loop)
goto(lurkerling loop)




--hive loop--
multirun(train lings)
attack_add(18, lurker)
attack_add(40, zergling)
attack_add(25, hydralisk)
multirun(add defiler)
wait(5)
multirun(train hydra)
wait(5)
multirun(add guardian)
wait(3000)
multirun(add scourge)
attack_prepare()
attack_do()
attack_clear()
clear_combatdata()
goto(hive loop)

--t_lurker--
train(4, lurker)
wait_force(4, lurker)
stop()



--add defiler--
notowns_jump(defiler_mound, terminate)
attack_add(1, defiler)
stop()

--add guardian--
notowns_jump(greater_spire, terminate)
attack_add(2, guardian)
attack_add(1, devourer)
stop()



--mass lurkers--

train(2, lurker)
train(3, lurker)
train(4, lurker)
train(5, lurker)
wait_force(5, lurker)
train(6, lurker)
train(7, lurker)
train(8, lurker)
train(9, lurker)
train(10, lurker)
wait_force(10, lurker)
train(11, lurker)
train(12, lurker)
train(13, lurker)
train(14, lurker)
wait_force(14, lurker)
wait(1600)
train(17, lurker)
goto(mass lurkers)

		--zvt natural--
	start_town()
	build(1, hatchery, 105)
	wait_build(1, hatchery)
	get_oldpeons(2)
	multirun(natural defenses)
	build(3, drone, 75)
	wait_buildstart(3, drone)
	build(6, drone, 70)
	wait_buildstart(6, drone)
	multirun(gas)

	build(9, drone, 70)
	wait_buildstart(9, drone)
	build(12, drone, 70)
	wait_buildstart(12, drone)

	build(16, drone, 50)
	wait_buildstart(16, drone)
	multirun(replace)
	build(18, drone, 30)

	stop()
#for 2 hatch builds)














--zvt spire--
enemyowns_jump(science_facility, get zvt spire)
wait(1000)
goto(zvt spire)

--get zvt spire--
wait_build(1, lair)
build(1, spire, 80)
wait_build(1, spire)
wait_build(1, hive)
--zvt mutas--
resources_jump(2000, 900, get zvt mutas)
enemyowns_jump(physics_lab, get zvt mutas)
wait(1500)
goto(zvt mutas)
--get zvt mutas--
define_max(8, mutalisk)
define_max(5, guardian)
define_max(2, devourer)
wait_build(1, ultralisk_cavern)
wait(10000)
build(1, greater_spire, 80)
wait_build(1, greater_spire)
defenseuse_gg(1, mutalisk)
defenseuse_ga(1, mutalisk)
defenseuse_aa(1, mutalisk)
defenseuse_ag(1, mutalisk)
defenseuse_gg(1, guardian)
defenseuse_ga(1, guardian)
defenseuse_ag(1, devourer)
defenseuse_aa(1, devourer)
stop()

--zvt hydra up--
wait_train(12, hydralisk)
upgrade(1, hydralisk_range, 80)
wait(3000)
upgrade(1, hydralisk_speed, 80)
stop()

--anti mech--
define_max(12, zergling)
build(1, hydralisk_den, 90)
wait_buildstart(1, hydralisk_den)
build(1, lair, 90)
multirun(antiair)
wait(5)
multirun(spire)
wait_build(1, hydralisk_den)
defenseuse_gg(1, hydralisk)
defenseuse_ga(1, hydralisk)
train(2, hydralisk)
define_max(30, mutalisk)
multirun(vs mech stuff)
goto(3 hatch muta)

--vs mech stuff--
wait(6000)
multirun(ling_speed)
goto(tech 2)

#==========================Zerg vs Zerg
--zvz--
define_max(60, zergling)
define_max(8, scourge)
multirun(zvz harass)
--zvz rush watch--
rush(0, 9 pool zvz)
enemyresources_jump(170, 0, 9 pool zvz)
resources_jump(100, 0, zvz 2)
wait(50)
goto(zvz rush watch)
--zvz 2--
#Overpool
build(1, overlord, 80)
wait_build(1, overlord)
rush(0, overpool zvz)
random_jump(84, overpool zvz)
#12 hatch
define_max(12, drone)
build(12, drone, 80)
wait_buildstart(12, drone)
rush(0, 12 pool zvz)
wait(300)
expand(1, zvt natural)
wait(100)
build(1, spawning_pool, 80)
wait_buildstart(1, spawning_pool)
multirun(gas)
wait_build(1, spawning_pool)
define_max(60, drone)
defenseuse_gg(1, zergling)
defenseuse_ga(1, zergling)
train(6, zergling)
farms_timing()
goto(zvz continue)

--12 pool zvz--
build(1, spawning_pool, 80)
wait_buildstart(1, spawning_pool)
multirun(gas)
wait_build(1, spawning_pool)
define_max(60, drone)
defenseuse_gg(1, zergling)
defenseuse_ga(1, zergling)
train(6, zergling)
expand(1, zvt natural)
farms_timing()
goto(zvz continue)



--9 pool zvz--
build(1, spawning_pool, 190)
wait_buildstart(1, spawning_pool)
build(2, overlord, 80)
build(10, drone, 80)
defenseuse_gg(1, zergling)
defenseuse_ga(1, zergling)
train(8, zergling)

build(2, hatchery, 80)
farms_timing()
build(1, extractor, 90)
wait_buildstart(1, extractor)
goto(zvz continue)

--overpool zvz--

build(2, overlord, 80)
build(1, spawning_pool, 80)
wait_buildstart(1, spawning_pool)
multirun(gas)
build(10, drone, 80)
wait_build(1, spawning_pool)

defenseuse_gg(1, zergling)
defenseuse_ga(1, zergling)

train(6, zergling)
farms_timing()

--zvz continue--
multirun(zvz lings)
random_jump(64, mass lings)

build(1, lair, 110)
wait_buildstart(1, lair)
multirun(ling_speed)
multirun(defenses)
--zvz continue 3--
define_max(40, mutalisk)
define_max(60, drone)
multirun(spire)
wait(15)
multirun(zvz mutas)
wait(15)
wait_build(1, spire)
defensebuild_gg(1, mutalisk)
defensebuild_ag(1, mutalisk)
defensebuild_aa(1, mutalisk)
defensebuild_gg(1, zergling)
expand(1, zvt natural)
attack_add(5, mutalisk)
attack_add(12, zergling)
multirun(add scourge)
wait(500)
attack_prepare()
attack_do()
attack_clear()
clear_combatdata()
build(2, hatchery, 90)
multirun(more hatch)

multirun(evo_ups)
multirun(more expo)
--zvz loop--

attack_add(18, mutalisk)
attack_add(30, zergling)
wait(1800)
multirun(add scourge)
attack_prepare()
attack_do()
attack_clear()
clear_combatdata()
if_owned(greater_spire, zvz final)
goto(zvz loop)
--zvz final--

attack_add(30, mutalisk)
attack_add(30, zergling)
multirun(devourer guard)
wait(3000)
attack_prepare()
attack_do()
attack_clear()
goto(zvz final)

--devourer guard--
notowns_jump(greater_spire, terminate)
attack_add(3, guardian)
attack_add(3, devourer)
stop()

--mass lings--
multirun(ling_speed)
build(2, hatchery, 80)
attack_add(12, zergling)
wait(500)
attack_prepare()
expand(1, natural)
attack_do()
attack_clear()
attack_add(8, zergling)
clear_combatdata()
build(1, lair, 110)
attack_do()
attack_clear()
goto(zvz continue 3)

--ling harass--
wait_train(4, zergling)
rush(0, terminate)
wait_force(6, zergling)
send_suicide(0)
stop()

--zvz lings--
train(12, zergling)
wait(1000)
train(16, zergling)
--zvz lings 2--
wait(3000)
train(50, zergling)
goto(zvz lings 2)




--zvz mutas--
wait_build(1, spire)
train(9, mutalisk)
train(18, mutalisk)
train(27, mutalisk)
train(40, mutalisk)
wait(1500)
goto(zvz mutas)

--zvz harass--
wait_buildstart(1, spawning_pool)
wait(400)
rush(0, terminate)
wait_train(6, zergling)
send_suicide(0)
stop()

###--- 5pool

--5_pool--

build(5, drone, 80)
wait_buildstart(5, drone)
build(1, spawning_pool, 80)
define_max(5, drone)
multirun(5 pool rush)
wait_build(1, spawning_pool)
train(8, zergling)
wait_force(8, zergling)
send_suicide(0)
train(10, zergling)
wait_force(10, zergling)
send_suicide(0)
farms_timing()
defenseuse_gg(1, zergling)
defenseuse_ga(1, zergling)
define_max(60, drone)

build(9, drone, 80)
wait_buildstart(9, drone)
expand(1, natural)
race_jump(zvt 5 pool, overpool zvz, zvp start)

--5 pool rush--
wait_build(1, spawning_pool)
attack_add(6, zergling)
attack_do()
attack_clear()
clear_combatdata()
time_jump(9, terminate)
wait(100)
goto(5 pool rush)


--zvt 5 pool--
wait(300)
expand(1, zvt natural)
build(1, extractor, 90)
wait_build(1, extractor)

place_guard(drone, 0)
farms_timing()
goto(normal)

#==========================ISLANDS=============================================================
myki(1344, 101, aiscript):
start_town()
transports_off()
farms_notiming()

--start--
#if DEBUG
debug(null, Myk Island Script 1.0)
--null--
#endif
define_max(3, Zerg Queen)
define_max(12, Zerg Scourge)
define_max(100, Zerg Zergling)
define_max(100, Zerg Mutalisk)
define_max(100, Zerg Hydralisk)
define_max(100, Zerg Guardian)
define_max(255, Zerg Ultralisk)
define_max(255, Zerg Lurker)
define_max(255, Zerg Defiler)
build(1, Zerg Hatchery, 80)
build(1, Zerg Overlord, 80)
build(9, Zerg Drone, 80)
wait_buildstart(9, Zerg Drone)
build(1, Zerg Extractor, 150)
wait_buildstart(1, Zerg Extractor)
build(2, Zerg Overlord, 80)
wait_buildstart(2, Zerg Overlord)
build(12, Zerg Drone, 80)
wait_buildstart(12, Zerg Drone)
build(2, Zerg Hatchery, 80)
wait_buildstart(2, Zerg Hatchery)
build(1, Zerg Spawning Pool, 80)
wait_buildstart(1, Zerg Spawning Pool)
multirun(b_drones_a)
farms_timing()
build(2, Zerg Lair, 100)
wait_buildstart(2, Zerg Lair)
multirun(b_sunk)
wait_build(1, Zerg Lair)
upgrade(1, Ventral Sacs, 80)
wait_build(2, Zerg Lair)
upgrade(1, Pneumatized Carapace, 80)
wait(100)
build(1, Zerg Hydralisk Den, 80)
wait_build(1, Zerg Hydralisk Den)
multirun(r_hydradentech)
multirun(t_zvt_hydra)
wait_train(12, Zerg Hydralisk)
multirun(fast_expo_loop)
multirun(lingspeed)
--loop--
multirun(kill)
wait(100)
multirun(t_zvt_hydra)
multirun(t_ling)
wait(4000)
goto(loop)
--b_sunk--
player_need(1, Zerg Spawning Pool)
creep(4)
build(1, Zerg Creep Colony, 80)
wait_build(1, Zerg Creep Colony)
build(1, Zerg Sunken Colony, 80)
wait_buildstart(1, Zerg Sunken Colony)
wait(200)
creep(4)
build(2, Zerg Creep Colony, 80)
wait_build(2, Zerg Creep Colony)
build(2, Zerg Sunken Colony, 80)
wait_buildstart(2, Zerg Sunken Colony)
wait(200)
creep(4)
build(3, Zerg Creep Colony, 80)
wait_build(3, Zerg Creep Colony)
build(3, Zerg Sunken Colony, 80)
wait_buildstart(3, Zerg Sunken Colony)
stop()
#===Unit Trianing===
--zvt_wait_den--
wait(100)
build(1, Zerg Hydralisk Den, 80)
wait_build(1, Zerg Hydralisk Den)
goto(t_zvt_hydra)
--t_zvt_hydra--
notowns_jump(Zerg Hydralisk Den, zvt_wait_den)
defensebuild_gg(1, Zerg Hydralisk)
defenseuse_gg(1, Zerg Hydralisk)
defenseuse_ga(1, Zerg Hydralisk)
defenseuse_ag(1, Zerg Hydralisk)
defenseuse_aa(1, Zerg Hydralisk)
train(3, Zerg Hydralisk)
attack_add(3, Zerg Hydralisk)
wait(10)
train(6, Zerg Hydralisk)
attack_add(3, Zerg Hydralisk)
wait(10)
train(9, Zerg Hydralisk)
attack_add(3, Zerg Hydralisk)
wait(10)
train(12, Zerg Hydralisk)
attack_add(3, Zerg Hydralisk)
wait(10)
train(15, Zerg Hydralisk)
attack_add(3, Zerg Hydralisk)
wait(10)
train(18, Zerg Hydralisk)
attack_add(3, Zerg Hydralisk)
wait(10)
train(21, Zerg Hydralisk)
attack_add(3, Zerg Hydralisk)
wait(10)
train(24, Zerg Hydralisk)
attack_add(3, Zerg Hydralisk)
stop()
--wait_pool--
wait(100)
build(1, Zerg Spawning Pool, 80)
wait_build(1, Zerg Spawning Pool)
goto(t_ling)
--t_ling--
wait(10)
notowns_jump(Zerg Spawning Pool, wait_pool)
defenseuse_gg(1, Zerg Zergling)
defensebuild_gg(1, Zerg Zergling)
train(6, Zerg Zergling)
attack_add(6, Zerg Zergling)
wait(10)
train(12, Zerg Zergling)
attack_add(6, Zerg Zergling)
wait(10)
train(18, Zerg Zergling)
attack_add(6, Zerg Zergling)
wait(10)
train(24, Zerg Zergling)
attack_add(6, Zerg Zergling)
wait(10)
train(30, Zerg Zergling)
attack_add(6, Zerg Zergling)
wait(10)
train(36, Zerg Zergling)
attack_add(6, Zerg Zergling)
wait(10)
train(42, Zerg Zergling)
attack_add(6, Zerg Zergling)
wait(10)
train(48, Zerg Zergling)
attack_add(6, Zerg Zergling)
stop()
--lingspeed--
wait_build(1, Zerg Spawning Pool)
upgrade(1, Metabolic Boost, 70)
stop()
#===Upgrades & Research===

--r_hydradentech--
wait_build(1, Zerg Hydralisk Den)
upgrade(1, Muscular Augments, 80)
wait(2000)
upgrade(1, Grooved Spines, 80)
enemyowns_jump(Terran Command Center, halt)
wait(2000)
tech(Lurker Aspect, 50)
stop()
#===Drones & Expo Geyser===
--b_drones_a--
wait_build(1, Zerg Hatchery)
build(1, Zerg Drone, 80)
wait_buildstart(1, Zerg Drone)
build(2, Zerg Drone, 80)
wait_buildstart(2, Zerg Drone)
build(3, Zerg Drone, 80)
wait_buildstart(3, Zerg Drone)
build(4, Zerg Drone, 80)
wait_buildstart(4, Zerg Drone)
build(5, Zerg Drone, 80)
wait_buildstart(5, Zerg Drone)
build(6, Zerg Drone, 80)
wait_buildstart(6, Zerg Drone)
build(7, Zerg Drone, 80)
wait_buildstart(7, Zerg Drone)
build(8, Zerg Drone, 80)
wait_buildstart(8, Zerg Drone)
build(9, Zerg Drone, 80)
wait_buildstart(9, Zerg Drone)
build(10, Zerg Drone, 80)
wait_buildstart(10, Zerg Drone)
build(11, Zerg Drone, 80)
wait_buildstart(11, Zerg Drone)
build(12, Zerg Drone, 80)
wait_buildstart(12, Zerg Drone)
build(13, Zerg Drone, 80)
wait_buildstart(13, Zerg Drone)
build(14, Zerg Drone, 80)
wait_buildstart(14, Zerg Drone)
build(15, Zerg Drone, 80)
wait_buildstart(15, Zerg Drone)
build(16, Zerg Drone, 80)
wait_buildstart(16, Zerg Drone)
build(17, Zerg Drone, 80)
wait_buildstart(17, Zerg Drone)
build(18, Zerg Drone, 80)
wait_buildstart(18, Zerg Drone)
build(19, Zerg Drone, 80)
wait_buildstart(19, Zerg Drone)
build(20, Zerg Drone, 80)
wait_buildstart(20, Zerg Drone)
build(21, Zerg Drone, 80)
wait_buildstart(21, Zerg Drone)
build(22, Zerg Drone, 80)
wait_buildstart(22, Zerg Drone)
build(23, Zerg Drone, 80)
wait_buildstart(23, Zerg Drone)
build(24, Zerg Drone, 80)
wait_buildstart(24, Zerg Drone)
build(25, Zerg Drone, 80)
wait_buildstart(25, Zerg Drone)
stop()
--b_extractor--
wait_build(1, Zerg Hatchery)
build(1, Zerg Extractor, 80)
stop()
#===Expansions===
--fast_expo_loop--
expand(99, zvt_expo_build)
wait(4000)
goto(fast_expo_loop)
--zvt_expo_build--
start_town()
build(1, Zerg Hatchery, 80)
wait_build(1, Zerg Hatchery)
multirun(b_drones_a)
multirun(b_extractor)
multirun(b_hatchery)
multirun(b_expo_sunk)
stop()
--b_expo_sunk--
creep(3)
build(1, Zerg Creep Colony, 90)
wait_build(1, Zerg Creep Colony)
build(1, Zerg Sunken Colony, 90)
wait_buildstart(1, Zerg Sunken Colony)
wait(200)
creep(3)
build(2, Zerg Creep Colony, 90)
wait_build(2, Zerg Creep Colony)
build(2, Zerg Sunken Colony, 90)
wait_buildstart(2, Zerg Sunken Colony)
wait(200)
creep(3)
build(3, Zerg Creep Colony, 90)
wait_build(3, Zerg Creep Colony)
build(3, Zerg Sunken Colony, 90)
wait_buildstart(3, Zerg Sunken Colony)
wait(200)
creep(3)
build(4, Zerg Creep Colony, 90)
wait_build(4, Zerg Creep Colony)
build(4, Zerg Sunken Colony, 90)
wait_buildstart(4, Zerg Sunken Colony)
wait(200)
creep(3)
build(5, Zerg Creep Colony, 90)
wait_build(1, Zerg Creep Colony)
build(5, Zerg Sunken Colony, 90)
wait_buildstart(5, Zerg Sunken Colony)
stop()
#===Misc.===
--b_hatchery--
resources_jump(900, 0, b_hatchery_a)
wait(100)
goto(b_hatchery)
--b_hatchery_a--
build(2, Zerg Hatchery, 80)
wait_build(2, Zerg Hatchery)
stop()
--kill--
clear_combatdata()
attack_prepare()
wait(10)
clear_combatdata()
attack_do()
wait(10)
attack_clear()
stop()
--halt--
wait(100)
stop()
	







