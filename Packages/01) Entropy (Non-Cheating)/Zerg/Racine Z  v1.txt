# Racine Z v1.0

ZMCx(1344, 101, aiscript):

    start_town()
    transports_off()
    farms_notiming()

#if DEBUG
    debug(null, Racine Zerg 1.0)
	--null--
#endif

    build(1, hatchery, 1)
    wait_build(1, hatchery)
    build(9, drone, 1)
    wait_buildstart(9, drone)
    build(2, overlord, 1)
    wait_build(2, overlord)
    build(11, drone, 1)
    wait_buildstart(11, drone)
    build(1, spawning_pool, 1)
    wait_buildstart(1, spawning_pool)
    build(1, extractor, 10)
    wait_buildstart(1, extractor)
    check_transports()
    build(13, drone, 1)
    wait_buildstart(13, drone)
    build(1, lair, 1)
    wait_buildstart(1, lair)
    build(2, hatchery, 1)
    wait_buildstart(2, hatchery)
    build(1, hydralisk_den, 4)
    wait_buildstart(1, hydralisk_den)
    farms_timing()
    tech(lurker_aspect, 30)
    build(13, drone, 1)
    wait_buildstart(13, drone)
    defensebuild_gg(1, zergling)
    defenseuse_gg(1, zergling)
    train(4, zergling)
    defenseuse_gg(1, lurker)
    defensebuild_gg(1, lurker)
    place_guard(lurker, 0)
    place_guard(lurker, 2)
    expand(1, zt_exp)
    train(4, hydralisk)
    train(16, zergling)
    train(4, lurker)
    expand(1, zt_exp)
    upgrade(1, zergling_speed, 1)
    train(6, hydralisk)
    train(20, zergling)
    train(6, lurker)
    train(24, zergling)
    multirun(zt_loop)
    upgrade(1, hydralisk_speed, 1)
    train(14, hydralisk)
    train(4, lurker)
    multirun(zt_allexp)
    upgrade(1, hydralisk_range, 1)
    upgrade(1, overlord_speed, 1)
    multirun(zt_upgrades)
    train(22, hydralisk)

        --zt_waitresources--
    wait(600)
    resources_jump(500, 200, zt_buildrest)
    goto(zt_waitresources)

        --zt_buildrest--
    build(3, hatchery, 1)
    wait_buildstart(3, hatchery)
    build(13, drone, 1)
    wait_build(13, drone)
    stop()

        --zt_upgrades--
    build(3, evolution_chamber, 1)
    wait_build(3, evolution_chamber)
    upgrade(1, z_carapace, 1)
    upgrade(1, z_missile_attack, 1)
    upgrade(1, z_melee_attack, 1)
    upgrade(1, overlord_capacity, 1)
    wait(900)
    build(1, queen_nest, 1)
    wait_build(1, queen_nest)
    build(1, hive, 1)
    wait_build(1, hive)
    upgrade(2, z_carapace, 2)
    upgrade(2, z_missile_attack, 2)
    upgrade(2, z_melee_attack, 2)
    wait(4000)
    upgrade(3, z_carapace, 3)
    upgrade(3, z_missile_attack, 3)
    upgrade(3, z_melee_attack, 3)
    stop()

        --zt_loop--
    multirun(zt_unitsatk)
    multirun(zt_doexp)
    train(14, hydralisk)
    train(14, lurker)
    attack_add(14, lurker)
    attack_add(26, zergling)
    attack_prepare()
    attack_do()
    attack_clear()
    send_suicide(0)
    clear_combatdata()
    train(50, hydralisk)
    attack_add(44, hydralisk)
    attack_add(14, lurker)
    attack_prepare()
    attack_do()
    attack_clear()
    goto(zt_loop)

        --zt_doexp--
    expand(99, zt_exp)
    stop()

        --zt_unitsatk--
    train(10, hydralisk)
    train(16, zergling)
    train(10, lurker)
    train(20, zergling)
    train(20, hydralisk)
    train(14, lurker)
    train(30, zergling)
    wait(1)
    stop()

        --zt_allexp--
    expand(99, zt_exp)
    wait(6500)
    goto(zt_allexp)

        --zt_exp--
    start_town()
    build(1, hatchery, 1)
    wait_build(1, hatchery)
    enemyowns_jump(factory, zt_continueexp)
    get_oldpeons(4)
    defensebuild_gg(1, hydralisk)
    defenseuse_gg(1, hydralisk)
    notowns_jump(lurker, zt_manysunk)
    goto(zt_continueexp)

        --zt_manysunk--
    build(5, drone, 10)
    wait_build(5, drone)
    wait(1750)
    build(5, creep_colony, 50)
    defenseuse_gg(1, zergling)
    wait_buildstart(5, creep_colony)
    build(1, creep_colony, 5)
    creep(0)
    build(2, creep_colony, 5)
    creep(0)
    build(3, creep_colony, 5)
    creep(0)
    build(4, creep_colony, 5)
    creep(0)
    build(5, sunken_colony, 50)
    wait_build(5, sunken_colony)

        --zt_continueexp--
    build(3, drone, 1)
    wait_build(3, drone)
    build(3, creep_colony, 10)
    wait_build(3, creep_colony)
    build(3, sunken_colony, 10)
    wait_build(3, sunken_colony)
    build(10, drone, 1)
    build(1, extractor, 1)
    stop()



