#Myk's Protoss AI version 3.5
#
# stat_txt.tbl entry 1343: Protoss Expansion Custom Level<0>
PMCx(1343, 101, aiscript):
	start_town()
	transports_off()
	farms_notiming()
#if DEBUG
	debug(null, Myk's Protoss AI 3.5)
		--null--
#endif
	race_jump(PvT, PvZ, PvT)
--PvT--
	define_max(2, Protoss Shuttle)
	define_max(3, Protoss Observer)
	define_max(100, Protoss Zealot)
	define_max(100, Protoss Dragoon)
	define_max(2, Protoss High Templar)
	define_max(255, Protoss Dark Templar)
	define_max(255, Protoss Archon)
	define_max(255, Protoss Corsair)
	define_max(255, Protoss Dark Archon)
	define_max(255, Protoss Reaver)
	define_max(255, Protoss Carrier)
	define_max(255, Protoss Arbiter)
	define_max(255, Protoss Scout)
	build(1, Protoss Nexus, 150)
	build(8, Protoss Probe, 80)
	wait_buildstart(8, Protoss Probe)
	build(1, Protoss Pylon, 80)
	multirun(b_probes)
	wait_buildstart(10, Protoss Probe)
	build(1, Protoss Gateway, 80)
	wait_buildstart(12, Protoss Probe)
	build(1, Protoss Assimilator, 80)
	wait_buildstart(13, Protoss Probe)
	build(1, Protoss Cybernetics Core, 80)
	farms_timing()
	multirun(give_the_money)
	wait_build(1, Protoss Cybernetics Core)
	train(1, Protoss Dragoon)
	multirun(r_goon_range)
	multirun(t_dragoon)
	build(1, Protoss Robotics Facility, 80)
	wait_build(1, Protoss Robotics Facility)
	build(1, Protoss Observatory, 80)
	wait_build(1, Protoss Observatory)
	multirun(t_obs_2)
	expand(99, pvt_nat_expo)
	multirun(more_gates)
	build(1, Protoss Citadel of Adun, 80)
	wait_build(1, Protoss Citadel of Adun)
	multirun(r_legspeed)
	multirun(t_zealot)
	build(1, Protoss Templar Archives, 80)
	multirun(r_templar)
	multirun(t_2_ht)
	build(1, Protoss Stargate, 80)
	wait_build(1, Protoss Stargate)
	build(1, Protoss Fleet Beacon, 80)
	wait_build(1, Protoss Fleet Beacon)
	multirun(pvt_expandmore)
	multirun(for_aiur)
	enemyowns_jump(Protoss Nexus, PvT_A)
	random_jump(128, PvT_A)
	goto(PvT_B)
		--PvT_A--
	multirun(t_zealot)
	multirun(t_dragoon)
	multirun(t_2_ht)
	define_max(8, Protoss Arbiter)
	build(1, Protoss Arbiter Tribunal, 80)
	wait_build(1, Protoss Arbiter Tribunal)
	multirun(t_arbiter)
	multirun(r_arbiterz)
	build(2, Protoss Stargate, 80)
	wait_build(2, Protoss Stargate)
	wait_train(1, Protoss Arbiter)
		--pvt_a_endloop--
	multirun(for_aiur)
	wait(100)
	multirun(t_zealot)
	multirun(t_dragoon)
	multirun(t_arbiter)
	multirun(t_2_ht)
	wait(3500)
	goto(pvt_a_endloop)
		--PvT_B--
	multirun(t_zealot)
	multirun(t_dragoon)
	multirun(t_2_ht)
	define_max(12, Protoss Carrier)
	multirun(r_core_airups)
	multirun(r_carrier_cap)
	build(3, Protoss Stargate, 80)
	wait_build(3, Protoss Stargate)
		--pvt_b_endloop--
	multirun(for_aiur)
	wait(100)
	multirun(t_zealot)
	multirun(t_dragoon)
	multirun(t_carrier)
	multirun(t_2_ht)
	wait(3500)
	goto(pvt_b_endloop)
	
#===PvZ===
--PvZ--
	define_max(5, Protoss Shuttle)
	define_max(3, Protoss Observer)
	define_max(6, Protoss High Templar)
	define_max(10, Protoss Reaver)
	define_max(9, Protoss Dark Templar)
	define_max(2, Protoss Corsair)
	define_max(100, Protoss Dragoon)
	define_max(100, Protoss Zealot)
	define_max(100, Protoss Archon)
	define_max(255, Protoss Dark Archon)
	define_max(255, Protoss Carrier)
	define_max(255, Protoss Arbiter)
	define_max(255, Protoss Scout)
	build(1, Protoss Nexus, 150)
	build(8, Protoss Probe, 80)
	wait_buildstart(8, Protoss Probe)
	multirun(b_probes)
	expand(1, Protoss_FE)
	multirun(give_the_money)
	wait_buildstart(12, Protoss Probe)
	build(1, Protoss Pylon, 80)
	wait_buildstart(1, Protoss Pylon)
	build(1, Protoss Assimilator, 80)
	wait_buildstart(1, Protoss Assimilator)
	wait_build(1, Protoss Pylon)
	farms_timing()
	build(1, Protoss Cybernetics Core, 80)
	wait_buildstart(1, Protoss Cybernetics Core)
	defensebuild_gg(1, Protoss Zealot)
	defenseuse_gg(1, Protoss Zealot)
	wait_build(1, Protoss Cybernetics Core)
	build(1, Protoss Stargate, 80)
	wait_buildstart(1, Protoss Stargate)
	build(1, Protoss Citadel of Adun, 80)
	wait_build(1, Protoss Stargate)
	train(1, Protoss Corsair)
	multirun(corsairstuff)
	multirun(r_forge_melee)
	build(1, Protoss Templar Archives, 80)
	wait_buildstart(1, Protoss Templar Archives)
	build(3, Protoss Gateway, 80)
	wait_build(3, Protoss Gateway)
	train(2, Protoss Archon)
	multirun(t_small_archon)
	multirun(r_templar)
	wait(100)
	multirun(r_legspeed)
	multirun(t_zealot)
	wait(1000)
	multirun(more_gates)
	multirun(t_obs)
	wait(2000)
	multirun(pvz_expandmore)
		--PvZ_A_endloop--
	multirun(for_aiur)
	wait(100)
	multirun(t_zealot)
	multirun(t_dragoon)
	multirun(t_dt)
	multirun(t_ht)
	multirun(t_big_archon)
	wait(4000)
	goto(PvZ_A_endloop)
	
	--corsairstuff--
	train(1, Protoss Corsair)
	attack_add(1, Protoss Corsair)
	clear_combatdata()
	attack_do()
	attack_clear()
	stop()
#===PvZ FE===
--Protoss_FE--
	start_town()
	multirun(b_probes)
	multirun(get_probes)
	build(1, Protoss Pylon, 80)
	wait_build(1, Protoss Pylon)
	rush(0, forge_can_nex)
	build(1, Protoss Nexus, 80)
	wait_buildstart(1, Protoss Nexus)
	build(1, Protoss Forge, 80)
	wait_build(1, Protoss Forge)
	rush(0, nex_forge_can)
	build(1, Protoss Gateway, 80)
	wait_buildstart(1, Protoss Gateway)
	multirun(b_gas)
	multirun(b_cannons)
	stop()
		--nex_forge_can--
	build(2, Protoss Photon Cannon, 80)
	wait_buildstart(2, Protoss Photon Cannon)
	build(1, Protoss Gateway, 80)
	wait_buildstart(1, Protoss Gateway)
	multirun(b_gas)
	multirun(b_cannons)
	stop()
		--forge_can_nex--
	build(1, Protoss Forge, 80)
	wait_build(1, Protoss Forge)
	build(1, Protoss Photon Cannon, 80)
	wait_buildstart(1, Protoss Photon Cannon)
	build(2, Protoss Photon Cannon, 80)
	wait_buildstart(2, Protoss Photon Cannon)
	build(1, Protoss Nexus, 80)
	wait_buildstart(1, Protoss Nexus)
	build(1, Protoss Gateway, 80)
	wait_buildstart(1, Protoss Gateway)
	multirun(b_gas)
	multirun(b_cannons)
	stop()
		--get_probes--
	wait_build(1, Protoss Nexus)
	get_oldpeons(7)
	stop()
	
#===Training===
--t_zealot--
	wait(10)
	notowns_jump(Protoss Gateway, t_zealot)
	defensebuild_gg(1, Protoss Zealot)
	defensebuild_ga(1, Protoss Zealot)
	defenseuse_gg(1, Protoss Zealot)
	defenseuse_ga(1, Protoss Zealot)
	train(1, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(2, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(3, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(4, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(5, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(6, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(7, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(8, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(9, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(10, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(11, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(12, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(13, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(14, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(15, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(16, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(17, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(18, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(19, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(20, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(21, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(22, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(23, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	train(24, Protoss Zealot)
	attack_add(1, Protoss Zealot)
	stop()
	
--t_2_ht--
	wait(100)
	notowns_jump(Protoss Gateway, t_2_ht)
	notowns_jump(Protoss Templar Archives, t_2_ht)
	place_guard(Protoss High Templar, 0)
	place_guard(Protoss High Templar, 1)
	train(1, Protoss High Templar)
	attack_add(1, Protoss High Templar)
	train(2, Protoss High Templar)
	attack_add(1, Protoss High Templar)
	stop()
	
--t_ht--
	wait(10)
	notowns_jump(Protoss Gateway, t_ht)
	notowns_jump(Protoss Templar Archives, t_ht)
	place_guard(Protoss High Templar, 0)
	place_guard(Protoss High Templar, 1)
	place_guard(Protoss High Templar, 2)
	train(1, Protoss High Templar)
	attack_add(1, Protoss High Templar)
	train(2, Protoss High Templar)
	attack_add(1, Protoss High Templar)
	train(3, Protoss High Templar)
	attack_add(1, Protoss High Templar)
	stop()
	
--t_dt--
	wait(10)
	notowns_jump(Protoss Gateway, t_dt)
	notowns_jump(Protoss Templar Archives, t_dt)
	defenseuse_gg(1, Protoss Dark Templar)
	defensebuild_gg(1, Protoss Dark Templar)
	train(1, Protoss Dark Templar)
	attack_add(1, Protoss Dark Templar)
	train(2, Protoss Dark Templar)
	attack_add(1, Protoss Dark Templar)
	train(3, Protoss Dark Templar)
	attack_add(1, Protoss Dark Templar)
	stop()
	
--t_sair--
	wait(10)
	notowns_jump(Protoss Stargate, t_sair)
	defensebuild_aa(1, Protoss Corsair)
	defensebuild_ag(1, Protoss Corsair)
	defenseuse_aa(1, Protoss Corsair)
	defenseuse_ag(1, Protoss Corsair)
	train(1, Protoss Corsair)
	attack_add(1, Protoss Corsair)
	train(2, Protoss Corsair)
	attack_add(1, Protoss Corsair)
	train(3, Protoss Corsair)
	attack_add(1, Protoss Corsair)
	train(4, Protoss Corsair)
	attack_add(1, Protoss Corsair)
	train(5, Protoss Corsair)
	attack_add(1, Protoss Corsair)
	train(6, Protoss Corsair)
	attack_add(1, Protoss Corsair)
	train(7, Protoss Corsair)
	attack_add(1, Protoss Corsair)
	train(8, Protoss Corsair)
	attack_add(1, Protoss Corsair)
	train(9, Protoss Corsair)
	attack_add(1, Protoss Corsair)
	train(10, Protoss Corsair)
	attack_add(1, Protoss Corsair)
	train(11, Protoss Corsair)
	attack_add(1, Protoss Corsair)
	train(12, Protoss Corsair)
	attack_add(1, Protoss Corsair)
	stop()
	
--t_small_archon--
	wait(100)
	notowns_jump(Protoss Gateway, t_small_archon)
	notowns_jump(Protoss Templar Archives, t_small_archon)
	defensebuild_gg(1, Protoss Archon)
	defensebuild_ga(1, Protoss Archon)
	defensebuild_ag(1, Protoss Archon)
	defensebuild_aa(1, Protoss Archon)
	defenseuse_gg(1, Protoss Archon)
	defenseuse_ga(1, Protoss Archon)
	defenseuse_aa(1, Protoss Archon)
	defenseuse_ag(1, Protoss Archon)
	train(1, Protoss Archon)
	attack_add(1, Protoss Archon)
	train(2, Protoss Archon)
	attack_add(1, Protoss Archon)
	stop()
--t_big_archon--
	wait(100)
	notowns_jump(Protoss Gateway, t_big_archon)
	notowns_jump(Protoss Templar Archives, t_big_archon)
	defensebuild_gg(1, Protoss Archon)
	defensebuild_ga(1, Protoss Archon)
	defensebuild_ag(1, Protoss Archon)
	defensebuild_aa(1, Protoss Archon)
	defenseuse_gg(1, Protoss Archon)
	defenseuse_ga(1, Protoss Archon)
	defenseuse_aa(1, Protoss Archon)
	defenseuse_ag(1, Protoss Archon)
	train(1, Protoss Archon)
	attack_add(1, Protoss Archon)
	train(2, Protoss Archon)
	attack_add(1, Protoss Archon)
	train(3, Protoss Archon)
	attack_add(1, Protoss Archon)
	train(4, Protoss Archon)
	attack_add(1, Protoss Archon)
	train(5, Protoss Archon)
	attack_add(1, Protoss Archon)
	train(6, Protoss Archon)
	attack_add(1, Protoss Archon)
	stop()

--t_dragoon--
	wait(100)
	notowns_jump(Protoss Gateway, t_dragoon)
	notowns_jump(Protoss Cybernetics Core, t_dragoon)
	defensebuild_gg(1, Protoss Dragoon)
	defensebuild_ga(1, Protoss Dragoon)
	defensebuild_ag(1, Protoss Dragoon)
	defensebuild_aa(1, Protoss Dragoon)
	defenseuse_gg(1, Protoss Dragoon)
	defenseuse_ga(1, Protoss Dragoon)
	defenseuse_ag(1, Protoss Dragoon)
	defenseuse_aa(1, Protoss Dragoon)
	train(1, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(2, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(3, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(4, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(5, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(6, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(7, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(8, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(9, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(10, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(11, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(12, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(13, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(14, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(15, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(16, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(17, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(18, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(19, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(20, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(21, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(22, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(23, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	train(24, Protoss Dragoon)
	attack_add(1, Protoss Dragoon)
	stop()
--t_reaver--
	wait(10)
	notowns_jump(Protoss Robotics Facility, t_reaver)
	notowns_jump(Protoss Robotics Support Bay, t_reaver)
	defenseuse_gg(1, Protoss Reaver)
	train(1, Protoss Reaver)
	attack_add(1, Protoss Reaver)
	train(2, Protoss Reaver)
	attack_add(1, Protoss Reaver)
	train(3, Protoss Reaver)
	attack_add(1, Protoss Reaver)
	train(4, Protoss Reaver)
	attack_add(1, Protoss Reaver)
	train(5, Protoss Reaver)
	attack_add(1, Protoss Reaver)
	train(6, Protoss Reaver)
	attack_add(1, Protoss Reaver)
	stop()
	
--t_obs--
	wait(100)
	notowns_jump(Protoss Cybernetics Core, t_obs)
	build(1, Protoss Robotics Facility, 80)
	wait_build(1, Protoss Robotics Facility)
	multirun(transports)
	build(1, Protoss Observatory, 80)
	wait_build(1, Protoss Observatory)
	upgrade(1, Gravitic Boosters, 80)
--t_obs_2--
	wait(300)
	notowns_jump(Protoss Observatory, t_obs_2)
	place_guard(Protoss Observer, 0)
	place_guard(Protoss Observer, 1)
	place_guard(Protoss Observer, 2)
	train(1, Protoss Observer)
	attack_add(1, Protoss Observer)
	wait(300)
	train(2, Protoss Observer)
	attack_add(1, Protoss Observer)
	wait(300)
	train(3, Protoss Observer)
	attack_add(1, Protoss Observer)
	wait(300)
	goto(t_obs_2)
	
		--transports--
	wait_build(1, Protoss Robotics Facility)
	wait_train(1, Protoss Observer)
	check_transports()
	stop()
		--t_arbiter--
	notowns_jump(Protoss Arbiter Tribunal, halt)
	place_guard(Protoss Arbiter, 0)
	place_guard(Protoss Arbiter, 1)
	place_guard(Protoss Arbiter, 2)
	train(1, Protoss Arbiter)
	attack_add(1, Protoss Arbiter)
	train(2, Protoss Arbiter)
	attack_add(1, Protoss Arbiter)
	train(3, Protoss Arbiter)
	attack_add(1, Protoss Arbiter)
	train(4, Protoss Arbiter)
	attack_add(1, Protoss Arbiter)
	train(5, Protoss Arbiter)
	attack_add(1, Protoss Arbiter)
	train(6, Protoss Arbiter)
	attack_add(1, Protoss Arbiter)
	stop()
	
		--t_carrier--
	defensebuild_gg(1, Protoss Carrier)
	defensebuild_ga(1, Protoss Carrier)
	defensebuild_ag(1, Protoss Carrier)
	defensebuild_aa(1, Protoss Carrier)
	defenseuse_gg(1, Protoss Carrier)
	defenseuse_ga(1, Protoss Carrier)
	defenseuse_ag(1, Protoss Carrier)
	defenseuse_aa(1, Protoss Carrier)
	train(1, Protoss Carrier)
	attack_add(1, Protoss Carrier)
	train(2, Protoss Carrier)
	attack_add(1, Protoss Carrier)
	train(3, Protoss Carrier)
	attack_add(1, Protoss Carrier)
	train(4, Protoss Carrier)
	attack_add(1, Protoss Carrier)
	train(5, Protoss Carrier)
	attack_add(1, Protoss Carrier)
	train(6, Protoss Carrier)
	attack_add(1, Protoss Carrier)
	train(7, Protoss Carrier)
	attack_add(1, Protoss Carrier)
	train(8, Protoss Carrier)
	attack_add(1, Protoss Carrier)
	train(9, Protoss Carrier)
	attack_add(1, Protoss Carrier)
	train(10, Protoss Carrier)
	attack_add(1, Protoss Carrier)
	train(11, Protoss Carrier)
	attack_add(1, Protoss Carrier)
	train(12, Protoss Carrier)
	attack_add(1, Protoss Carrier)
	stop()
	
#===Research===
#		--r_fleetbeacon--
#	wait_build(1, Protoss Fleet Beacon)
#	wait_train(1, Protoss Corsair)
#	tech(Disruption Web, 80)
#	wait(1200)
#	wait_train(1, Protoss Carrier)
	--r_carrier_cap--
	wait_build(1, Protoss Fleet Beacon)
	upgrade(1, Carrier Capacity, 80)
	stop()
		--r_core_airups--
	upgrade(1, Protoss Air Weapons, 80)
	wait(4500)
	upgrade(1, Protoss Plating, 80)
	wait(4500)
	wait_build(1, Protoss Fleet Beacon)
	upgrade(2, Protoss Air Weapons, 80)
	wait(5000)
	upgrade(2, Protoss Plating, 80)
	wait(5000)
	upgrade(3, Protoss Air Weapons, 80)
	wait(5000)
	upgrade(3, Protoss Plating, 80)
	stop()
		--r_goon_range--
	wait_build(1, Protoss Cybernetics Core)
	upgrade(1, Singularity Charge, 80)
	stop()
		--r_templar--
	wait_build(1, Protoss Templar Archives)
	tech(Psionic Storm, 80)
	wait(1800)
	upgrade(1, Khaydarin Amulet, 50)
	stop()
		--r_legspeed--
	wait_build(1, Protoss Citadel of Adun)
	upgrade(1, Leg Enhancements, 80)
	stop()
		--r_forge_melee--
	wait(25)
	notowns_jump(Protoss Forge, r_forge_melee)
	upgrade(1, Protoss Ground Weapons, 80)
	wait(4000)
		--r_forge_melee_b--
	wait(100)
	notowns_jump(Protoss Templar Archives, r_forge_melee_b)
	upgrade(2, Protoss Ground Weapons, 80)
	wait(4500)
	upgrade(3, Protoss Ground Weapons, 80)
	stop()
		--r_arbiterz--
	wait_build(1, Protoss Arbiter Tribunal)
	tech(Stasis Field, 80)
	wait(1600)
	upgrade(1, Khaydarin Core, 80)
	stop()
#====Building===
		--more_gates--
	resources_jump(350, 0, more_gates_a)
	wait(300)
	goto(more_gates)
		--more_gates_a--
	build(4, Protoss Gateway, 80)
	wait_buildstart(4, Protoss Gateway)
		--more_gates_b--
	resources_jump(450, 0, more_gates_c)
	wait(300)
	goto(more_gates_b)
		--more_gates_c--
	build(6, Protoss Gateway, 80)
	wait_buildstart(6, Protoss Gateway)
	stop()
	
		--m_main_cannons--
	enemyowns_jump(Zerg Spire, b_cannons)
	enemyowns_jump(Zerg Hive, b_cannons)
	wait(100)
	goto(m_main_cannons)
#===Expansions + Expansion script stuff===	
		--pvz_expandmore--
	expand(99, pvz_non_fe_nat_expo)
	wait(7000)
	goto(pvz_expandmore)
		--pvz_non_fe_nat_expo--
	start_town()
	build(1, Protoss Nexus, 80)
	wait_buildstart(1, Protoss Nexus)
	capt_expand()
	build(1, Protoss Pylon, 80)
	wait_build(1, Protoss Pylon)
	multirun(b_cannons)
	multirun(b_gas)
	multirun(b_probes)
	multirun(b_extras)
	stop()
	
		--pvt_expandmore--
	expand(99, pvt_nat_expo)
	wait(7000)
	goto(pvt_expandmore)
		--pvt_nat_expo--
	start_town()
	build(1, Protoss Nexus, 80)
	wait_build(1, Protoss Nexus)
	multirun(b_gas)
	multirun(b_probes)
	multirun(b_extras)
	build(1, Protoss Pylon, 80)
	wait_buildstart(1, Protoss Pylon)
	stop()
	
		--b_extras--
	wait_build(1, Protoss Nexus)
	wait_build(1, Protoss Pylon)
	build(1, Protoss Gateway, 80)
	wait_build(1, Protoss Gateway)
	wait(300)
	build(2, Protoss Gateway, 80)
	wait_build(1, Protoss Gateway)
	stop()
		--b_expo_forge--
	build(1, Protoss Pylon, 80)	
	wait_build(1, Protoss Pylon)
	build(1, Protoss Forge, 80)
	wait_build(1, Protoss Forge)
	wait(100)
	multirun(b_cannons)
	stop()
		--b_cannons--
	wait(100)
	notowns_jump(Protoss Forge, b_expo_forge)
	build(1, Protoss Photon Cannon, 80)
	wait_build(1, Protoss Photon Cannon)
	wait(200)
	build(2, Protoss Photon Cannon, 80)
	wait_build(2, Protoss Photon Cannon)
	wait(200)
	build(3, Protoss Photon Cannon, 80)
	wait_build(3, Protoss Photon Cannon)
	wait(200)
	build(4, Protoss Photon Cannon, 80)
	wait_build(4, Protoss Photon Cannon)
	wait(200)
	build(5, Protoss Photon Cannon, 80)
	wait_build(5, Protoss Photon Cannon)
	wait(200)
	build(6, Protoss Photon Cannon, 80)
	wait_build(6, Protoss Photon Cannon)
	wait(200)
	build(7, Protoss Photon Cannon, 80)
	wait_build(7, Protoss Photon Cannon)
	stop()
		--b_gas--
	wait_build(1, Protoss Nexus)
	build(1, Protoss Assimilator, 80)
	stop()
		--b_probes--
	wait_build(1, Protoss Nexus)
	build(1, Protoss Probe, 80)
	wait_build(1, Protoss Probe)
	build(2, Protoss Probe, 80)
	wait_build(2, Protoss Probe)
	build(3, Protoss Probe, 80)
	wait_build(3, Protoss Probe)
	build(4, Protoss Probe, 80)
	wait_build(4, Protoss Probe)
	build(5, Protoss Probe, 80)
	wait_build(5, Protoss Probe)
	build(6, Protoss Probe, 80)
	wait_build(6, Protoss Probe)
	build(7, Protoss Probe, 80)
	wait_build(7, Protoss Probe)
	build(8, Protoss Probe, 80)
	wait_build(8, Protoss Probe)
	build(9, Protoss Probe, 80)
	wait_build(9, Protoss Probe)
	build(10, Protoss Probe, 80)
	wait_build(10, Protoss Probe)
	build(11, Protoss Probe, 80)
	wait_build(11, Protoss Probe)
	build(12, Protoss Probe, 80)
	wait_build(12, Protoss Probe)
	build(13, Protoss Probe, 80)
	wait_build(13, Protoss Probe)
	build(14, Protoss Probe, 80)
	wait_build(14, Protoss Probe)
	build(15, Protoss Probe, 80)
	wait_build(15, Protoss Probe)
	build(16, Protoss Probe, 80)
	wait_build(16, Protoss Probe)
	build(17, Protoss Probe, 80)
	wait_build(17, Protoss Probe)
	build(18, Protoss Probe, 80)
	wait_build(18, Protoss Probe)
	build(19, Protoss Probe, 80)
	wait_build(19, Protoss Probe)
	build(20, Protoss Probe, 80)
	wait_build(20, Protoss Probe)
	build(21, Protoss Probe, 80)
	wait_build(21, Protoss Probe)
	build(22, Protoss Probe, 80)
	wait_build(22, Protoss Probe)
	build(23, Protoss Probe, 80)
	wait_build(23, Protoss Probe)
	build(24, Protoss Probe, 80)
	wait_build(24, Protoss Probe)
	stop()
#===Misc.===445x240
		--for_aiur--
	clear_combatdata()
	attack_prepare()
	wait(10)
	clear_combatdata()
	attack_do()
	wait(10)
	attack_clear()
	stop()
		--give_the_money--
	wait(100)
	#resources_jump(400, 400, give_the_money)
	#give_money()
	wait(100)
	goto(give_the_money)
		--halt--
	stop()