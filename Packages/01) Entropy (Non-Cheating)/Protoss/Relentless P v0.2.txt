# stat_txt.tbl entry 1343: Protoss Expansion Custom Level<0>
PMCx(1343, 101, aiscript):
	start_town()
	transports_off()
	farms_notiming()
	define_max(50, Protoss Probe)
	define_max(30, Protoss Zealot)
	define_max(16, Protoss Dragoon)
	define_max(6, Protoss High Templar)
	define_max(12, Protoss Dark Templar)
	define_max(12, Protoss Archon)
	define_max(255, Protoss Dark Archon)
	define_max(255, Protoss Shuttle)
	define_max(255, Protoss Reaver)
	define_max(2, Protoss Observer)
	define_max(255, Protoss Scout)
	define_max(255, Protoss Carrier)
	define_max(2, Protoss Arbiter)
	define_max(12, Protoss Corsair)

#if DEBUG
	debug(null, Relentless Protoss v0.1 by Perpetual)
	--null--
#endif

		--basic_building--
	build(7, Protoss Probe, 80)
	wait_build(7, Protoss Probe)
	enemyowns_jump(Zerg Spawning Pool, anti_rush)
	build(8, Protoss Probe, 80)
	wait_buildstart(8, Protoss Probe)
	build(1, Protoss Pylon, 80)
	wait_buildstart(1, Protoss Pylon)
	build(10, Protoss Probe, 80)
	wait_buildstart(10, Protoss Probe)
	build(1, Protoss Gateway, 80)
	wait_buildstart(1, Protoss Gateway)
	build(12, Protoss Probe, 80)
	wait_buildstart(12, Protoss Probe)
	goto(more_building)
		--more_building--	
	build(2, Protoss Gateway, 80)
	wait_buildstart(2, Protoss Gateway)
	multirun(zealot_rush)
	build(13, Protoss Probe, 80)
	wait_buildstart(13, Protoss Probe)
	build(2, Protoss Pylon, 80)
	wait_build(2, Protoss Gateway)
	build(14, Protoss Probe, 80)
	wait_build(14, Protoss Probe)
	build(3, Protoss Pylon, 80)
	wait_buildstart(3, Protoss Pylon)
	build(1, Protoss Assimilator, 80)
	wait_buildstart(1, Protoss Assimilator)
	build(16, Protoss Probe, 80)
	wait_build(3, Protoss Pylon)
	farms_timing()
	wait_buildstart(4, Protoss Pylon)
	build(1, Protoss Cybernetics Core, 80)
	wait_build(1, Protoss Cybernetics Core)
	build(1, Protoss Forge, 80)
	wait_buildstart(1, Protoss Forge)
	build(17, Protoss Probe, 80)
	wait_buildstart(17, Protoss Probe)	
	multirun(essentials)
	multirun(build_probe)
	multirun(invis_check)
	multirun(basic_force)
	expand(1, natural_exe)
	build(1, Protoss Citadel of Adun, 20)
	wait_buildstart(1, Protoss Citadel of Adun)
	multirun(r_goon_range)
	wait_build(1, Protoss Citadel of Adun)
	build(1, Protoss Templar Archives, 80)
	wait_buildstart(1, Protoss Templar Archives)
	multirun(r_units)
	wait_build(1, Protoss Templar Archives)
	multirun(advanced_force)
	multirun(gas_check)
	multirun(r_zealot_speed)
	build(3, Protoss Gateway, 40)
	wait_buildstart(3, Protoss Gateway)
	build(4, Protoss Gateway, 10)
	wait_build(4, Protoss Gateway)
	multirun(templar_guards)
	multirun(r_psi_storm)
	expand(2, second_exe)
	build(5, Protoss Gateway, 10)
	wait_build(5, Protoss Gateway)
	build(1, Protoss Stargate, 10)
	wait_buildstart(1, Protoss Stargate)
	multirun(air_check)
	build(6, Protoss Gateway, 10)
	wait_build(6, Protoss Gateway)
	multirun(exe_timer)
	build(1, Protoss Fleet Beacon, 20)
	wait_build(1, Protoss Fleet Beacon)
	multirun(r_disruption_web)
	build(1, Protoss Arbiter Tribunal, 20)
	wait_build(1, Protoss Arbiter Tribunal)
	multirun(r_arbiter)
	goto(res_check)	
			--res_check--
	wait(200)
	resources_jump(1000, 0, add_gates_I)
	goto(res_check)		
		--add_gates_I--
	wait(1000)	
	build(7, Protoss Gateway, 40)
	wait_build(7, Protoss Gateway)
	resources_jump(1000, 0, add_gates_II)
	goto(add_gates_I)	
	--add_gates_II--
	wait(1000)	
	build(8, Protoss Gateway, 40)
	wait_build(8, Protoss Gateway)
	resources_jump(1000, 0, add_gates_III)
	goto(add_gates_II)
	--add_gates_III--
	wait(1000)	
	build(9, Protoss Gateway, 40)
	wait_build(9, Protoss Gateway)
	stop()
	
		--anti_rush--
	build(1, Protoss Pylon, 80)
	wait_buildstart(1, Protoss Pylon)
	build(8, Protoss Probe, 80)
	wait_buildstart(8, Protoss Probe)
	build(1, Protoss Gateway, 80)
	wait_buildstart(8, Protoss Probe)
	build(9, Protoss Probe, 80)
	wait_buildstart(9, Protoss Probe)
	train(1, Protoss Zealot)
	build(10, Protoss Probe, 80)
	wait_buildstart(10, Protoss Probe)
	train(2, Protoss Zealot)
	build(11, Protoss Probe, 80)
	wait_buildstart(11, Protoss Probe)
	build(12, Protoss Probe, 80)
	wait_buildstart(12, Protoss Probe)
	goto(more_building)
	
		--observer_build--
	build(1, Protoss Robotics Facility, 100)
	wait_build(1, Protoss Robotics Facility)
	build(1, Protoss Observatory, 100)
	wait_build(1, Protoss Observatory)
	multirun(t_observer)
	wait_train(1, Protoss Observer)
	upgrade(1, Gravitic Boosters, 20)
	stop()
		--t_observer--	
	attack_add(1, Protoss Observer)
	wait_train(1, Protoss Observer)
	wait(400)
	goto(t_observer)

	#Units
			--zealot_rush--
	wait_build(1, Protoss Gateway)
	attack_add(9, Protoss Zealot)
	wait_force(9, Protoss Zealot)
	attack_do()
	attack_clear()
	clear_combatdata()
	stop()
			--basic_force--
	attack_add(8, Protoss Zealot)
	attack_add(4, Protoss Dragoon)
	stop()
			--advanced_force--
#	attack_add(4, Protoss Dark Templar)
#	wait_force(4, Protoss Dark Templar)
	attack_add(10, Protoss Dark Templar)
	attack_add(16, Protoss Zealot)
	attack_add(9, Protoss Dragoon)
	wait_force(6, Protoss Dark Templar)
	wait_force(12, Protoss Zealot)
	attack_prepare()
	clear_combatdata()
	attack_do()
	attack_clear()
	wait(100)
	goto(final_force)
			--final_force--
	wait(100)
	notowns_jump(Protoss Arbiter Tribunal, advanced_force)
	attack_add(30, Protoss Zealot)
	attack_add(16, Protoss Dragoon)
	attack_add(10, Protoss Dark Templar)
	attack_add(1, Protoss Arbiter)
	wait_force(1, Protoss Arbiter)
	wait_force(8, Protoss Dark Templar)	
	wait_force(12, Protoss Zealot)
	wait_force(6, Protoss Dragoon)
	clear_combatdata()
	attack_do()
	attack_clear()
	multirun(air_check)
	wait(100)
	goto(final_force)
		--add_archon--
	wait(20)
	attack_add(1, Protoss Archon)
	goto(gas_check)
		--gas_check--
	wait(200)
	resources_jump(0, 700, add_archon)
	goto(gas_check)	
	
		--templar_guards--	
	place_guard(Protoss High Templar, 0)
	stop()
	
		--t_sairs--
	wait(200)
	wait_build(1, Protoss Stargate)
	train(1, Protoss Corsair)
	wait_train(1, Protoss Corsair)
	train(2, Protoss Corsair)
	wait_train(1, Protoss Corsair)
	train(3, Protoss Dragoon)
	wait_train(1, Protoss Corsair)
	train(4, Protoss Corsair)
	wait_train(1, Protoss Corsair)
	train(5, Protoss Corsair)
	wait_train(1, Protoss Corsair)
	train(6, Protoss Corsair)
	wait_train(1, Protoss Corsair)
	goto(t_sairs)
	
		--build_probe--
	wait_build(1, Protoss Nexus)
	build(1, Protoss Probe, 80)
	wait_build(1, Protoss Probe)
	build(2, Protoss Probe, 80)
	wait_build(2, Protoss Probe)
	build(3, Protoss Probe, 80)
	wait_build(3, Protoss Probe)
	build(4, Protoss Probe, 80)
	wait_build(4, Protoss Probe)
	build(5, Protoss Probe, 80)
	wait_build(5, Protoss Probe)
	build(6, Protoss Probe, 80)
	wait_build(6, Protoss Probe)
	build(7, Protoss Probe, 80)
	wait_build(7, Protoss Probe)
	build(8, Protoss Probe, 80)
	wait_build(8, Protoss Probe)
	build(9, Protoss Probe, 80)
	wait_build(9, Protoss Probe)
	build(10, Protoss Probe, 80)
	wait_build(10, Protoss Probe)
	build(11, Protoss Probe, 80)
	wait_build(11, Protoss Probe)
	build(12, Protoss Probe, 80)
	wait_build(12, Protoss Probe)
	build(13, Protoss Probe, 80)
	wait_build(13, Protoss Probe)
	build(14, Protoss Probe, 80)
	wait_build(14, Protoss Probe)
	build(15, Protoss Probe, 80)
	wait_build(15, Protoss Probe)
	stop()
	
	#Expand
		--natural_exe--
	start_town()
	build(1, Protoss Nexus, 80)
	wait_buildstart(1, Protoss Nexus)
	wait(1300)
	build(1, Protoss Pylon, 45)
	wait_build(1, Protoss Nexus)
	get_oldpeons(4)
	multirun(build_probe)
	build(1, Protoss Photon Cannon, 5)
	wait_buildstart(1, Protoss Photon Cannon)
	build(2, Protoss Photon Cannon, 5)
	wait_buildstart(2, Protoss Photon Cannon)
	stop()
	
		--second_exe--
	start_town()
	build(1, Protoss Nexus, 80)
	wait_buildstart(1, Protoss Nexus)
	wait(1300)
	build(1, Protoss Pylon, 45)
	wait_build(1, Protoss Nexus)
	get_oldpeons(4)
	multirun(build_probe)
	build(1, Protoss Photon Cannon, 5)
	wait_buildstart(1, Protoss Photon Cannon)
	build(2, Protoss Photon Cannon, 5)
	wait_build(2, Protoss Photon Cannon)
	stop()
		--exe_timer--
	wait(7200)
	expand(99, subsequent_exe)
	goto(exe_timer)
		--subsequent_exe--
	start_town()
	build(1, Protoss Nexus, 80)
	wait_buildstart(1, Protoss Nexus)
	wait(1300)
	build(2, Protoss Pylon, 60)
	wait_build(1, Protoss Nexus)
	get_oldpeons(15)
	multirun(build_probe)
	build(1, Protoss Photon Cannon, 5)
	wait_buildstart(1, Protoss Photon Cannon)
	build(2, Protoss Photon Cannon, 5)
	wait_build(2, Protoss Photon Cannon)
	stop()
	
	#research
	--r_disruption_web--
	wait(200)
	wait_train(1, Protoss Corsair)
	notowns_jump(Protoss Fleet Beacon, r_disruption_web)
	tech(Disruption Web, 80)
	stop()
	
	--r_arbiter--
	wait(200)
	notowns_jump(Protoss Arbiter Tribunal, r_arbiter)
	place_guard(Protoss Arbiter, 0)
	wait_force(1, Protoss Arbiter)
	tech(Stasis Field, 80)
	wait(1500)
	tech(Recall, 80)
	stop()
	
	--r_zealot_speed--
	wait(200)
	notowns_jump(Protoss Citadel of Adun, r_zealot_speed)
	upgrade(1, Leg Enhancements, 60)
	stop()
	
	--r_psi_storm--
	wait(200)
	notowns_jump(Protoss Templar Archives, r_psi_storm)
	wait_train(1, Protoss High Templar)
	tech(Psionic Storm, 80)
	stop()
	
	--r_goon_range--
	wait_train(1, Protoss Dragoon)
	upgrade(1, Singularity Charge, 80)
	stop()
	
	--r_units--
	upgrade(1, Protoss Ground Weapons, 50)
	wait(4000)
	upgrade(2, Protoss Ground Weapons, 50)
	wait(4500)
	upgrade(3, Protoss Ground Weapons, 50)
	wait(5000)
	upgrade(1, Protoss Plasma Shields, 50)
	wait(4000)
	upgrade(2, Protoss Plasma Shields, 50)
	wait(4500)
	upgrade(3, Protoss Plasma Shields, 50)
	wait(5000)
	upgrade(1, Protoss Armor, 50)
	wait(4000)
	upgrade(2, Protoss Armor, 50)
	wait(4500)
	upgrade(3, Protoss Armor, 50)
	wait(5000)
	stop()


	#misc
#	--stop_it--
#	wait(200)
#	stop()
	
	--invis_check--
	enemyowns_jump(Zerg Lurker, observer_build)
	enemyowns_jump(Terran Control Tower, observer_build)
	enemyowns_jump(Terran Covert Ops, observer_build)
	enemyowns_jump(Protoss Templar Archives, observer_build)
	wait(200)
	goto(invis_check)
	--air_check--
	enemyowns_jump(Terran Wraith, t_sairs)
	enemyowns_jump(Terran Science Vessel, t_sairs)
	enemyowns_jump(Terran Battlecruiser, t_sairs)
	enemyowns_jump(Zerg Mutalisk, t_sairs)
	enemyowns_jump(Protoss Observer, t_sairs)
	wait(200)
	goto(air_check)
	
	--essentials--
	wait_build(1, Protoss Gateway)
	player_need(1, Protoss Gateway)
	defenseuse_gg(1, Protoss Zealot)
	wait_build(1, Protoss Cybernetics Core)
	player_need(1, Protoss Cybernetics Core)
	defenseuse_gg(1, Protoss Dragoon)
    defenseuse_ga(1, Protoss Dragoon)
    defenseuse_ag(1, Protoss Dragoon)
    defenseuse_aa(1, Protoss Dragoon)
	wait_build(1, Protoss Citadel of Adun)
	player_need(1, Protoss Citadel of Adun)
	wait_build(1, Protoss Templar Archives)
	player_need(1, Protoss Templar Archives)
	defenseuse_gg(1, Protoss Dark Templar)
    defenseuse_ga(1, Protoss Dark Templar)
	wait_build(1, Protoss Stargate)
	player_need(1, Protoss Stargate)
	defenseuse_aa(1, Protoss Corsair)
    defenseuse_ag(1, Protoss Corsair)
	stop()

