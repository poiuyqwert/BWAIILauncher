# Storm Zerg 2.6, by Archon_Wing
# Island Scripts are the default Blizzard ones
## Courtesy of Broodwarai.com

# Zerg Expansion Custom Level
ZMCx(1344, 101, aiscript):

	start_town()
	transports_off()
	farms_notiming()

#if DEBUG
	debug(null, Storm Zerg 2.6 by Archon_Wing)
	--null--
#endif

	define_max(64, zergling)
	define_max(50, drone)
	define_max(39, mutalisk)
	define_max(12, guardian)
	define_max(3, devourer)
	define_max(255, scourge)
	define_max(20, hydralisk)
	define_max(2, defiler)
	define_max(10, ultralisk)
	define_max(4, lurker)
	build(1, drone, 150)
	wait_buildstart(1, drone)
	build(1, hatchery, 180)
	wait_buildstart(1, hatchery)
	build(1, overlord, 130)
	wait_buildstart(1, overlord)
	build(6, drone, 80)
	multirun(ling_speed)
	#random_jump(32, 6_pool)
	build(9, drone, 80)
	wait_buildstart(9, drone)
	enemyowns_jump(spawning_pool, 9_pool)
	#random_jump(64, overpool)
	enemyowns_jump(command_center, 12_pool)
	#random_jump(48, 12_pool)
	build(1, spawning_pool, 80)
	wait_buildstart(1, spawning_pool)
	build(11, drone, 80)
	multirun(ling pressure)
	#random_jump(64, ling all in)
	#default is overpool


	race_jump(early game, zvz early game, early game)
	
	--9_pool--
	build(1, spawning_pool, 130)
	wait_buildstart(1, spawning_pool)
	build(1, overlord, 80)
	wait_build(1, overlord)
	build(11, drone, 80)
	multirun(ling pressure)
	#random_jump(64, ling all in)

	race_jump(early game, zvz early game, early game)
	
	--ling all in--
	wait_build(1, spawning_pool)
	defensebuild_gg(1, zergling)
	defensebuild_ga(1, zergling)
	defenseuse_gg(1, zergling)
	defenseuse_ga(1, zergling)
	expand(1, low econ expo)
	build(1, extractor, 80)
	multirun(mass early ling)
	wait(600)
	farms_timing()
	wait(3500)
	send_suicide(0)
	build(13, drone, 80)
	multirun(main_defense)
	build(2, hatchery, 80)
	wait_build(1, spire)
	multirun(more expo)
	kill_thread()
	multirun(add muta)

	multirun(add ling)
	multirun(hydra antiair)
	multirun(add hydra)

	build(16, drone, 80)
	wait(3000)
	clear_combatdata()
	send_suicide(0)
	multirun(need den)
	multirun(evo_ups)
	multirun(air_up)

	multirun(mass hatch)

	if_owned(spire, mutaling)
	goto(lings)
	
	
	--mass early ling--
	killable()
	train(6, zergling)
	train(9, zergling)
	train(12, zergling)
	train(15, zergling)
	train(18, zergling)
	train(21, zergling)
	train(24, zergling)
	train(27, zergling)
	train(30, zergling)
    stop()
	

	










		--12_pool--
	multirun(ling pressure)
	build(2, overlord, 80)
	wait_build(2, overlord)
	build(11, drone, 80)
	wait_buildstart(11, drone)
	wait(200)
	rush(0, 12_pool 2)
	goto(12 hatch)
	--12_pool 2--

	build(12, drone, 80)
	wait_buildstart(12, drone)
	build(1, spawning_pool, 80)
	wait_buildstart(1, spawning_pool)
	wait(200)
	#random_jump(64, 12 pool all in)
	expand(1, natural)
	wait_build(1, spawning_pool)
	defenseuse_gg(2, zergling)
	defenseuse_ga(2, zergling)
	train(3, zergling)
	--early econ game--
	build(13, drone, 80)
	build(2, hatchery, 80)
	multirun(need den)
	multirun(hydra antiair)
	build(1, extractor, 80)
	defensebuild_gg(1, zergling)
	defensebuild_ga(1, zergling)
	farms_timing()

	multirun(evo_ups)
	multirun(air_up)
	player_need(1, extractor)
	train(5, zergling)
	multirun(main_defense)
	build(16, drone, 80)
	train(8, zergling)
	build(18, drone, 80)
	train(18, zergling)
	multirun(more expo)
	train(30, zergling)
	multirun(mass hatch)
	goto(lings)
	

	


	--12 hatch--
	build(12, drone, 80)
	wait_buildstart(12, drone)
	wait(120)
	#random_jump(64, 12 hatch all in)
	expand(1, natural)
	wait(120)
	build(1, spawning_pool, 80)
	wait_build(1, spawning_pool)
	defenseuse_gg(1, zergling)
	defenseuse_ga(1, zergling)
	train(3, zergling)
	goto(early econ game)
	


	--ling pressure--
	wait_buildstart(1, spawning_pool)

	wait(300)
	enemyowns_jump(barracks, nothing)
	wait(200)
	enemyowns_jump(gateway, nothing)
	rush(0, nothing)
	wait_build(1, spawning_pool)
	enemyowns_jump(photon_cannon, nothing)
	wait_train(6, zergling)
	send_suicide(0)
	stop()
	
	
	
	

		--early game--

	multirun(evo_ups)
	multirun(air_up)
	wait_build(1, spawning_pool)
	defenseuse_gg(2, zergling)
	defenseuse_ga(2, zergling)
	train(3, zergling)
	expand(1, natural)
	train(5, zergling)
	multirun(need den)
	multirun(hydra antiair)
	defensebuild_gg(1, zergling)
	defensebuild_ga(1, zergling)
	multirun(main_defense)
	build(2, hatchery, 80)
	build(14, drone, 80)
	build(1, extractor, 80)
	farms_timing()
	train(24, zergling)
	multirun(more expo)

	train(32, zergling)
	multirun(mass hatch)
	goto(lings)
	
			--zvz early game--
	random_jump(64, ling all in)

	multirun(evo_ups)
	multirun(air_up)
	defenseuse_gg(1, zergling)
	defenseuse_ga(1, zergling)
	train(3, zergling)
	build(2, hatchery, 80)
	build(12, drone, 80)
	farms_timing()
	build(13, drone, 80)
	train(4, zergling)
	defensebuild_gg(1, zergling)
	defensebuild_ga(1, zergling)
	multirun(main_defense)
	build(14, drone, 80)
	build(1, extractor, 80)

	multirun(hydra antiair)
	train(16, zergling)
	train(22, zergling)
	multirun(mass hatch)
	multirun(expand)

		--lings--
	multirun(add ling)
	multirun(add hydra)
	wait(3600)
	clear_combatdata()
	send_suicide(0)
	if_owned(greater_spire, guardians)
	if_owned(spire, mutaling)
	wait(1)
	goto(lings)

		--mutaling--
	resources_jump(400, 700, big_mutaling)
	multirun(add ling)
	multirun(add muta)
	multirun(add hydra)
	wait(5200)
	clear_combatdata()
	send_suicide(0)
	resources_jump(400, 800, hive tech)
	resources_jump(300, 400, big_mutaling)
	wait(1)
	if_owned(greater_spire, mutaling)
	wait(1)
	if_owned(spire, mutaling)
	wait(1)
	goto(lings)


		--big_mutaling--
	multirun(add ling)
	multirun(add big muta)
	multirun(add hydra)
	wait(6000)
	clear_combatdata()
	send_suicide(0)
	resources_jump(400, 800, hive tech)
	if_owned(spire, big_mutaling)
	wait(1)
	if_owned(greater_spire, big_mutaling)
	wait(1)
	goto(lings)


		--hive tech--
	wait(1)

	notowns_jump(hive, big_mutaling)
	race_jump(hive tech 2, zvz mass air, hive tech 2)
	--hive tech 2--
	random_jump(128, ultralisks)
	--guardians--

	notowns_jump(greater_spire, big_mutaling)
	multirun(add ling)
	multirun(add heavy air)
	multirun(add hydra)
	wait(6500)
	clear_combatdata()
	send_suicide(0)
	wait(1)

	resources_jump(400, 800, hive tech)
	resources_jump(300, 400, big_mutaling)
	wait(1)
	if_owned(spire, mutaling)
	wait(1)
	if_owned(greater_spire, mutaling)
	wait(1)
	goto(lings)

	--zvz mass air--
	notowns_jump(greater_spire, big_mutaling)
	define_max(48, mutalisk)
	multirun(add zvz heavy air)
	multirun(add ling)
	multirun(add hydra)
	wait(6500)
	clear_combatdata()
	send_suicide(0)
	resources_jump(400, 800, hive tech)
	resources_jump(300, 400, big_mutaling)
	wait(1)
	if_owned(spire, mutaling)
	wait(1)
	if_owned(greater_spire, mutaling)
	wait(1)
	goto(lings)
	
	--ultralisks--
	wait(10)
	notowns_jump(ultralisk_cavern, guardians)
	multirun(add ling)
	multirun(add ultra)
	multirun(add hydra)
	wait(6500)
	clear_combatdata()
	send_suicide(0)
	wait(1)
	resources_jump(400, 800, hive tech)
	resources_jump(300, 400, big_mutaling)
	wait(1)
	if_owned(spire, mutaling)
	wait(1)
	if_owned(greater_spire, mutaling)
	wait(1)
	goto(lings)
	
	--add ling--
	train(16, zergling)
	train(24, zergling)
	train(32, zergling)
	train(48, zergling)
	train(64, zergling)
	stop()
	
	--add hydra--
	notowns_jump(hydralisk_den, terminate)
	enemyowns_jump(stargate, more hydra)
	enemyowns_jump(starport, more hydra)
	enemyowns_jump(spire, more hydra)
	resources_jump(600, 300, more hydra)
	train(5, hydralisk)
	stop()
	
	--more hydra--
	train(5, hydralisk)
	train(10, mutalisk)
	train(15, mutalisk)
	train(20, hydralisk)
	stop()
	--add muta--
	if_owned(greater_spire, add muta 2)
	notowns_jump(spire, terminate)
	--add muta 2--
	train(3, mutalisk)
	train(6, mutalisk)
	train(9, mutalisk)
	train(12, mutalisk)
	stop()
	--add big muta--
	train(3, mutalisk)
	train(6, mutalisk)
	train(9, mutalisk)
	train(12, mutalisk)
	train(15, mutalisk)
	train(18, mutalisk)
	train(21, mutalisk)
	train(24, mutalisk)
	stop()
	--add heavy air--
	train(3, mutalisk)
	train(6, mutalisk)
	train(9, mutalisk)
	train(12, mutalisk)
	train(3, guardian)
	train(6, guardian)
	train(12, guardian)
	train(1, devourer)
	train(3, devourer)
	stop()
	--add ultra--
	train(10, ultralisk)
	--terminate--
	stop()
	--add zvz heavy air--
	train(30, mutalisk)
	train(5, devourer)
	train(40, mutalisk)
	stop()
	


#build more hatches





		--mass hatch--
	resources_jump(800, 0, build mass hatch)
	wait(500)

	goto(mass hatch)


		--build mass hatch--
	creep(3)
	build(3, hatchery, 50)
	wait_buildstart(3, hatchery)
	build(4, hatchery, 40)

	wait_build(4, hatchery)

		--mass hatch 2--
	resources_jump(1200, 0, build mass hatch 2)
	wait(500)

	goto(mass hatch 2)


		--build mass hatch 2--
	build(5, hatchery, 40)
	build(6, hatchery, 40)

	wait_build(6, hatchery)

		--mass hatch 3--
	resources_jump(2500, 0, build mass hatch 3)
	wait(500)

	goto(mass hatch 3)


		--build mass hatch 3--
	build(7, hatchery, 40)
	build(8, hatchery, 40)
	stop()
#more expos
		--expand--
	resources_jump(500, 0, build natural)
	time_jump(10, build natural)
	wait(100)

	goto(expand)


		--build natural--
	expand(1, natural)
	wait(5000)

	
	--more expo--
	wait(2000)
	expand(2, expo)

	--more expo 2--
	wait(6500)
	expand(99, expo)
	wait(10)
	goto(more expo 2)
#12 hatch/12 pool expo	
		--natural--
	start_town()
	defenseuse_gg(1, zergling)
	defenseuse_ga(1, zergling)
	build(1, hatchery, 100)
	wait_build(1, hatchery)
	multirun(expo stuff)	
	get_oldpeons(2)
	multirun(natural defenses)
	wait(5000)
	multirun(gas)
	multirun(replace)
	wait_train(1, hydralisk)
	player_need(1, hydralisk_den)
	stop()
	
	--gas--
	build(1, extractor, 35)
	stop()
	




	#all other expo

		--expo--
	start_town()
	build(1, hatchery, 130)
	wait_build(1, hatchery)
	multirun(expo stuff)	
	multirun(defenses)
	get_oldpeons(7)
	multirun(gas)
	multirun(replace)
	wait_train(1, hydralisk)
	player_need(1, hydralisk_den)
	stop()
	
	--low econ expo--
	start_town()
	build(1, hatchery, 100)
	wait_build(1, hatchery)
	multirun(expo stuff)
	wait(700)
	multirun(gas)
	build(1, creep_colony, 60)
	wait_build(1, creep_colony)
	player_need(1, spawning_pool)
	build(1, sunken_colony, 60)
	wait(1700)
	multirun(defenses)
	multirun(gas)
	multirun(replace)
	wait_train(1, hydralisk)
	player_need(1, hydralisk_den)
	stop()

		

	--replace--
	wait_train(1, mutalisk)
	--replace 2--
	notowns_jump(spire, replace spire)
	--replace 3--
	wait(1000)
	goto(replace 2)	
	
	--replace spire--
	if_owned(greater_spire, replace 3)
	player_need(1, spire)
	goto(replace 3)
	

	
	
			--expo stuff--
	build(6, drone, 75)
	wait_build(6, drone)
	build(9, drone, 65)

	wait_build(1, sunken_colony)
	
	build(13, drone, 25)
	wait_build(13, drone)
	build(2, hatchery, 30)
	wait_buildstart(2, hatchery)
	build(16, drone, 20)
	stop()


	--main_defense--
	player_need(1, spawning_pool)
	creep(4)
	build(1, creep_colony, 60)
	wait_build(1, creep_colony)
	build(1, sunken_colony, 60)
	wait_buildstart(1, sunken_colony)


		--main_spore--
	enemyowns_jump(stargate, build main_spore)
	enemyowns_jump(starport, build main_spore)
	enemyowns_jump(spire, build zvz main spore)
	wait(600)

	goto(main_spore)


		--build main_spore--
	player_need(1, extractor)
	build(1, evolution_chamber, 80)
	wait_build(1, evolution_chamber)

		--wait build main spore--
	wait(100)

	notowns_jump(evolution_chamber, wait build main spore)
	build(2, creep_colony, 90)

	wait_build(2, creep_colony)
	build(1, spore_colony, 90)
	build(3, creep_colony, 65)
	wait_build(3, creep_colony)
	build(2, spore_colony, 65)
	stop()
	--need den--
	enemyowns_jump(stargate, need den 2)
	enemyowns_jump(factory, need den 2)
	enemyowns_jump(spire, need den 2)
	wait(600)
	goto(need den)
	--need den 2--
	build(1, extractor, 80)
	wait_build(1, extractor)
	player_need(1, hydralisk_den)
	stop()


	
	--main zvz spore--
	enemyowns_jump(stargate, build main_spore)
	enemyowns_jump(starport, build main_spore)
	enemyowns_jump(spire, build zvz main spore)
	wait(400)

	goto(main zvz spore)


		--build zvz main spore--
	player_need(1, evolution_chamber)
	send_suicide(0)

		--wait zvz main spore--
	wait(100)

	notowns_jump(evolution_chamber, wait zvz main spore)
	creep(4)
	build(1, hydralisk_den, 80)
	build(2, creep_colony, 80)
	wait_buildstart(2, creep_colony)
	build(3, creep_colony, 80)
	wait_build(2, creep_colony)
	build(1, spore_colony, 80)
	wait_build(3, creep_colony)
	build(2, spore_colony, 80)
	wait_build(2, spore_colony)
	stop()
	#defense for 12 pool/12 hatch
		--natural defenses--
	wait_build(1, drone)
	
	race_jump(mass sunk, defenses, mass sunk)
	--mass sunk--
	creep(0)
	build(1, creep_colony, 90)
	wait_build(1, creep_colony)
	player_need(1, spawning_pool)
	build(1, sunken_colony, 90)
	wait_build(1, sunken_colony)
	creep(0)
	build(2, creep_colony, 90)
	wait_build(2, creep_colony)
	build(2, sunken_colony, 90)
	wait_build(2, sunken_colony)
	creep(0)
	build(3, creep_colony, 90)
	wait_build(3, creep_colony)
	creep(0)
	build(3, sunken_colony, 90)
	creep(0)
	build(4, creep_colony, 80)
	wait_build(4, creep_colony)

	build(4, sunken_colony, 80)
	creep(0)
	build(5, creep_colony, 80)
	wait_build(5, creep_colony)
	build(5, sunken_colony, 80)
	
	
	--natural antiair--
	enemyowns_jump(starport, expo spore)
	enemyowns_jump(stargate, expo spore)
	wait(1000)
	goto(natural antiair)
	
	--expo spore--
	build(6, creep_colony, 80)
	wait_build(6, creep_colony)
	build(1, spore_colony, 80)
	stop()
	
	#defense for other expos


		--defenses--
	player_need(1, spawning_pool)
	wait_build(1, drone)
	creep(0)
	build(1, creep_colony, 70)
	wait_build(1, creep_colony)
	build(1, sunken_colony, 70)
	creep(0)
	build(2, creep_colony, 50)
	wait_buildstart(2, creep_colony)
	race_jump(zvtp d, zvz d, zvtp d)

			--zvtp d--
	wait_build(2, creep_colony)
	build(2, sunken_colony, 80)
	creep(0)
	build(3, creep_colony, 80)
	wait_build(3, creep_colony)
	build(3, sunken_colony, 80)
	wait_build(3, sunken_colony)


#spores at expo
		--antiair--
	enemyowns_jump(stargate, spore)
	enemyowns_jump(starport, spore)
	resources_jump(1000, 0, spore)
	wait(1000)

	goto(antiair)


		--spore--
	player_need(1, evolution_chamber)
	notowns_jump(evolution_chamber, wait evo)
	
	build(4, creep_colony, 80)
	wait_build(4, creep_colony)
	build(1, spore_colony, 80)
	wait_build(16, drone)
	build(5, creep_colony, 30)
	wait(300)
	build(6, creep_colony, 30)
	wait_build(6, creep_colony)
	build(5, sunken_colony, 30)
	stop()
	
	--wait evo--
	wait(300)
	goto(spore)


		--zvz d--
	wait_build(1, creep_colony)
	build(1, sunken_colony, 80)
	wait_build(1, sunken_colony)

		--zvz antiair--
	enemyowns_jump(spire, zvz spore)
	wait(400)

	goto(zvz antiair)


		--zvz spore--
	player_need(1, evolution_chamber)
	creep(4)
	build(3, creep_colony, 80)
	wait_build(3, creep_colony)
	build(2, spore_colony, 80)
	stop()

#ling spd
		--ling_speed--
	resources_jump(0, 100, research_speed)
	wait(200)

	goto(ling_speed)


		--research_speed--
	upgrade(1, zergling_speed, 60)

#tech
		--lair--
	resources_jump(550, 100, build_lair)
	enemyowns_jump(command_center, build_lair)
	enemyowns_jump(lair, build_lair)
	enemyowns_jump(dark_templar, build_lair)
	time_jump(10, build_lair)
	wait(400)

	goto(lair)


		--build_lair--
	build(1, lair, 100)
	wait_build(1, lair)
	build(1, spire, 100)
	wait_build(1, spire)
	defenseuse_aa(1, mutalisk)
	defenseuse_gg(1, mutalisk)
	defenseuse_ga(1, mutalisk)
	defenseuse_ag(1, mutalisk)

	race_jump(get den, ol speed, get den)
	--get den--


	build(1, hydralisk_den, 100)
	wait_build(1, hydralisk_den)
	defensebuild_gg(1, hydralisk)
	defensebuild_ga(1, hydralisk)
	
	--ol speed--
	resources_jump(1000, 500, get ol speed)
	enemyowns_jump(lurker, get ol speed)
	enemyowns_jump(stargate, get ol speed)
	enemyowns_jump(starport, get ol speed)
	enemyowns_jump(dark_templar, get ol speed)
	wait(500)
	goto(ol speed)
	--get ol speed--

	upgrade(1, overlord_speed, 50)
	wait(2500)
	upgrade(1, overlord_capacity, 50)
	wait(2500)
	
			--need hive--
	resources_jump(400, 1000, hive)
	wait(500)

	goto(need hive)


		--hive--

	build(1, queen_nest, 60)
	wait_build(1, queen_nest)
	build(1, hive, 60)
	wait_build(1, hive)
	player_need(1, spawning_pool)
	wait(1)
	upgrade(1, zergling_attack, 90)
	wait(1)
	build(1, greater_spire, 50)
	wait_build(1, greater_spire)
	defenseuse_gg(1, guardian)
	defenseuse_ga(1, guardian)
	defenseuse_aa(1, devourer)
	defenseuse_ag(1, devourer)
	build(1, defiler_mound, 50)
	wait_build(1, defiler_mound)
	place_guard(defiler, 0)
	tech(plague, 60)
	place_guard(defiler, 1)
	wait(2900)
	upgrade(1, defiler_mana, 30)
	race_jump(get ultra, nothing, get ultra)
	--get ultra--
	build(1, ultralisk_cavern, 80)
	wait_build(1, ultralisk_cavern)
	defensebuild_gg(1, ultralisk)
	defensebuild_ga(1, ultralisk)
	defenseuse_gg(1, ultralisk)
	defenseuse_ga(1, ultralisk)
	wait_train(1, ultralisk)
	upgrade(1, ultralisk_armor, 60)
	wait(3000)

	upgrade(1, ultralisk_speed, 60)
	stop()

	--nothing-- 		
	stop()


		--evo_ups--
	wait_build(1, lair)
	player_need(1, evolution_chamber)
	wait(550)
	--evo_ups 2--
	wait(300)
	notowns_jump(evolution_chamber, evo_ups 2)
	upgrade(1, z_carapace, 60)
	wait(4500)

	upgrade(1, z_melee_attack, 60)
	wait(4500)

	upgrade(2, z_carapace, 60)
	player_need(2, evolution_chamber)
	wait(1200)
	upgrade(2, z_melee_attack, 60)

	wait(4800)

	wait_build(1, hive)
	upgrade(3, z_carapace, 60)
	upgrade(3, z_melee_attack, 60)
	wait(4500)
	wait_train(1, hydralisk)
	upgrade(1, z_missile_attack, 40)
	wait(4500)
	upgrade(2, z_missile_attack, 40)
	wait(4800)
	upgrade(3, z_missile_attack, 40)
	stop()


		--air_up--
	wait_train(10, mutalisk)
	wait_build(1, spire)
	upgrade(1, z_flyer_carapace, 30)
	wait(4500)

	upgrade(1, z_flyer_attack, 30)
	wait(4500)
	upgrade(2, z_flyer_carapace, 30)
	wait(4500)
	upgrade(2, z_flyer_attack, 30)
	wait(4500)
	wait_buildstart(1, greater_spire)
	player_need(1, spire)
	wait_build(1, greater_spire)

	upgrade(3, z_flyer_carapace, 30)
	wait_build(1, spire)
	upgrade(3, z_flyer_attack, 30)
	stop()






	--hydra antiair--
	wait_build(1, hydralisk_den)
	notowns_jump(hydralisk_den, build den)
	defensebuild_aa(2, hydralisk)
	defensebuild_ag(2, hydralisk)
	train(1, hydralisk)
	defenseuse_aa(1, hydralisk)
	defenseuse_ag(1, hydralisk)
	defenseuse_gg(1, hydralisk)
	defenseuse_ga(1, hydralisk)
	train(5, hydralisk)
	--hydra up--
	wait_train(5, hydralisk)
	wait(600)
	notowns_jump(hydralisk_den, hydra up)
	upgrade(1, hydralisk_range, 60)
	wait(2000)
	upgrade(1, hydralisk_speed, 60)
	wait_build(1, lair)
	race_jump(lurkers, terminate, lurkers)
	--lurkers--
	wait(2600)
	tech(lurker_aspect, 60)
	wait(2000)
	train(1, lurker)
	defenseuse_gg(1, lurker)
	defenseuse_ga(1, lurker)
	--train lurkers--
	train(4, lurker)
	wait(3700)
	goto(train lurkers)

	--build den--
	build(1, hydralisk_den, 80)
	goto(hydra antiair)

