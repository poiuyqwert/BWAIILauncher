# baja T v1.2
# Last Update Nov 13, 2009

TMCx(1342, 101, aiscript):

	start_town()
	farms_notiming()
	transports_off()
    
#if DEBUG
	debug(null, baja T v1.2)
 --null--
#endif

	build(1, command_center, 150)
	wait_buildstart(1, command_center)
	build(4, scv, 130)
	wait_buildstart(4, scv)

	define_max(40, scv)
	define_max(2, dropship)
	define_max(255, wraith)
	define_max(255, goliath)
	define_max(6, battlecruiser)
	define_max(6, science_vessel)
	
	build(7, scv, 80)
	wait_buildstart(7, scv)
	build(1, barracks, 80)
	wait_buildstart(1, barracks)
	defenseuse_gg(1, marine)
	defenseuse_ag(1, marine)
	build(8, scv, 80)
	wait_buildstart(8, scv)
	build(1, supply_depot, 80)
	wait_buildstart(1, supply_depot)
	build(9, scv, 80)
	wait_buildstart(9, scv)
		enemyowns_jump(nexus, vs_Toss)
		enemyowns_jump(spawning_pool, Z_rush)
		goto(skip_bunker)

--Z_rush--
	build(1, bunker, 80)
	wait_buildstart(1, bunker)
	train(1, marine)
	defensebuild_gg(1, marine)
	train(1, marine)

--skip_bunker--
	build(10, scv, 80)
	wait_buildstart(10,scv)
	build(2, barracks, 80)
	wait_buildstart(2, barracks)
	defensebuild_gg(1, marine)
	train(1, marine)
	build(11, scv, 80)
	wait_buildstart(11, scv)
	train(2, marine)
	build(12, scv, 80)
	wait_buildstart(12, scv)
	train(3, marine)
	build(2, supply_depot, 100)
	wait_buildstart(2, supply_depot)
	train(5, marine)
	build(1, academy, 80)
	wait_buildstart(1, academy)
	defenseuse_gg(1, firebat)
	train(7, marine)
	build(1, refinery, 80)
	wait_buildstart(1, refinery)
	build(13, scv, 80)
	wait_buildstart(13, scv)
	build(3, supply_depot, 80)
	wait_buildstart(3, supply_depot)
	build(14, scv, 80)
	wait_buildstart(14, scv)
	train(2, medic)
	train(11, marine)
	build(1, engineering_bay, 80)
	wait_buildstart(1, engineering_bay)
		multirun(ebay_upgrades)
	build(3, barracks, 80)
	wait_buildstart(3, barracks)
	attack_add(15, marine)
	attack_add(2, medic)
	attack_prepare()
	build(15, scv, 80)
	wait_buildstart(15, scv)
		multirun(academy_upgrades)
	attack_do()
	attack_clear()
	build(4, barracks, 80)
	wait_buildstart(4, barracks)
		multirun(comsat)
		farms_timing()
	train(8, firebat)
	train(4, medic)
	train(18, marine)
	train(26, marine)
	attack_add(30, marine)
	attack_add(8, medic)
	attack_add(14, firebat)
	wait(1000)
	attack_prepare()
	wait(2000)
	attack_do()
	attack_clear()
		expand(1, expo)

--Toss_return--
		multirun(scvs2)
	build(1, factory, 80)
	wait_buildstart(1, factory)
	build(1, machine_shop, 100)
	wait_buildstart(1, machine_shop)
	defenseuse_gg(1, siege_tank)
	place_guard(siege_tank, 0)
		multirun(m_shop_upgrades)
		multirun(primary_attack)
		multirun(expo_loop)
		
--resources_check--
	wait(1000)
		groundmap_jump(ground_map)
		goto(mid_game)

--ground_map--
		resources_jump(1000, 800, mid_game)
		goto(resources_check)
			
--mid_game--
	build(1, starport, 80)
	wait_buildstart(1, starport)
	build(1, control_tower, 80)
	wait_buildstart(1, control_tower)
		check_transports()
	build(1, science_facility, 80)
	wait_buildstart(1, science_facility)
		multirun(science)
	place_guard(science_vessel, 0)
	place_guard(science_vessel, 1)
	build(1, physics_lab, 80)
	wait_buildstart(1, physics_lab)
	defenseuse_gg(1, battlecruiser)
	build(1, armory, 80)
	wait_buildstart(1, armory)
		multirun(armory_upgrades)

--suicide_loop--
	wait(5000)
	train(3, battlecruiser)
	send_suicide(0)
		goto(suicide_loop)

###### ATTACKS #########################

--primary_attack--
		multirun(infantry)
	wait_train(24, marine)
	wait_train(3, medic)
	attack_add(20, marine)
	attack_add(8, firebat)
	attack_add(3, medic)
	attack_prepare()
	wait(1500)
	attack_do()
	attack_clear()
		notowns_jump(factory, primary_attack)
		multirun(tanks)
		notowns_jump(science_facility, primary_attack)

--tz_main_attack--
		multirun(infantry)
		multirun(tanks)
		multirun(sci_vessels)
	attack_add(4, siege_tank)
	attack_add(30, marine)
	attack_add(6, firebat)
	attack_add(4, medic)
	attack_add(2, science_vessel)
	attack_prepare()
	wait(2500)
	attack_do()
	attack_clear()
		goto(tz_main_attack)

--tech_attack--
	attack_add(30, marine)
	attack_add(8, medic)
	attack_add(10, firebat)
	attack_add(4, siege_tank)
	attack_add(10, ghost)
	attack_prepare()
	wait(2700)
	attack_do()
	attack_clear()
		multirun(infantry2)
		multirun(tanks)
		multirun(sci_vessels)
		goto(tech_attack)

--infantry--
	train(20, marine)
	train(3, medic)
	train(5, firebat)
	train(30, marine)
	train(6, medic)
	train(10, firebat)
	train(40, marine)
	stop()

--infantry2--
	train(20, marine)
	train(3, medic)
	train(5, ghost)
	train(5, firebat)
	train(30, marine)
	train(6, medic)
	train(10, ghost)
	train(10, firebat)
	train(40, marine)
	stop()

--tanks--
	train(2, siege_tank)
	wait(400)
	train(4, siege_tank)
	wait(400)
	train(6, siege_tank)
	wait(400)
	train(8, siege_tank)
	wait(400)
	stop()

--sci_vessels--
	train(1, science_vessel)
	wait(1000)
	train(2, science_vessel)
	stop()

##### EXPANSIONS #####

--expo_loop--
	wait(7000)
		expand(99, expo)
		goto(expo_loop)

--expo_loop2--
	wait(9000)
		expand(99, expo)
		goto(expo_loop2)

--expo--
	start_town()
	build(1, command_center, 80)
	wait_build(1, command_center)
	defenseuse_gg(1, marine)
	build(1, bunker, 70)
	wait_buildstart(1, bunker)
		multirun(comsat)
		multirun(gas)
		multirun(expo_buildings)
	build(6, scv, 80)
	wait_build(6, scv)
	wait(2000)
		goto(scvs2)

--expo2--
	start_town()
	build(1, command_center, 80)
	wait_build(1, command_center)
		multirun(gas)
	build(6, scv, 80)
	wait(2000)
		goto(scvs2)

--expo3--
	start_town()
	build(1, bunker, 80)
	wait_build(1, bunker)
	build(1, command_center, 80)
	wait_build(1, command_center)
	build(3, scv, 70)
	wait_build(3, scv)
		multirun(gas)
	build(6, scv, 70)
	wait_build(6, scv)
		multirun(comsat)
	build(10, scv, 70)
	wait_build(10, scv)
	build(2, bunker, 70)
	wait_buildstart(2, bunker)
	wait(200)
	build(3, bunker, 70)
	wait_buildstart(3, bunker)
	wait(1000)
	multirun(turrets)
	place_guard(siege_tank, 0)
	defenseuse_gg(1, siege_tank)
	build(12, scv, 70)
	wait_build(12,scv)

--expo_dudes--
	multirun(infantry)
	multirun(tanks)
	wait(500)
	goto(expo_dudes)

--scvs2--
	build(15, scv, 70)
	wait_build(15, scv)
	build(16, scv, 70)
	wait_build(16, scv)
	build(17, scv, 70)
	wait_build(17, scv)
	build(18, scv, 70)
	wait_build(18, scv)
	build(19, scv, 70)
	wait_build(19, scv)
	build(20, scv, 70)
	wait_build(20, scv)
	build(21, scv, 70)
	wait_build(21, scv)
	build(22, scv, 70)
	wait_build(22, scv)
	build(23, scv, 70)
	wait_build(23, scv)
	build(24, scv, 70)
	wait_build(24, scv)
	build(25, scv, 70)
	stop()

--expo_buildings--
		enemyowns_jump(command_center, expo_2factory)
	build(1, barracks, 80)
	wait_build(1, barracks)
	wait(1000)
		enemyowns_jump(nexus, expo_factory)
		notowns_jump(factory, expo_factory)
	build(2, barracks, 80)
	wait_buildstart(2, barracks)
		goto(late_game_starport)

--expo_factory--
	build(1, factory, 80)
	wait_build(1, factory)
	build(1, machine_shop, 70)
		goto(late_game_starport)

--expo_2factory--
	build(1, factory, 70)
	wait_build(1, factory)
	build(1, machine_shop, 70)
	wait_buildstart(1, machine_shop)
	build(2, factory, 70)
	wait_build(2, factory)
	build(2, machine_shop, 70)
	wait_buildstart(2, machine_shop)

--late_game_starport--
	wait(1500)
		notowns_jump(starport, late_game_starport)
	build(1, starport, 70)
	wait_build(1, starport)
	build(1, control_tower, 70)
	stop()

##### UPGRADES #######

--ebay_upgrades--
	wait_build(1, engineering_bay)
	upgrade(1, t_infantry_weapon, 80)
	wait(4500)
	upgrade(1, t_infantry_armor, 80)
	wait(4500)
	wait_build(1, science_facility)
	build(2, engineering_bay, 80)
	wait_buildstart(2, engineering_bay)
	upgrade(2, t_infantry_weapon, 80)
	wait_build(2, engineering_bay)
	upgrade(2, t_infantry_armor, 80)
	wait(4500)
	upgrade(3, t_infantry_armor, 80)
	upgrade(3, t_infantry_weapon, 80)
	stop()

--academy_upgrades--
	wait_build(1, academy)
	tech(stim_packs, 150)
	wait(2000)
	upgrade(1, marine_range, 70)
	wait(2000)
	upgrade(1, medic_mana, 70)
		enemyowns_jump(nexus, stop)
	wait(12000)
	tech(restoration, 70)
	stop()

--m_shop_upgrades--
	wait(1000)
		notowns_jump(machine_shop, m_shop_upgrades)
	tech(siege_mode, 70)
	wait(3000)
	wait_train(4, goliath)
	upgrade(1, goliath_range, 70)
	stop()

--armory_upgrades--
	upgrade(1, t_vehicle_weapon, 30)
	wait(4500)
	upgrade(1, t_vehicle_plating, 30)
	stop()

--ghost_upgrades--
	tech(lockdown, 70)
	wait(2000)
	upgrade(1, ghost_sight, 30)
	wait(2000)
	upgrade(1, ghost_mana, 30)
	stop()

--science--
		enemyowns_jump(nexus, PScience)
		enemyowns_jump(command_center, stop)
		enemyowns_jump(hatchery, ZScience)
--PScience--
	tech(emp_shockwave, 70)
	stop()

--ZScience--
	tech(irradiate, 70)
	stop()

################  Misc. ########

--detection--
	wait(1000)
		enemyowns_jump(citadel_of_adun, turrets)
		enemyowns_jump(templar_archives, turrets)
		goto(detection)

--turrets--
	build(1, missile_turret, 80)
	wait_buildstart(1, missile_turret)
	wait(200)
	build(2, missile_turret, 80)
	wait_buildstart(2, missile_turret)
	wait(200)
	build(3, missile_turret, 80)
	wait_buildstart(3, missile_turret)
	wait(200)
	build(4, missile_turret, 80)
	wait_buildstart(4, missile_turret)
	build(3, bunker, 80)
	wait(200)
	build(5, missile_turret, 80)
	wait_buildstart(5, missile_turret)
	wait(200)
	build(6, missile_turret, 80)
	wait_buildstart(6, missile_turret)
	wait(200)
	build(7, missile_turret, 80)
	wait_build(7, missile_turret)
	build(8, missile_turret, 80)
	wait(8000)
	expand(99, expo)
	stop()

--gas--
	build(1, refinery, 80)
	stop()

--comsat--
	build(1, comsat_station, 80)
	stop()

--stop--
	stop()	

############ Vs. Toss ######################

--vs_Toss--
	build(11, scv, 80)
	wait_buildstart(11,scv)
	build(2, barracks, 80)
	wait_buildstart(2, barracks)
	train(1, marine)
	build(12, scv, 80)
	wait_buildstart(12, scv)
		enemyowns_jump(gateway, standard_P)
		goto(P_FE)

--standard_P--
	train(2, marine)
	build(13, scv, 80)
	wait_buildstart(13, scv)
	train(3, marine)
	build(2, supply_depot, 100)
	wait_buildstart(2, supply_depot)
	train(5, marine)
	build(1, academy, 80)
	wait_buildstart(1, academy)
		enemyowns_jump(citadel_of_adun, dt_rush)
		enemyowns_jump(zealot, P_bunker)
		goto(skip_P_bunker)

--dt_rush--
	build(1, engineering_bay, 80)
	wait_buildstart(1, engineering_bay)
		multirun(turrets)
		multirun(comsat)
	build(1, bunker, 80)
	wait_buildstart(1, bunker)
		goto(skip_P_bunker)

--P_bunker--
	enemyowns_jump(cybernetics_core, P_rush2) 
	build(1, bunker, 80)
	wait_buildstart(1, bunker)

--skip_P_bunker--
	train(7, marine)
	build(14, scv, 80)
	wait_buildstart(14, scv)
		enemyowns_jump(shield_battery, P_bunker2)
		goto(skip_bunker2)

--P_bunker2--
	build(2, bunker, 80)
	wait_buildstart(2, bunker)

--skip_bunker2--
	build(3, supply_depot, 80)
	wait_buildstart(3, supply_depot)
	build(15, scv, 80)
	wait_buildstart(15, scv)
	train(11, marine)
	build(3, barracks, 80)
	wait_buildstart(3, barracks)
		enemyowns_jump(robotics_facility, reavers)
	build(1, refinery, 80)
	wait_buildstart(1, refinery)
	build(16, scv, 80)
	wait_buildstart(16, scv)
	build(1, bunker, 80)
	wait_buildstart(1, bunker)
	train(15, marine)
	build(4, barracks, 80)
	wait_buildstart(4, barracks)
	train(1, medic)
	train(1, firebat)
		multirun(comsat)
		multirun(academy_upgrades)
	attack_add(24, marine)
	attack_add(8, firebat)
	attack_add(6, medic)
	wait(2000)
	attack_prepare()
	build(1, engineering_bay, 80)
	wait_buildstart(1, engineering_bay)
	attack_do()
	attack_clear()
		expand(99, expo)
		multirun(ebay_upgrades)
		multirun(primary_attack)
		multirun(detection)
		goto(Toss_return)

--reavers--
	build(1, refinery, 80)
	wait_buildstart(1, refinery)
		expand(99, expo)
		expand(99, expo2)
	build(1, factory, 80)
	wait_buildstart(1, factory)
	build(1, machine_shop, 100)
	wait_buildstart(1, machine_shop)
	defenseuse_gg(1, siege_tank)
	place_guard(siege_tank, 0)
		multirun(m_shop_upgrades)
	build(1, armory, 80)
	wait_buildstart(1, armory)
	wait(200)
	build(1, starport, 80)
	wait_buildstart(1, starport)
	build(1, control_tower, 80)
	wait_buildstart(1, control_tower)
	tech(spider_mines, 70)
	train(8, vulture)
	build(1, science_facility, 80)
	wait_buildstart(1, science_facility)
	build(1, covert_ops, 80)
	wait_buildstart(1, covert_ops)	
		multirun(ghost_upgrades)
	build(1, engineering_bay, 80)
	wait_buildstart(1, engineering_bay)
		multirun(ebay_upgrades)
		multirun(academy_upgrades)
		check_transports()
		multirun(expo_loop)
	train(8, vulture)
	train(30, marine)
	train(6, medic)
	train(10, firebat)
	train(10, ghost)
	train(4, siege_tank)
	wait_train(3, ghost)
		goto(tech_attack)

--P_rush--
	train(2, marine)
	build(2, supply_depot, 80)
	wait_buildstart(2, supply_depot)
	train(4, marine)
	attack_add(4, marine)
	attack_prepare()
	wait(200)
	attack_do()
	attack_clear()
		goto(standard_P)

--P_rush2--
	train(7, marine)
	build(3, supply_depot, 80)
	wait_buildstart(3, supply_depot)
	train(11, marine)
	attack_add(11, marine)
	attack_prepare()
	wait(400)
	attack_do()
	attack_clear()

--P_FE--
		enemyowns_jump(zealot, P_FE_continue)
		enemyowns_jump(photon_cannon, P_FE_continue)
		enemyresources_jump(500, 0, P_rush)
--P_FE_continue--
	build(16, scv, 80)
	wait_buildstart(16, scv)
		expand(99, expo3)
	wait(1500)
	build(2, supply_depot, 80)
	wait_buildstart(2, supply_depot)
	train(3, marine)
	build(17, scv, 80)
	wait_buildstart(17, scv)
	build(1, academy, 80)
	wait_buildstart(1, academy)
		multirun(academy_upgrades)
		farms_timing()
	build(3, barracks, 80)
	wait_buildstart(3, barracks)
	train(4, marine)
	build(18, scv, 80)
	wait_buildstart(18, scv)
	build(1, engineering_bay, 80)
	wait_buildstart(1, engineering_bay)
		multirun(ebay_upgrades)
		multirun(comsat)
	wait_build(1, academy)
	train(3, medic)
	train(3, firebat)
	train(13, marine)
	build(4, barracks, 80)
	wait_buildstart(4, barracks)
	wait(200)
	build(5, barracks, 80)
	wait_buildstart(5, barracks)
		multirun(expo_loop2)
		multirun(primary_attack2)
	wait(6000)
	build(1, factory, 80)
	wait_buildstart(1, factory)
	build(1, machine_shop, 80)
	wait_buildstart(1, machine_shop)
		multirun(m_shop_upgrades)
		multirun(detection)
		goto(resources_check)

--primary_attack2--
		multirun(infantry)
	attack_add(30, marine)
	attack_add(12, firebat)
	attack_add(10, medic)
	wait(4000)
	attack_prepare()
	wait(800)
	attack_do()
	attack_clear()
		notowns_jump(factory, primary_attack2)
		multirun(tanks)
		attack_add(5, siege_tank)
		goto(primary_attack2)
		