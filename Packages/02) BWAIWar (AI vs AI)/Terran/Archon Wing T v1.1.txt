# Unleaded Terran AIvsAI v1.1
# by Archon_Wing

# Terran Expansion Custom Level
TMCx(1342, 101, aiscript):

	start_town()
	transports_off()
	farms_notiming()
	
	debug(null, Unleaded Tv1.1 by Archon_Wing)
	--null--
	
	define_max(80, marine)
	define_max(70, scv)
	define_max(16, medic)
	define_max(20, siege_tank)
	define_max(35, goliath)
	define_max(255, wraith)
	define_max(255, dropship)
	define_max(20, battlecruiser)
	define_max(12, science_vessel)
	define_max(4, valkyrie)
	define_max(40, ghost)
	define_max(1, nuclear_missile)
	define_max(12, firebat)
	define_max(30, vulture)
	build(1, command_center, 180)
	wait_buildstart(1, command_center)
	build(6, scv, 80)
	wait_buildstart(6, scv)
	multirun(comsat)
	multirun(cloak_detect)
	multirun(lair_detect)
	groundmap_jump(block1)
	goto(island)


		--block1--
#default to tvz in team games, if zerg is there
	enemyowns_jump(hatchery, tvz)
	race_jump(tvt, tvz, tvp)


		--tvt--
	define_max(6, marine)
	multirun(armory_up)
	multirun(shop upgrades)
	multirun(scv)
	multirun(air detect)
	wait_build(8, scv)
	#random_jump(24, bbs)
	multirun(tvt bunker)
	multirun(anti fe)
	build(1, supply_depot, 90)
	wait_build(11, scv)
	build(1, barracks, 90)
	build(1, refinery, 80)
	wait_buildstart(1, barracks)


	wait_build(1, barracks)
	defensebuild_gg(1, marine)
	defensebuild_ga(1, marine)
	defensebuild_aa(1, marine)
	defensebuild_ag(1, marine)
	defenseuse_gg(1, marine)
	defenseuse_ga(1, marine)
	defenseuse_aa(1, marine)
	defenseuse_ag(1, marine)
	train(1, marine)
	build(2, supply_depot, 80)
	train(2, marine)
	build(1, factory, 80)
	train(4, marine)
	wait_build(1, factory)
	train(1, vulture)
	defenseuse_gg(1, vulture)
	defenseuse_ga(1, vulture)
	farms_timing()
	#random_jump(24, wraith_rush)
	multirun(tvt_rush)
	build(1, machine_shop, 100)
	goto(tvt_fe)

	
	--tvt bunker--
	wait_build(8, scv)
	rush(0, build tvt bunker)
	wait_build(12, scv)
	rush(0, check gas tvt)
	--terminate--
	stop()
	
	--check gas tvt--
	enemyowns_jump(refinery, terminate)
	--build tvt bunker--
	build(1, barracks, 80)
	wait_build(1, barracks)
	defenseuse_gg(1, marine)
	train(1, marine)
	build(1, bunker, 80)
	train(3, marine)
	stop()


		--shop upgrades--
	wait_build(1, machine_shop)
	tech(siege_mode, 80)
	wait(1200)

	tech(spider_mines, 80)
	wait(1200)

	wait_build(1, armory)
	wait(250)
	upgrade(1, goliath_range, 80)
	stop()


		--tvt_rush--
# First rush in tvt, can be a feint
	wait_build(1, machine_shop)
	attack_add(6, siege_tank)
	attack_add(4, vulture)
	attack_add(4, marine)
	wait_train(4, vulture)

	attack_prepare()
	#random_jump(84, fake)
	attack_do()
	attack_clear()
	clear_combatdata()
	stop()





		--tvt_fe--
#1 fact expo with siege tank defense
	wait_build(1, factory)
	expand(1, fact_expo)
	build(1, machine_shop, 80)
	wait_build(1, machine_shop)
	train(1, siege_tank)
	defenseuse_gg(2, siege_tank)
	defenseuse_ga(2, siege_tank)
	defensebuild_gg(2, siege_tank)
	defensebuild_ga(2, siege_tank)
	build(2, factory, 80)
	train(3, siege_tank)
	build(2, machine_shop, 80)
	train(5, siege_tank)
	farms_timing()
	build(3, factory, 80)
	train(7, siege_tank)
	build(1, armory, 80)
	train(9, siege_tank)
	multirun(more_expo)
	wait_build(1, armory)
	train(2, goliath)
	defenseclear_gg()
	defenseclear_ga()
	defenseclear_aa()
	defenseclear_ag()
	defenseuse_gg(1, vulture)
	defenseuse_ga(1, vulture)
	defenseuse_gg(2, siege_tank)
	defensebuild_gg(2, siege_tank)
	defensebuild_ga(2, siege_tank)
	defenseuse_ga(1, siege_tank)
	defenseuse_ga(1, goliath)
	defenseuse_gg(1, goliath)
	defenseuse_aa(1, goliath)
	defenseuse_ag(1, goliath)
	defensebuild_aa(1, goliath)
	defensebuild_ag(1, goliath)
	train(10, siege_tank)
	train(5, goliath)
	build(5, factory, 80)
	build(1, starport, 80)
	wait_buildstart(1, starport)
	build(2, armory, 80)
	multirun(get_bc)
	goto(bc_war)





		--bigger tanks--
#for use before bc production can begin
	attack_add(15, siege_tank)
	attack_add(10, goliath)
	wait_force(12, siege_tank)
	attack_prepare()
	attack_do()
	attack_clear()
	clear_combatdata()
	wait(100)

	goto(bc_war2)


		--bc_war--
	multirun(ship_up)

		--bc_war2--
	notowns_jump(physics_lab, bigger tanks)
	notowns_jump(control_tower, bigger tanks)
	train(3, battlecruiser)
	defenseuse_ag(1, battlecruiser)
	defenseuse_aa(1, battlecruiser)
	defenseuse_gg(1, battlecruiser)
	defenseuse_ga(1, battlecruiser)
	defensebuild_aa(1, valkyrie)

		--bc_attack--
	define_max(5, dropship)
	attack_add(6, battlecruiser)
	attack_add(6, siege_tank)
	attack_add(15, goliath)
	wait_force(6, battlecruiser)
	attack_prepare()
	attack_do()
	attack_clear()
	clear_combatdata()
	resources_jump(900, 900, bc_assault)
	wait(200)

	goto(bc_attack)


		--bc_assault--
	define_max(5, dropship)
	attack_add(10, battlecruiser)
	attack_add(12, siege_tank)
	attack_add(12, goliath)
	wait_force(10, battlecruiser)
	attack_prepare()
	attack_do()
	attack_clear()
	clear_combatdata()
	wait(100)

	resources_jump(900, 900, bc_assault)
	wait(100)

	goto(bc_attack)


		--more_expo--
	wait(3500)
	expand(99, morefact_expo)
	wait(4000)

	goto(more_expo)


		--fact_expo--
	start_town()
	build(1, command_center, 150)
	wait_build(1, command_center)
	multirun(build_factory)
	multirun(expo fact turret)
	multirun(scv)
	get_oldpeons(10)
	wait_build(20, scv)
	multirun(expo_comsat)
	multirun(rebuild crap)
	multirun(rebuild bc crap)
	stop()


		--build_factory--
	player_need(1, barracks)
	build(1, factory, 40)
	wait_build(1, factory)
	build(1, machine_shop, 35)
	stop()
		--expo fact turret--
	player_need(1, engineering_bay)

		--wait for ebay--
	wait(200)

	notowns_jump(engineering_bay, wait for ebay)
	build(1, missile_turret, 80)

	wait(100)
	build(2, missile_turret, 30)
	
	--more expo fact turret--
	enemyowns_jump(physics_lab, build more expo fact turret)
	enemyowns_jump(fleet_beacon, build more expo fact turret)
	wait(700)
	goto(more expo fact turret)
	
	--build more expo fact turret--
	creep(0)
	wait(400)
	build(3, missile_turret, 80)
	wait_buildstart(3, missile_turret)
	build(4, missile_turret, 50)
	wait_buildstart(4, missile_turret)
	build(4, missile_turret, 50)
	wait_buildstart(4, missile_turret)
	build(4, missile_turret, 50)
	wait_buildstart(4, missile_turret)
	build(5, missile_turret, 50)
	wait_buildstart(5, missile_turret)
	build(6, missile_turret, 50)
	wait_buildstart(6, missile_turret)
	build(7, missile_turret, 50)
	stop()
	
	


		--morefact_expo--
	start_town()
	build(1, command_center, 120)
	wait_build(1, command_center)
	build(15, scv, 50)
	wait_buildstart(1, scv)
	get_oldpeons(10)
	multirun(build_factory)
	multirun(rebuild crap)
	multirun(rebuild bc crap)
	wait_build(15, scv)
	multirun(expo_addon)
	stop()



		--expo_addon--
#comsat is default, so nukes are actively discourged til resources are high
	notowns_jump(covert_ops, expo_comsat)
	notowns_jump(academy, expo_comsat)
	#random_jump(64, expo_comsat)
	resources_jump(400, 1600, nuke)

		--expo_comsat--
	wait_build(1, command_center)
	notowns_jump(academy, expo academy)
	player_need(1, academy)
	build(1, comsat_station, 90)
	stop()
	
	--expo academy--
	player_need(1, barracks)
	wait(1)
	player_need(1, academy)
	wait(150)
	goto(expo_comsat)

	--rebuild crap--
	wait_train(1, goliath)
	player_need(1, barracks)
	wait(1)
	player_need(1, factory)
	wait(1)
	player_need(1, armory)
	stop()

	--rebuild bc crap--
	wait_train(1, dropship)
	player_need(1, starport)
	wait(1)
	player_need(1, control_tower)
	wait(1)
	race_jump(rebuild lab, no bcs, no bcs)
	--rebuild lab--
	wait_train(1, battlecruiser)
	player_need(1, science_facility)
	wait(1)
	player_need(1, physics_lab)

	--no bcs--
	stop()


		--nuke--
	wait_train(6, ghost)
	build(1, nuclear_silo, 60)
	stop()


		--get_bc--
#get the tech for bcs ready
	wait_build(1, starport)
	build(1, science_facility, 80)
	build(1, control_tower, 80)
	define_max(2, dropship)
	
	wait_build(1, science_facility)
	wait_build(1, control_tower)
	check_transports()
	--get_bc 2--
	resources_jump(1000, 800, get_bc 3)
	wait(700)
	goto(get_bc 2)
	--get_bc 3--
	build(1, physics_lab, 80)
	wait_build(1, physics_lab)

	build(2, starport, 80)
	wait_build(2, starport)
	build(2, control_tower, 80)
	build(3, starport, 80)
	wait_build(3, starport)
	build(3, control_tower, 80)
	wait(5000)

	build(2, science_facility, 80)
	wait_build(2, science_facility)
	build(1, covert_ops, 80)
	race_jump(spy, don't get ghost, spy)


		--don't get ghost--
	wait(1)

	stop()

--tvz--
	multirun(pool_detect)
	multirun(scv)
	wait_build(8, scv)
	build(1, supply_depot, 90)
	wait_build(9, scv)
	build(1, barracks, 90)
	#random_jump(30, tvz_mech)
	build(2, barracks, 80)
	multirun(stim_range)
	wait_buildstart(13, scv)
	#random_jump(96, 2 rax)
	rush(0, 2 rax)
	#random_jump(192, 2 rax faster expo)
#1 rax expo
	multirun(get_vessel)
	wait_build(1, barracks)
	train(1, marine)
	defenseuse_gg(1, marine)
	defenseuse_ga(1, marine)
	defenseuse_aa(1, marine)
	defenseuse_ag(1, marine)
	defensebuild_gg(1, marine)
	defensebuild_ga(1, marine)
	defensebuild_aa(1, marine)
	defensebuild_ag(1, marine)
	farms_timing()
	train(5, marine)
	expand(1, tvz_natural)
	build(2, barracks, 100)
	train(8, marine)
	build(1, refinery, 80)
	build(1, academy, 80)
	train(14, marine)

	goto(fe)




		--2 rax--
	build(2, barracks, 80)
	wait_build(1, barracks)
	train(1, marine)
	defenseuse_gg(1, marine)
	defenseuse_ga(1, marine)
	defenseuse_aa(1, marine)
	defenseuse_ag(1, marine)
	train(2, marine)
	farms_timing()

	#random_jump(24, tvz_cheese)
	multirun(get_vessel)
	train(4, marine)
	build(1, refinery, 80)
	build(1, academy, 80)
	train(8, marine)
	defensebuild_gg(1, marine)
	defensebuild_ga(1, marine)
	defensebuild_aa(1, marine)
	defensebuild_ag(1, marine)
	expand(1, tvz_natural)


		--fe--
	multirun(tvz tanks)
	
	multirun(ebay_up)
	define_max(16, siege_tank)
	define_max(2, dropship)
	wait_build(1, academy)
	defensebuild_gg(1, firebat)
	train(1, firebat)
	defenseuse_gg(1, firebat)
	train(2, firebat)
	train(10, marine)
	build(3, barracks, 80)
	train(2, medic)
	train(15, marine)


	build(1, factory, 150)
	train(20, marine)

	#	--tvz_continue--
	multirun(tvz_moreexpo)
	multirun(tvz more structures)
	goto(tvz_push)
	










	
	--need factory--
	player_need(1, factory)
	wait(300)
	enemyowns_jump(hive, suicide)
	time_jump(21, suicide)
	goto(tvz_push a)
	--need shop--
	build(1, machine_shop, 100)
	wait_build(1, machine_shop)
	goto(tvz_push a)
	
	--suicide--
	attack_do()
	attack_clear()
	clear_combatdata()
	wait(100)
	goto(tvz_push)
	
	

		--tvz_push--
	attack_add(50, marine)
	attack_add(10, medic)
	attack_add(8, firebat)
	--tvz_push a--
	enemyowns_jump(ultralisk_cavern, start push)
	enemyowns_jump(greater_spire, start push)
	notowns_jump(factory, need factory)
	notowns_jump(machine_shop, need shop)
	attack_add(5, siege_tank)
	notowns_jump(science_facility, skip vessel)
	notowns_jump(science_facility, skip vessel)
	attack_add(1, science_vessel)

	--skip vessel--
	train(35, marine)
	train(3, siege_tank)
	--start push--
	attack_prepare()
	wait(100)
	attack_do()
	attack_clear()
	clear_combatdata()
	train(40, marine)
	train(5, siege_tank)
	wait(1)
	notowns_jump(science_facility, tvz_push)
	wait(1)
	notowns_jump(control_tower, tvz_push)
	resources_jump(300, 800, tvz_end)


	--tvz_push 2--
	attack_add(40, marine)
	attack_add(10, medic)
	attack_add(6, firebat)
	attack_add(3, siege_tank)
	attack_add(3, science_vessel)
	train(40, marine)
	train(5, siege_tank)

	attack_prepare()
	wait(100)
	attack_do()
	attack_clear()
	clear_combatdata()
	resources_jump(400, 2000, tvz_assault)
	resources_jump(300, 800, tvz_end)
	wait(1)

	goto(tvz_push 2)

	


		--tvz_end--
	attack_add(50, marine)
	attack_add(12, medic)
	attack_add(8, firebat)
	attack_add(7, siege_tank)
	attack_add(5, science_vessel)
	train(40, marine)
	train(5, siege_tank)

	attack_prepare()
	wait(100)
	attack_do()
	attack_clear()
	clear_combatdata()
	resources_jump(1000, 1500, tvz_assault)
	wait(1)
	resources_jump(300, 800, tvz_end)

	goto(tvz_push 2)


		--tvz_assault--
	define_max(4, dropship)
	attack_add(50, marine)
	attack_add(12, medic)
	attack_add(12, firebat)
	attack_add(12, science_vessel)
	attack_add(16, siege_tank)
	train(40, marine)
	train(5, siege_tank)
	attack_prepare()
	wait(100)
	attack_do()
	attack_clear()
	wait(1)

	resources_jump(500, 2500, tvz_assault)
	wait(1)

	goto(tvz_end)


		--tvz tanks--
	wait_build(1, factory)
	build(1, machine_shop, 95)
	wait_build(1, machine_shop)
	tech(siege_mode, 95)
	defenseuse_gg(1, siege_tank)
	defenseuse_ga(1, siege_tank)
	defensebuild_gg(1, siege_tank)
	defensebuild_ga(1, siege_tank)
	stop()





		--pool_detect--
	enemyowns_jump(spawning_pool, bunker)
	wait(50)

	goto(pool_detect)


		--bunker--
	player_need(1, barracks)
	wait_build(1, barracks)
	train(1, marine)
	defenseuse_gg(1, marine)
	build(1, bunker, 90)
	train(3, marine)
	stop()





		--tvz more structures--
	build(1, engineering_bay, 60)
	build(4, barracks, 60)
	wait(200)
	build(5, barracks, 60)
	wait(200)
	player_need(2, engineering_bay)


	--tvz more stuff--
	resources_jump(800, 0, get more stuff)
	wait(400)
	goto(tvz more stuff)
	--get more stuff--
	wait(100)
	wait_build(1, control_tower)
	wait_train(3, science_vessel)
	check_transports()
	build(2, factory, 80)
	wait_build(2, factory)
	build(2, machine_shop, 80)
	stop()



	


		--stim_range--
	wait_build(1, academy)
	tech(stim_packs, 50)
	wait(1250)

	upgrade(1, marine_range, 50)
	stop()


		


		--get_vessel--
	wait_train(16, marine)
	build(1, factory, 80)
	wait_build(1, factory)
	--get_vessel 2--
	enemyowns_jump(hive, get_vessel 3)
	time_jump(18, get_vessel 3)
	wait(500)
	goto(get_vessel 2)
	--get_vessel 3--
	player_need(1, starport)
	wait_build(1, starport)
	player_need(1, control_tower)
	wait(1)
	player_need(1, science_facility)
	wait_build(1, science_facility)
	wait_build(1, control_tower)
	train(1, science_vessel)
	tech(irradiate, 60)
	train(2, science_vessel)
	train(3, science_vessel)
	wait_train(3, science_vessel)
	build(2, starport, 60)
	train(4, science_vessel)
	wait_build(2, starport)
	build(2, control_tower, 60)
	train(5, science_vessel)
	wait(4600)
	train(6, science_vessel)
	upgrade(1, science_vessel_mana, 30)
	train(7, science_vessel)
	stop()


		--tvz_moreexpo--
	wait(3000)
	expand(99, tvz_expo)
	wait(3000)

	goto(tvz_moreexpo)


		--tvz_natural--
	start_town()
	build(1, command_center, 170)
	wait_build(1, command_center)
	player_need(1, barracks)
	multirun(scv)
	multirun(tvz_naturaldefense)
	get_oldpeons(7)
	multirun(natural turrets)
	wait_build(12, scv)
	multirun(gas)
	multirun(expo_comsat)
	multirun(natural barracks)
	stop()
	
	--gas--
	build(1, refinery, 50)
	stop()




		--tvz_naturaldefense--
	wait_build(1, command_center)
	build(1, bunker, 80)
	build(2, bunker, 50)
	wait_build(2, bunker)
	build(3, bunker, 25)
	stop()

	--natural turrets--
	enemyowns_jump(spire, build natural turrets)
	enemyowns_jump(lurker, build natural turrets)
	enemyowns_jump(templar_archives, build natural turrets)
	wait(600)
	goto(natural turrets)
	

		--build natural turrets--
	notowns_jump(engineering_bay, natural ebay)
	goto(build natural turrets 2)
	--natural ebay--
	player_need(1, engineering_bay)
	wait(400)
	goto(build natural turrets)
	--build natural turrets 2--
	creep(4)
	build(1, missile_turret, 70)
	wait_buildstart(1, missile_turret)
	build(2, missile_turret, 50)
	wait_build(2, missile_turret)


	--more natural turret--
	enemyowns_jump(mutalisk, build more natural turret)
	enemyowns_jump(carrier, build more expo fact turret)
	wait(600)
	goto(more natural turret)
	--build more natural turret--
	build(3, missile_turret, 50)
	wait_buildstart(3, missile_turret)
	build(4, missile_turret, 50)


	stop()
	


		--natural barracks--
	wait(4000)
	build(1, barracks, 60)
	wait_build(1, barracks)
	build(2, barracks, 60)
	stop()





		--tvz_expo--
	start_town()
	build(1, command_center, 95)
	wait_build(1, command_center)
	multirun(scv)
	get_oldpeons(12)
	multirun(tvz_expo_building)
	wait_build(15, scv)
	multirun(expo_addon)
	stop()




		--tvz_expo_building--
	player_need(1, engineering_bay)

		--tvz expo ebay--
	wait(100)

	notowns_jump(engineering_bay, tvz expo ebay)
	build(1, missile_turret, 60)
	build(1, barracks, 30)
	build(2, missile_turret, 60)
	build(2, barracks, 30)
	wait_build(2, barracks)
	build(3, barracks, 30)
	stop()


		--lair_detect--
	enemyowns_jump(lair, lair_defense)
	wait(400)

	goto(lair_detect)


		--lair_defense--
	wait(1000)

	goto(detection)
	





		--tvp--

	multirun(scv)
	multirun(anti fe)
	multirun(fleet_beacon_panic)
	wait_buildstart(9, scv)

	build(1, supply_depot, 80)
	wait_buildstart(10, scv)
	build(1, barracks, 80)
	wait_buildstart(12, scv)
	goto(tvp_mm)







		--tvp_support--
	wait_build(1, starport)

	build(1, control_tower, 80)
	build(1, science_facility, 80)
	--tvp_support 2--
	resources_jump(600, 1200, tvp air support)
	wait(1000)
	goto(tvp_support 2)
		--tvp air support--
	wait_build(1, science_facility)
	wait_build(1, control_tower)
	train(1, science_vessel)
	wait_train(1, science_vessel)
	tech(emp_shockwave, 60)
	wait(6000)
	train(2, science_vessel)
	wait(6000)
	train(3, science_vessel)
	stop()


		--tvp_mm--
	multirun(tvz tanks)
	multirun(stim_range)
	multirun(ebay_up)
	build(2, barracks, 80)
	wait_build(1, barracks)
	defenseuse_gg(1, marine)
	defenseuse_ga(1, marine)
	defenseuse_aa(1, marine)
	defenseuse_ag(1, marine)
	
	train(1, marine)
	build(2, supply_depot, 80)
	multirun(tvp bunker)

	train(4, marine)
	farms_timing()

	train(6, marine)
	build(1, refinery, 80)

	train(8, marine)
	build(1, academy, 80)
	train(12, marine)
	build(2, bunker, 60)
	train(14, marine)
	train(1, firebat)
	defenseuse_gg(1, firebat)
	train(3, firebat)
	train(16, marine)
	train(3, medic)
	build(1, factory, 100)
	build(1, engineering_bay, 80)
	multirun(tvp_mm turrets)
	
	# --tvp cheese--
	attack_add(16, marine)
	attack_add(3, medic)
	attack_add(3, firebat)
	wait(200)
	attack_prepare()
	attack_do()
	attack_clear()
	clear_combatdata()
	# --tvp_mm 3--
	train(20, marine)
	player_need(1, factory)
	expand(1, tvz_natural)
	build(3, barracks, 80)
	train(28, marine)
	build(1, machine_shop, 100)


	wait_build(1, machine_shop)
	train(1, siege_tank)
	train(2, vulture)
	defenseuse_gg(1, siege_tank)
	defenseuse_ga(1, siege_tank)
	build(4, barracks, 80)
	expand(99, tvz_expo)
	build(4, barracks, 80)
	train(3, firebat)
	train(30, marine)
	train(4, siege_tank)
	train(2, vulture)
	tech(optical_flare, 60)
	goto(tvp mm mid)


		--tvp mm mid--
	build(1, starport, 80)
	multirun(tvz more structures)
	multirun(tvp_support)
	multirun(need ghost)
	multirun(tvz_moreexpo)




		--deep 6--

	attack_add(35, marine)

	attack_add(7, medic)
	attack_add(5, siege_tank)
	notowns_jump(science_facility, deep 6a)
	attack_add(1, science_vessel)
	notowns_jump(covert_ops, deep 6a)
	attack_add(5, ghost)
	--deep 6a--
	wait(300)
	attack_prepare()
	attack_do()
	attack_clear()
	clear_combatdata()
	resources_jump(1000, 1000, deep 6 2)
	wait(1)

	goto(deep 6)



		--deep 6 2--
	wait(1)

	notowns_jump(covert_ops, deep 6)
	define_max(4, dropship)
	attack_add(55, marine)
	attack_add(12, ghost)
	attack_add(7, medic)
	attack_add(5, siege_tank)
	attack_add(1, science_vessel)
	wait(300)
	attack_prepare()
	attack_do()
	attack_clear()
	clear_combatdata()
	wait(1)

	resources_jump(1000, 1000, deep 6 2)
	resources_jump(600, 600, deep 6)

#detection and antiair

	--tvp bunker--
	enemyowns_jump(gateway, build tvp bunker)
	wait(120)
	goto(tvp bunker)
	
	--build tvp bunker--
	build(1, bunker, 70)
	stop()


		--cloak_detect--
	enemyowns_jump(templar_archives, detection)
	wait(400)

	goto(cloak_detect)


	--detection--
	build(1, academy, 100)
	goto(turrets)


		--air detect--
	enemyowns_jump(stargate, air counter)
	enemyowns_jump(starport, air counter)
	enemyowns_jump(shuttle, air counter)
	enemyowns_jump(spire, air counter)
	wait(500)

	goto(air detect)


		--air counter--
	build(1, engineering_bay, 80)
	build(1, academy, 80)
	wait_build(1, factory)
	build(1, armory, 100)
	wait_build(1, armory)
	defenseuse_gg(1, goliath)
	defenseuse_ga(1, goliath)
	defenseuse_aa(2, goliath)
	defenseuse_ag(2, goliath)
	defensebuild_aa(1, goliath)
	defensebuild_ag(1, goliath)
	goto(turrets)
	
	--tvp_mm turrets--
	enemyowns_jump(fleet_beacon, turrets)
	wait(1000)
	goto(tvp_mm turrets)
	

		--fleet_beacon_panic--
	enemyowns_jump(fleet_beacon, prepare_antiair)
	wait(1000)

	goto(fleet_beacon_panic)


		--prepare_antiair--
	wait_build(4, missile_turret)
	creep(0)
	build(5, missile_turret, 60)
	wait(100)
	build(6, missile_turret, 60)
	wait(100)
	build(7, missile_turret, 60)
	wait(100)
	build(8, missile_turret, 50)
	stop()


		--spy--
	enemyowns_jump(fleet_beacon, need ghost)
	resources_jump(0, 2000, need ghost)
	wait(400)

	goto(spy)


		--need ghost--
	wait_build(1, science_facility)
	build(1, covert_ops, 80)
	wait_build(1, covert_ops)
	wait(200)

	notowns_jump(academy, ghost academy)
	goto(build ghost)


		--ghost academy--
	build(1, academy, 80)
	wait_build(1, academy)

		--build ghost--
	tech(lockdown, 80)
	defensebuild_aa(1, ghost)
	defensebuild_ag(1, ghost)
	train(2, ghost)
	wait(1700)

	tech(personnel_cloaking, 60)
	train(4, ghost)
	wait(3000)

	upgrade(1, ghost_mana, 30)
	train(6, ghost)
	wait(1250)

	upgrade(1, ghost_sight, 20)
	stop()


		--turrets--
	notowns_jump(engineering_bay, build ebay)
	goto(turrets 2)
	--build ebay--
	build(1, engineering_bay, 80)
	wait_build(1, engineering_bay)
	--turrets 2--
	race_jump(tvtp placement,tvz placement, tvtp placement)
	--tvtp placement--
	creep(4)
	build(1, missile_turret, 80)
	wait(100)
	build(2, missile_turret, 80)
	wait(100)
	build(3, missile_turret, 50)
	wait(100)
	build(4, missile_turret, 30)
	stop()

	--tvz placement--
	creep(4)
	build(1, missile_turret, 80)
	wait(100)
	creep(0)
	build(2, missile_turret, 80)
	wait(100)
	creep(4)
	build(3, missile_turret, 80)
	wait(100)
	
	--tvz placement 2--
	enemyowns_jump(spire, place more turrets)
	wait(500)
	goto(tvz placement 2)
	--place more turrets--
	creep(0)
	build(4, missile_turret, 50)
	wait_build(4, missile_turret)
	creep(4)
	build(5, missile_turret, 50)
	wait_build(5, missile_turret)
	creep(0)
	build(6, missile_turret, 50)
	stop()
	
#upgrades

		--ebay_up--
	wait(500)

	notowns_jump(engineering_bay, ebay_up)
	upgrade(1, t_infantry_weapon, 60)
	wait(4500)

	upgrade(1, t_infantry_armor, 60)
	wait(3000)
	player_need(2, engineering_bay)
	wait(1500)

	wait_build(1, science_facility)
	upgrade(2, t_infantry_weapon, 60)


	upgrade(2, t_infantry_armor, 60)
	wait(4800)

	upgrade(3, t_infantry_weapon, 60)

	upgrade(3, t_infantry_armor, 60)
	stop()


		--armory_up--
		
	wait_build(1, armory)
	upgrade(1, t_vehicle_weapon, 50)
	wait(4500)
	upgrade(1, t_vehicle_plating, 50)
	wait(4500)

	wait_build(1, science_facility)
	build(2, armory, 80)
	wait_build(2, armory)
	upgrade(2, t_vehicle_weapon, 40)
	upgrade(2, t_vehicle_plating, 40)
	wait(4800)

	upgrade(3, t_vehicle_weapon, 40)
	upgrade(3, t_vehicle_plating, 30)
	stop()
	



		--ship_up--
	wait(2000)

	wait_build(2, armory)
	wait_build(1, physics_lab)
	build(3, armory, 80)
	wait_build(3, armory)

	upgrade(1, t_ship_weapon, 30)
	wait(5000)

	upgrade(1, t_ship_plating, 30)
	wait_build(1, science_facility)
	upgrade(2, t_ship_weapon, 30)
	wait(5000)

	upgrade(2, t_ship_plating, 30)
	wait(5000)

	upgrade(3, t_ship_weapon, 30)
	wait(5000)

	upgrade(3, t_ship_plating, 20)
	stop()


		--comsat--
	wait_build(1, academy)
	build(1, comsat_station, 80)
	stop()
#Early rush if no early military units (tvp/tvt)
	--anti fe--
	wait_buildstart(13, scv)
	rush(0, no early rush)
	#random_jump(64, no early rush)
	goto(crash fe)
	--no early rush--
	stop()

	--crash fe--
	wait_build(1, barracks)
	attack_add(2, marine)
	attack_do()
	attack_clear()
	attack_add(4, marine)
	attack_prepare()
	enemyowns_jump(zealot, cancel attack)
	enemyowns_jump(photon_cannon, cancel attack)
	attack_do()
	stop()

	
	--cancel attack--
	attack_clear()
	expand(1, tvz_natural)
	stop()
	

#Constant Scv production


		--scv--
	wait_build(1, command_center)
	build(1, scv, 80)
	wait_build(1, scv)
	build(2, scv, 80)
	wait_build(2, scv)
	build(3, scv, 80)
	wait_build(3, scv)
	build(4, scv, 80)
	wait_build(4, scv)
	build(5, scv, 80)
	wait_build(5, scv)
	build(6, scv, 80)
	wait_build(6, scv)
	build(7, scv, 80)
	wait_build(7, scv)
	build(8, scv, 80)
	wait_build(8, scv)
	build(9, scv, 80)
	wait_build(9, scv)
	build(10, scv, 80)
	wait_build(10, scv)
	build(11, scv, 80)
	wait_build(11, scv)
	build(12, scv, 80)
	wait_build(12, scv)
	build(13, scv, 80)
	wait_build(13, scv)
	build(14, scv, 80)
	wait_build(14, scv)
	build(15, scv, 80)
	wait_build(15, scv)
	build(16, scv, 80)
	wait_build(16, scv)
	build(17, scv, 80)
	wait_build(17, scv)
	build(18, scv, 80)
	wait_build(18, scv)
	build(19, scv, 80)
	wait_build(19, scv)
	build(20, scv, 80)
	wait_build(20, scv)
	build(21, scv, 80)
	wait_build(21, scv)
	build(22, scv, 80)
	wait_build(22, scv)
	build(23, scv, 80)
	wait_build(23, scv)
	build(24, scv, 80)
	wait_build(24, scv)
	build(25, scv, 80)
	wait_build(25, scv)
	stop()
#Island Maps


		--island--
	multirun(scv)
	multirun(get airexpo)
	define_max(1, dropship)
	define_max(20, wraith)
	define_max(4, valkyrie)
	wait_buildstart(9, scv)
	build(1, supply_depot, 80)
	build(1, refinery, 80)
	build(1, barracks, 80)
	farms_timing()
	build(1, factory, 70)
	wait_buildstart(1, factory)
	train(1, marine)
	defenseuse_gg(1, marine)
	defenseuse_ga(1, marine)
	defenseuse_aa(1, marine)
	defenseuse_ag(1, marine)
	wait_build(1, factory)
	build(1, armory, 80)
	train(1, vulture)
	defenseuse_gg(1, vulture)
	defenseuse_ga(1, vulture)
	build(1, bunker, 80)
	train(2, marine)
	train(2, vulture)
	build(1, machine_shop, 80)
	multirun(island up)
	build(2, factory, 80)
	wait_buildstart(2, factory)
	train(1, goliath)
	defenseuse_ga(1, goliath)
	defenseuse_gg(1, goliath)
	defenseuse_aa(1, goliath)
	defenseuse_ag(1, goliath)
	defensebuild_ga(1, goliath)
	defensebuild_gg(1, goliath)
	defensebuild_aa(1, goliath)
	defensebuild_ag(1, goliath)
	train(10, goliath)
	train(1, wraith)
	defenseuse_ag(1, wraith)
	defenseuse_aa(1, wraith)
	defenseuse_gg(1, wraith)
	defenseuse_ga(1, wraith)
	define_max(2, dropship)
	build(2, machine_shop, 60)
	train(5, vulture)
	multirun(island armory)
	defensebuild_aa(1, valkyrie)
	defensebuild_ag(1, valkyrie)
	multirun(island stuff)
	multirun(cloaking)
	define_max(5, dropship)
	defenseuse_gg(1, siege_tank)
	defenseuse_ga(1, siege_tank)
	defensebuild_gg(1, siege_tank)
	defensebuild_ga(1, siege_tank)
	multirun(bc defense)

		--island attack--
	attack_add(12, goliath)
	attack_add(3, siege_tank)
	attack_add(5, vulture)
	attack_add(3, wraith)
	wait(1200)

	attack_prepare()
	wait(100)

	attack_do()
	attack_clear()
	clear_combatdata()
	wait(100)

	notowns_jump(physics_lab, island attack)

		--bcs--
	define_max(6, dropship)
	attack_add(6, siege_tank)
	attack_add(24, goliath)
	attack_add(6, battlecruiser)

	attack_add(4, wraith)
	attack_add(4, valkyrie)
	wait(1600)

	attack_prepare()
	attack_do()
	attack_clear()
	clear_combatdata()
	wait(100)

	resources_jump(1200, 1200, late game)
	wait(100)

	goto(bcs)


		--late game--
	define_max(6, dropship)
	attack_add(10, battlecruiser)
	attack_add(6, siege_tank)
	attack_add(12, goliath)
	attack_add(2, wraith)
	attack_add(4, valkyrie)
	wait(2100)

	attack_prepare()
	wait(100)

	attack_do()
	attack_clear()
	clear_combatdata()
	wait(100)

	resources_jump(1200, 1200, late game)
	goto(bcs)


		--cloaking--
	wait_train(3, wraith)
	wait_build(1, control_tower)
	tech(cloaking_field, 20)
	stop()
	--bc defense--
	wait_train(1, battlecruiser)
	defenseuse_ag(1, battlecruiser)
	defenseuse_aa(1, battlecruiser)
	defenseuse_gg(1, battlecruiser)
	defenseuse_ga(1, battlecruiser)
	stop()

		--get airexpo--
	wait_build(1, factory)
	build(1, starport, 80)
	wait_build(1, starport)
	build(1, control_tower, 80)
	wait_build(1, control_tower)
	check_transports()
	wait_train(1, dropship)
	expand(1, air natural)
	wait(4400)
	expand(2, air expo)
	wait(1500)


		--more airexpo--
	expand(99, air expo)
	wait(6800)

	goto(more airexpo)


		--air natural--
	start_town()
	build(1, command_center, 130)
	wait_build(1, command_center)
	multirun(scv)
	get_oldpeons(7)
	multirun(airexpo stuff)
	multirun(expo_addon)
	stop()


		--air expo--
	start_town()
	build(1, command_center, 100)
	wait_build(1, command_center)
	multirun(scv)
	multirun(airexpo stuff)
	get_oldpeons(10)
	multirun(expo_addon)
	stop()


		--airexpo stuff--
	player_need(1, factory)
	build(1, starport, 30)
	player_need(1, engineering_bay)
	build(1, missile_turret, 30)
	build(1, factory, 30)
	build(1, missile_turret, 30)
	wait_build(1, starport)
	build(1, control_tower, 30)
	build(2, missile_turret, 30)
	wait(200)
	build(3, missile_turret, 30)
	wait_build(1, factory)
	build(1, machine_shop, 30)
	build(4, missile_turret, 30)
	wait(200)
	build(5, missile_turret, 30)
	wait(200)
	build(6, missile_turret, 30)
	stop()


		--island up--
	wait_build(1, armory)
	wait_build(1, machine_shop)
	upgrade(1, goliath_range, 60)
	wait(1800)

	tech(siege_mode, 60)
	wait(1800)

	tech(spider_mines, 60)
	stop()


		--island armory--
	wait_build(1, armory)
	upgrade(1, t_vehicle_weapon, 60)
	player_need(2, armory)
	wait(1300)
	upgrade(1, t_vehicle_plating, 50)
	wait(5000)

	upgrade(1, t_ship_weapon, 50)
	upgrade(1, t_ship_plating, 50)
	wait(5000)

	wait_build(1, science_facility)
	upgrade(2, t_ship_weapon, 40)
	upgrade(2, t_ship_plating, 40)
	wait(5000)

	upgrade(3, t_ship_weapon, 30)
	upgrade(3, t_ship_plating, 30)
	wait(5000)

	upgrade(2, t_vehicle_weapon, 30)
	upgrade(2, t_vehicle_plating, 30)
	wait(5000)

	upgrade(3, t_vehicle_weapon, 30)
	upgrade(3, t_vehicle_plating, 30)
	stop()


		--island stuff--
	build(3, factory, 80)
	wait(100)
	build(4, factory, 80)
	wait_build(4, factory)
	build(2, machine_shop, 50)
	build(1, science_facility, 80)
	build(2, starport, 80)
	wait_build(1, science_facility)
	train(2, science_vessel)
	race_jump(get_bc 3, irradiate, emp)


		--irradiate--
	tech(irradiate, 60)
	train(7, science_vessel)
	goto(get_bc 3)


		--emp--
	tech(emp_shockwave, 60)
	goto(get_bc 3)


