# Peppe P v1.0

# Protoss Expansion Custom Level
PMCx(1343, 101, aiscript):

    start_town()
    transports_off()
    farms_notiming()

#if DEBUG
    debug(null, Peppe P v1.0)
    --null--
#endif
    
    define_max(44, probe)
    define_max(50, zealot)
    define_max(20, dragoon)
    define_max(20, dark_templar)
    define_max(2, high_templar)
    define_max(4, archon)
    define_max(8, corsair)
    define_max(2, shuttle)
    define_max(2, arbiter)
    define_max(4, observer)
    define_max(4, reaver)
    define_max(4, dark_archon)
    define_max(0, scout)
    define_max(8, carrier)
    groundmap_jump(groundmap)
    goto(air_map)

        --groundmap--
    multirun(use_ground)
    multirun(use_air)
    race_jump(open_terran, open_zerg, open_protoss)

        --air_map--
    multirun(add_shuttle)
    race_jump(open_terran, open_zerg, open_protoss)

# ============== Common Functions ==============
        --use_ground--
    defenseuse_gg(1, zealot)
    defenseuse_ga(1, zealot)
    defenseuse_gg(1, dark_templar)
    defenseuse_ga(1, dark_templar)
    defenseuse_gg(1, reaver)
    defenseuse_ga(1, reaver)
    defenseuse_gg(1, dragoon)
    defenseuse_ga(1, dragoon)
    defenseuse_ag(1, dragoon)
    defenseuse_aa(1, dragoon)
    defenseuse_gg(1, high_templar)
    defenseuse_ga(1, high_templar)
    defenseuse_ag(1, high_templar)
    defenseuse_aa(1, high_templar)
    defenseuse_gg(1, archon)
    defenseuse_ga(1, archon)
    defenseuse_ag(1, archon)
    defenseuse_aa(1, archon)
    stop()

        --use_air--
    defenseuse_aa(1, carrier)
    defenseuse_ag(1, carrier)
    defenseuse_ga(1, carrier)
    defenseuse_gg(1, carrier)
    defenseuse_aa(1, scout)
    defenseuse_ag(1, scout)
    defenseuse_ga(1, scout)
    defenseuse_gg(1, scout)
    defenseuse_aa(1, corsair)
    defenseuse_ag(1, corsair)
    defenseuse_aa(1, arbiter)
    defenseuse_ag(1, arbiter)
    defenseuse_ga(1, arbiter)
    defenseuse_gg(1, arbiter)
    stop()

        --full_probes--
    wait_build(1, nexus)
    build(1, probe, 1)
    wait_build(1, probe)
    build(2, probe, 1)
    wait_build(2, probe)
    build(3, probe, 1)
    wait_build(3, probe)
    build(4, probe, 1)
    wait_build(4, probe)
    build(5, probe, 1)
    wait_build(5, probe)
    build(6, probe, 1)
    wait_build(6, probe)
    build(7, probe, 1)
    wait_build(7, probe)
    build(8, probe, 1)
    wait_build(8, probe)
    build(9, probe, 1)
    wait_build(9, probe)
    build(10, probe, 1)
    wait_build(10, probe)
    build(11, probe, 1)
    wait_build(11, probe)
    build(12, probe, 1)
    wait_build(12, probe)
    build(13, probe, 1)
    wait_build(13, probe)
    build(14, probe, 1)
    wait_build(14, probe)
    build(15, probe, 1)
    wait_build(15, probe)
    build(16, probe, 1)
    wait_build(16, probe)
    build(17, probe, 1)
    wait_build(17, probe)
    build(18, probe, 1)
    wait_build(18, probe)
    build(19, probe, 1)
    wait_build(19, probe)
    build(20, probe, 1)
    wait_build(20, probe)
    stop()

# ============== Opening Zerg =============
        --open_zerg--
    goto(3gate_zeal)

# ============== Opening Terran =============
        --open_terran--
    goto(3gate_zeal_T)


# ============== Opening Protoss =============
        --open_protoss--
    goto(3gate_zeal_T)

# ============== 3gate_zeal ==============
        --3gate_zeal--
    build(1, nexus, 2)
    wait_build(1, nexus)
    multirun(full_probes)
    wait_buildstart(8, probe)
    build(1, pylon, 1)
    wait_buildstart(1, pylon)
    build(1, gateway, 1)
    wait_buildstart(1, gateway)
    wait_buildstart(12, probe)
    multirun(train_zealot)
    build(3, gateway, 1)
    wait_buildstart(3, gateway)
    farms_timing()
    wait_train(3, zealot)
    build(1, assimilator, 1)
    wait_buildstart(1, assimilator)
    multirun(forge_upgrades)
    multirun(stealth_check)
    multirun(zealot_attack_Z)
    wait_train(6, zealot)
    build(1, cybernetics_core, 1)
    wait_buildstart(1, cybernetics_core)
    multirun(citadel_upgrade)
    wait_train(15, zealot)
    expand(1, exp_2)
    wait(150)
    multirun(add_high_templar)
    multirun(add_dark_templar)
    multirun(zealot_main_attack)
    wait(500)
    build(4, gateway, 1)
    wait_buildstart(4, gateway)
    multirun(stealth_check_2)
    wait(5000)
    multirun(begin_expand)
    wait(500)
    multirun(build_photon)
    wait(3000)
    multirun(basic_air)
    multirun(suicide_loop)
    stop()

        --3gate_zeal_T--
    build(1, nexus, 2)
    wait_build(1, nexus)
    multirun(full_probes)
    wait_buildstart(8, probe)
    build(1, pylon, 1)
    wait_buildstart(1, pylon)
    build(1, gateway, 1)
    wait_buildstart(1, gateway)
    wait_buildstart(12, probe)
    multirun(train_zealot)
    farms_timing()
    build(1, assimilator, 1)
    wait_buildstart(1, assimilator)
    multirun(stealth_check)
    build(1, cybernetics_core, 1)
    wait_buildstart(1, cybernetics_core)
    multirun(add_reaver)
    wait(150)
    multirun(add_dragoon)
    wait(150)
    multirun(citadel_upgrade)
    wait(150)
    multirun(build_forge)
    wait(150)
    wait(5000)
    multirun(begin_expand)
    wait_train(1, reaver)
    multirun(zealot_main_attack_T)
    wait_train(2, reaver)
    multirun(add_shuttle)
    multirun(stealth_check_2)
    build(2, gateway, 1)
    wait_buildstart(2, gateway)
    wait(6000)
    multirun(forge_upgrades)
    wait(500)
    multirun(build_photon)
    multirun(add_dark_templar)
    wait(3000)
    build(3, gateway, 1)
    wait_buildstart(3, gateway)
    multirun(basic_air)
    wait(3000)
    multirun(suicide_loop)
    stop()

        --suicide_loop--
    wait(20000)
    send_suicide(0)
    wait(300)
    goto(suicide_loop)

        --zealot_attack_Z--
    wait_train(14, zealot)
    attack_add(8, zealot)
    attack_prepare()
    wait(450)
    attack_do()
    attack_clear()
    stop()

        --zealot_main_attack--
    wait_train(20, zealot)
    wait_train(4, archon)
    wait_train(10, dark_templar)
    attack_add(30, zealot)
    attack_add(4, archon)
    attack_add(4, dark_templar)
    attack_prepare()
    wait(1500)
    attack_do()
    attack_clear()
    wait(150)
    goto(zealot_main_attack)

        --zealot_main_attack_T--
    wait_train(20, zealot)
    wait_train(10, dragoon)
    wait_train(1, reaver)
    attack_add(20, zealot)
    attack_add(10, dragoon)
    attack_add(1, reaver)
    attack_prepare()
    wait(3000)
    attack_do()
    attack_clear()
    wait(150)
    goto(zealot_main_attack_T)


# ============== Expansions ==============
        --begin_expand--
    expand(99, exp_2)
    wait(7000)
    goto(begin_expand)

        --exp_2--
    start_town()
    build(1, nexus, 50)
    wait_buildstart(1, nexus)
    multirun(full_probes)
    wait(500)
    build(1, pylon, 1)
    wait_build(1, pylon)
    wait(150)
    creep(3)
    build(1, photon_cannon, 1)
    wait_buildstart(1, photon_cannon)
    wait_build(1, nexus)
    get_oldpeons(6)
    build(1, assimilator, 1)
    wait(300)
    creep(3)
    build(2, photon_cannon, 1)
    wait_buildstart(2, photon_cannon)
    wait(900)
    creep(3)
    build(3, photon_cannon, 1)
    wait_buildstart(3, photon_cannon)
    build(1, shield_battery, 1)
    wait(900)
    build(1, gateway, 1)
    wait_buildstart(1, gateway)
    wait(1500)
    build(2, pylon, 1)
    wait_build(2, pylon)
    wait(1000)
    creep(3)
    build(4, photon_cannon, 1)
    wait_buildstart(4, photon_cannon)
    wait(1000)
    creep(3)
    build(5, photon_cannon, 1)
    wait_buildstart(5, photon_cannon)
    build(2, shield_battery, 1)
    wait(6000)
    goto(player_needs)

# ============== Upgrades ==============
        --citadel_upgrade--
    build(1, gateway, 1)
    wait_build(1, gateway)
    build(1, cybernetics_core, 1)
    wait_build(1, cybernetics_core)
    build(1, citadel_of_adun, 1)
    wait_build(1, citadel_of_adun)
    upgrade(1, zealot_speed, 100)
    stop()

        --cyber_dragoon_range--
    wait_train(2, dragoon)
    wait_build(1, cybernetics_core)
    upgrade(1, dragoon_range, 100)
    stop()

        --cyber_upgrades_air--
    wait_train(6, corsair)
    build(1, cybernetics_core, 1)
    wait_build(1, cybernetics_core)
    upgrade(1, p_air_weapon, 1)
    wait(6000)
    upgrade(1, p_plating, 1)
    wait(12000)
    build(1, stargate, 1)
    wait_build(1, stargate)
    build(1, fleet_beacon, 1)
    wait_build(1, fleet_beacon)
    wait_build(1, cybernetics_core)
    upgrade(2, p_air_weapon, 1)
    wait(4500)
    upgrade(2, p_plating, 1)
    wait(4500)
    wait_build(1, fleet_beacon)
    wait_build(1, cybernetics_core)
    upgrade(3, p_air_weapon, 1)
    wait(4500)
    wait_build(1, fleet_beacon)
    wait_build(1, cybernetics_core)
    upgrade(3, p_plating, 1)
    stop()

        --forge_upgrades--
    build(1, forge, 1)
    wait_build(1, forge)
    upgrade(1, p_ground_weapon, 100)
    wait(4500)
    wait_build(1, forge)
    upgrade(2, p_ground_weapon, 100)
    wait(4500)
    wait_build(1, forge)
    upgrade(3, p_ground_weapon, 100)
    wait(4500)
    wait_build(1, forge)
    upgrade(1, p_plasma_shield, 100)
    wait(4500)
    wait_build(1, forge)
    upgrade(2, p_plasma_shield, 100)
    wait(4500)
    wait_build(1, forge)
    upgrade(3, p_plasma_shield, 100)
    wait(4500)
    wait_build(1, forge)
    upgrade(1, p_armor, 1)
    wait(4500)
    wait_build(1, forge)
    upgrade(2, p_armor, 1)
    wait_build(1, forge)
    wait(4500)
    upgrade(3, p_armor, 1)
    stop()

        --templar_research--
    wait_train(3, high_templar)
    wait_build(1, templar_archives)
    tech(psionic_storm, 1)
    wait(4500)
    build(1, templar_archives, 1)
    wait_build(1, templar_archives)
    upgrade(1, templar_mana, 1)
    wait(4500)
    wait_train(1, dark_archon)
    build(1, templar_archives, 1)
    wait_build(1, templar_archives)
    tech(mind_control, 1)
    wait(4500)
    wait_train(1, dark_archon)
    build(1, templar_archives, 1)
    wait_build(1, templar_archives)
    tech(maelstrom, 1)
    wait(4500)
    wait_train(1, dark_archon)
    build(1, templar_archives, 1)
    wait_build(1, templar_archives)
    upgrade(1, dark_archon_mana, 1)
    stop()

        --observer_research--
    wait_train(2, observer)
    wait_build(1, observatory)
    upgrade(1, observer_speed, 1)
    wait(6000)
    wait_build(1, observatory)
    upgrade(1, observer_sight, 1)
    stop()

        --research_reaver--
    wait_train(2, reaver)
    wait_build(1, robotics_support_bay)
    upgrade(1, scarab_damage, 1)
    wait(6000)
    wait_build(1, robotics_support_bay)
    wait_train(1, shuttle)
    upgrade(1, shuttle_speed, 1)
    stop()

        --research_carrier--
    wait_build(1, fleet_beacon)
    wait_train(1, carrier)
    upgrade(1, carrier_capacity, 80)
#wait 4500
#Scout Upgrades
#Corsair Upgrades
    stop()


# ============== Add Units ==============
        --build_forge--
    build(1, forge, 1)
    wait_build(1, forge)
    wait(300)
    stop()

        --build_photon--
    build(1, forge, 1)
    wait_build(1, forge)
    creep(3)
    build(1, photon_cannon, 1)
    wait_buildstart(1, photon_cannon)
    wait(300)
    creep(3)
    build(2, photon_cannon, 1)
    wait_buildstart(2, photon_cannon)
    wait(600)
    creep(3)
    build(3, photon_cannon, 1)
    wait_buildstart(3, photon_cannon)
    wait(900)
    build(2, pylon, 1)
    wait_build(2, pylon)
    wait(1200)
    creep(3)
    build(4, photon_cannon, 1)
    wait_buildstart(4, photon_cannon)
    wait(1500)
    creep(3)
    build(5, photon_cannon, 1)
    wait_buildstart(5, photon_cannon)
    wait(1800)
    creep(3)
    build(6, photon_cannon, 1)
    wait_buildstart(6, photon_cannon)
    stop()

        --add_dragoon--
    build(1, gateway, 1)
    wait_build(1, gateway)
    build(1, cybernetics_core, 1)
    wait_build(1, cybernetics_core)
    build(1, cybernetics_core, 1)
    wait_build(1, cybernetics_core)
    multirun(train_dragoon)
    multirun(cyber_dragoon_range)
    stop()

        --stealth_check--
    enemyowns_jump(ghost, build_photon)
    enemyowns_jump(wraith, build_photon)
    enemyowns_jump(dark_templar, build_photon)
    enemyowns_jump(lurker, build_photon)
    enemyowns_jump(arbiter, build_photon)
    wait(300)
    goto(stealth_check)

        --stealth_check_2--
    enemyowns_jump(ghost, build_observers)
    enemyowns_jump(wraith, build_observers)
    enemyowns_jump(dark_templar, build_observers)
    enemyowns_jump(lurker, build_observers)
    enemyowns_jump(arbiter, build_observers)
    wait(300)
    goto(stealth_check_2)

        --build_observers--
    build(1, robotics_facility, 100)
    wait_build(1, robotics_facility)
    build(1, observatory, 100)
    multirun(observer_research)
    goto(train_observer)

        --basic_air--
    build(1, cybernetics_core, 1)
    wait_build(1, cybernetics_core)
    race_jump(carrier_air, corsair_air, corsair_air)

        --add_reaver--
    build(1, robotics_facility, 1)
    wait_build(1, robotics_facility)
    build(1, robotics_support_bay, 1)
    wait_build(1, robotics_support_bay)
    multirun(research_reaver)
    goto(train_reaver)

        --add_shuttle--
    build(1, cybernetics_core, 1)
    wait_build(1, cybernetics_core)
    build(1, robotics_facility, 1)
    wait_build(1, robotics_facility)
    train(2, shuttle)
    check_transports()
    stop()

        --add_high_templar--
    build(1, cybernetics_core, 1)
    wait_build(1, cybernetics_core)
    build(1, citadel_of_adun, 2)
    wait_build(1, citadel_of_adun)
    build(1, templar_archives, 2)
    wait_build(1, templar_archives)
    multirun(templar_research)
    place_guard(high_templar, 0)
    wait(150)
    place_guard(high_templar, 1)
    race_jump(do_nothing, add_archons, add_archons)

        --add_dark_templar--
    build(1, citadel_of_adun, 2)
    wait_build(1, citadel_of_adun)
    build(1, templar_archives, 2)
    wait_build(1, templar_archives)
    multirun(train_dark_templar)
    stop()

        --add_archons--
    train(4, archon)
    wait(300)
    goto(add_archons)

# ============== Train Units ==============
        --train_reaver--
    wait_build(1, robotics_facility)
    wait_build(1, robotics_support_bay)
    train(4, reaver)
    wait(300)
    goto(train_reaver)

        --train_observer--
    train(4, observer)
    wait(300)
    goto(train_observer)

        --train_arbiters--
    train(1, arbiter)
    wait(300)
    train(2, arbiter)
    wait(300)
    goto(train_arbiters)

        --corsair_air--
    build(1, stargate, 1)
    wait_build(1, stargate)
    multirun(cyber_upgrades_air)
    wait(300)
    train(2, corsair)
    wait(300)
    train(4, corsair)
    wait(300)
    train(6, corsair)
    wait(300)
    train(8, corsair)
    wait(300)
    goto(corsair_air)

        --carrier_air--
    wait(6000)
    build(1, stargate, 1)
    wait_build(1, stargate)
    build(1, fleet_beacon, 80)
    wait_build(1, fleet_beacon)
    multirun(research_carrier)
    multirun(cyber_upgrades_air)
    train(2, carrier)
    wait(150)
    train(4, carrier)
    wait(150)
    train(6, carrier)
    goto(carrier_air)

        --train_dark_templar--
    wait_build(1, gateway)
    wait_build(1, templar_archives)
    train(4, dark_templar)
    wait(300)
    train(8, dark_templar)
    wait(300)
    train(12, dark_templar)
    wait(300)
    train(16, dark_templar)
    wait(300)
    train(20, dark_templar)
    wait(300)
    goto(train_dark_templar)

        --train_dragoon--
    wait_build(1, gateway)
    wait_build(1, cybernetics_core)
    train(4, dragoon)
    wait(300)
    train(8, dragoon)
    wait(300)
    train(12, dragoon)
    wait(300)
    train(16, dragoon)
    wait(300)
    train(20, dragoon)
    wait(300)
    goto(train_dragoon)

        --train_zealot--
    wait_build(1, gateway)
    train(4, zealot)
    wait(300)
    train(8, zealot)
    wait(300)
    train(12, zealot)
    wait(300)
    train(16, zealot)
    wait(300)
    train(20, zealot)
    wait(300)
    train(24, zealot)
    wait(300)
    train(28, zealot)
    wait(300)
    train(32, zealot)
    wait(300)
    goto(train_zealot)

# ============== Required Buildings ==============
        --player_needs--
    player_need(1, nexus)
    player_need(1, pylon)
    player_need(1, gateway)
    player_need(1, cybernetics_core)
    player_need(1, forge)
    wait_train(1, shuttle)
    wait(500)
    goto(player_needs_2)

        --player_needs_2--
    player_need(1, nexus)
    player_need(1, pylon)
    player_need(1, gateway)
    player_need(1, citadel_of_adun)
    player_need(1, cybernetics_core)
    player_need(1, forge)
    wait_train(1, dark_templar)
    player_need(1, templar_archives)
    wait(500)
    goto(player_needs_3)

        --player_needs_3--
    player_need(1, nexus)
    player_need(1, pylon)
    player_need(1, gateway)
    player_need(1, citadel_of_adun)
    player_need(1, cybernetics_core)
    player_need(1, forge)
    player_need(1, templar_archives)
    player_need(1, robotics_facility)
    wait_train(1, observer)
    player_need(1, observatory)
    stop()

#Used by Add_Archon Race Jump
        --do_nothing--
    wait(300)
    stop()


