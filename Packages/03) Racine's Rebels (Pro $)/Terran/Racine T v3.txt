# Racine 3.0.1 Terran
# March 18, 2013

TMCx(1342, 101, aiscript):
	start_town()
	transports_off()
	farms_notiming()

#if DEBUG
    debug(debugmsg, Racine 3 Terran)
    --debugmsg--
#endif

	race_jump(vs_t_air, vs_z_air, vs_p_air)


		--vs_t_air--
	groundmap_jump(vs_t_ground)
	define_max(40, scv)
	define_max(2, marine)
	define_max(8, dropship)
	define_max(255, wraith)
	define_max(4, goliath)
	build(1, command_center, 150)
	wait_build(1, command_center)
	build(8, scv, 80)
	wait_buildstart(8, scv)
	build(1, supply_depot, 80)
	wait_buildstart(1, supply_depot)
	build(9, scv, 80)
	wait_buildstart(9, scv)
	build(1, refinery, 80)
	wait_buildstart(1, refinery)
	build(10, scv, 80)
	wait_buildstart(10, scv)
	multirun(cheats)
	build(1, barracks, 80)
	wait_buildstart(1, barracks)
	build(12, scv, 80)
	wait_buildstart(12, scv)
	build(1, factory, 80)
	wait_buildstart(1, factory)
	farms_timing()
	train(1, marine)
	build(12, scv, 80)
	wait_buildstart(12, scv)
	random_jump(100, vta_wraith_rush)
	build(2, factory, 80)
	wait_buildstart(2, factory)
	train(2, marine)
	build(1, armory, 80)
	wait_buildstart(1, armory)
	multirun(vta_starport_stuff)
	build(16, scv, 80)
	wait_buildstart(16, scv)
	upgrade(1, t_vehicle_weapon, 30)
	train(4, goliath)
	wait_train(4, goliath)
	wait_train(1, dropship)
	attack_add(4, goliath)
	attack_prepare()
	build(2, machine_shop, 150)
	wait_buildstart(2, machine_shop)
	upgrade(1, goliath_range, 30)
	tech(siege_mode, 30)
	build(1, engineering_bay, 80)
	wait_buildstart(1, engineering_bay)
	train(2, siege_tank)
	defenseuse_gg(1, goliath)
	defenseuse_ag(1, goliath)
	attack_do()
	attack_clear()
	check_transports()
	build(2, missile_turret, 80)
	wait_buildstart(2, missile_turret)
	multirun(vta_expo_loop)
	train(4, siege_tank)
	build(4, factory, 80)
	wait_buildstart(4, factory)
	defensebuild_gg(1, siege_tank)
	defenseuse_gg(1, siege_tank)

		--vta_go_back--
	define_max(28, goliath)
	train(6, goliath)
	train(8, siege_tank)
	upgrade(1, t_vehicle_plating, 30)
	train(10, siege_tank)
	train(6, goliath)
	attack_add(6, siege_tank)
	attack_add(4, goliath)
	attack_prepare()
	train(10, siege_tank)
	train(10, goliath)
	goto(vta_attack_loop)


		--vta_attack_loop--
	attack_do()
	attack_clear()
	attack_add(10, siege_tank)
	attack_add(8, goliath)
	attack_prepare()
	train(18, siege_tank)
	train(15, goliath)
	attack_do()
	attack_clear()
	random_jump(30, vta_bc_tech)
	attack_add(8, siege_tank)
	attack_add(14, goliath)
	attack_prepare()
	train(20, siege_tank)
	train(16, goliath)
	wait(100) # added
	goto(vta_attack_loop)


		--vta_wraith_rush--
	define_max(15, wraith)
	define_max(30, goliath)
	define_max(50, scv)
	define_max(2, marine)
	build(8, scv, 80)
	wait_buildstart(8, scv)
	build(1, supply_depot, 8)
	wait_buildstart(1, supply_depot)
	build(11, scv, 80)
	wait_buildstart(11, scv)
	multirun(cheats)
	build(1, barracks, 80)
	wait_buildstart(1, barracks)
	build(1, refinery, 80)
	wait_buildstart(1, refinery)
	build(12, scv, 80)
	wait_buildstart(12, scv)
	build(1, factory, 80)
	wait_buildstart(1, factory)
	farms_timing()
	train(2, marine)
	build(16, scv, 80)
	wait_buildstart(16, scv)
	build(2, starport, 80)
	wait_buildstart(2, starport)
	build(17, scv, 80)
	wait_buildstart(17, scv)
	build(1, armory, 80)
	wait_buildstart(1, armory)
	build(18, scv, 80)
	wait_buildstart(18, scv)
	build(1, control_tower, 80)
	wait_buildstart(1, control_tower)
	train(1, wraith)
	build(19, scv, 80)
	wait_buildstart(19, scv)
	train(1, goliath)
	defenseuse_gg(1, goliath)
	defenseuse_ag(1, goliath)
	build(20, scv, 80)
	wait_buildstart(20, scv)
	tech(cloaking_field, 70)
	train(3, wraith)
	train(2, goliath)
	build(1, academy, 80)
	wait_buildstart(1, academy)
	upgrade(1, t_vehicle_weapon, 30)
	train(5, wraith)
	train(1, dropship)
	train(4, goliath)
	multirun(vta_wraith_return)
	train(6, wraith)
	build(1, comsat_station, 80)
	wait_buildstart(1, comsat_station)
	check_transports()
	defenseclear_gg()
	defensebuild_gg(3, goliath)
	defensebuild_ga(3, goliath)
	train(10, goliath)
	stop()


		--vta_expo_loop--
	expand(99, vta_expo)
	wait(6000)

	goto(vta_expo_loop)


		--vta_expo--
	start_town()
	build(1, command_center, 80)
	wait_build(1, command_center)
	build(2, scv, 80)
	wait_buildstart(2, scv)
	build(2, missile_turret, 80)
	wait_buildstart(2, missile_turret)
	build(4, scv, 80)
	wait_buildstart(4, scv)
	build(4, missile_turret, 80)
	wait_buildstart(4, missile_turret)
	build(8, scv, 80)
	wait_build(8, scv)
	build(1, refinery, 80)
	build(1, factory, 80)
	wait_buildstart(1, factory)
	build(10, scv, 80)
	wait_build(10, scv)
	build(1, machine_shop, 80)
	build(1, comsat_station, 80)
	stop()


		--vta_wraith_return--
	wait_train(4, goliath)
	attack_add(4, goliath)
	attack_add(6, wraith)
	attack_prepare()
	build(2, factory, 80)
	wait_build(2, factory)
	build(2, machine_shop, 80)
	wait_buildstart(2, machine_shop)
	build(2, control_tower, 80)
	wait_buildstart(2, control_tower)
	attack_do()
	attack_clear()
	defensebuild_gg(2, siege_tank)
	defenseuse_gg(2, siege_tank)
	train(4, siege_tank)
	multirun(vta_expo_loop)
	multirun(vta_turrets)
	train(6, goliath)
	build(4, factory, 80)
	wait_buildstart(4, factory)
	train(8, siege_tank)
	attack_add(6, siege_tank)
	attack_add(8, goliath)
	attack_prepare()
	train(10, siege_tank)
	train(12, goliath)
	train(16, siege_tank)
	attack_do()
	attack_clear()
	goto(vta_go_back)


		--vta_turrets--
	build(1, engineering_bay, 80)
	wait_build(1, engineering_bay)
	build(4, missile_turret, 80)
	wait_build(4, missile_turret)
	stop()

		--vta_bc_tech--
	build(1, science_facility, 80)
	wait_buildstart(1, science_facility)
	build(5, starport, 80)
	wait_build(5, starport)
	build(1, physics_lab, 80)
	wait_buildstart(1, physics_lab)
	build(5, control_tower, 80)
	wait_buildstart(5, control_tower)
	attack_add(8, siege_tank)
	attack_add(8, goliath)
	attack_prepare()
	tech(yamato_gun, 30)
	train(5, battlecruiser)
	defenseuse_ag(1, battlecruiser)
	defenseuse_aa(1, battlecruiser)
	multirun(vta_upgrades)
	train(10, battlecruiser)
	attack_do()
	attack_clear()
	attack_add(10, siege_tank)
	attack_add(4, goliath)
	attack_add(5, battlecruiser)
	attack_prepare()
	train(10, battlecruiser)

		--vta_bc_loop--
	attack_do()
	attack_clear()
	attack_add(10, siege_tank)
	attack_add(8, battlecruiser)
	attack_add(8, goliath)
	attack_prepare()
	train(15, battlecruiser)
	attack_do()
	attack_clear()
	attack_add(8, siege_tank)
	attack_add(10, goliath)
	attack_prepare()
	wait(900)
	train(20, siege_tank)
	goto(vta_bc_loop)


		--vta_starport_stuff--
	build(1, starport, 80)
	wait_build(1, starport)
	build(1, control_tower, 80)
	wait_build(1, control_tower)
	check_transports()
	stop()


		--vta_upgrades--
	build(4, armory, 80)
	wait_buildstart(4, armory)
	wait_train(12, siege_tank)
	upgrade(1, t_ship_plating, 70)
	upgrade(1, t_ship_weapon, 70)
	upgrade(2, t_vehicle_weapon, 70)
	upgrade(2, t_vehicle_plating, 70)
	wait(5000) # up from 4500

	wait_train(1, battlecruiser)
	upgrade(2, t_ship_plating, 70)
	upgrade(2, t_ship_weapon, 70)
	upgrade(3, t_vehicle_weapon, 70)
	upgrade(3, t_vehicle_plating, 70)
	wait(5000) # up from 4500

	wait_train(6, battlecruiser)
	upgrade(3, t_ship_plating, 70)
	upgrade(3, t_ship_weapon, 70)
	stop()


		--vs_t_ground--
	define_max(40, scv)
	define_max(255, wraith)
	define_max(5, dropship)
	define_max(255, ghost)
	define_max(2, vulture)
	define_max(255, valkyrie)
	define_max(255, science_vessel)
	define_max(10, marine)
	define_max(255, medic)
	define_max(255, firebat)
	define_max(255, nuclear_missile)
	define_max(20, battlecruiser)
	build(1, command_center, 150)
	wait_buildstart(1, command_center)
	build(4, scv, 130)
	wait_buildstart(4, scv)
	build(8, scv, 80)
	wait_buildstart(8, scv)
	multirun(cheats)
	build(1, supply_depot, 80)
	wait_buildstart(1, supply_depot)
	build(10, scv, 80)
	wait_buildstart(10, scv)
	build(1, barracks, 80)
	wait_buildstart(1, barracks)
	build(11, scv, 80)
	wait_buildstart(11, scv)
	build(1, refinery, 80)
	wait_buildstart(1, refinery)
	build(14, scv, 80)
	wait_buildstart(14, scv)
	defenseuse_gg(1, marine)
	defenseuse_ag(1, marine)
	build(1, factory, 80)
	wait_buildstart(1, factory)
	farms_timing()
	build(15, scv, 80)
	wait_buildstart(15, scv)
	random_jump(182, vtg_wraith_build)
	# 2 fact rush
	build(2, factory, 80)
	wait_buildstart(2, factory)
	build(17, scv, 80)
	wait_buildstart(17, scv)
	build(1, machine_shop, 80)
	wait_buildstart(1, machine_shop)
	build(18, scv, 80)
	wait_buildstart(18, scv)
	build(2, machine_shop, 80)
	wait_buildstart(2, machine_shop)
	tech(siege_mode, 30)
	train(2, siege_tank)
	build(20, scv, 80)
	wait_buildstart(20, scv)
	train(4, siege_tank)
	attack_do()
	attack_clear()
	attack_add(8, marine)
	attack_add(4, siege_tank)
	attack_prepare()
	multirun(vtg_expo_loop)
	wait(500)

	train(8, marine)
	multirun(vtg_buildings)
	build(21, scv, 80)
	wait_buildstart(21, scv)
	train(8, siege_tank)
	train(8, goliath)
	train(10, siege_tank)
	multirun(vtg_detection)

		--vtg_attack_loop--
	defensebuild_gg(1, siege_tank)
	defensebuild_gg(1, goliath)
	defensebuild_ga(1, goliath)
	attack_do()
	attack_clear()
	attack_add(10, siege_tank)
	attack_add(8, goliath)
	attack_prepare()
	train(16, siege_tank)
	train(10, goliath)
	attack_do()
	attack_clear()
	attack_add(12, siege_tank)
	attack_add(10, goliath)
	attack_prepare()
	wait(500)

	train(22, siege_tank)
	train(18, goliath)
	defenseclear_gg()
	random_jump(100, vtg_bc_jump)
	goto(vtg_attack_loop)


		--vtg_buildings--
	build(1, starport, 80)
	wait_buildstart(1, starport)
	build(1, armory, 80)
	wait_buildstart(1, armory)
	build(4, factory, 80)
	wait_buildstart(4, factory)
	build(1, engineering_bay, 80)
	wait_buildstart(1, engineering_bay)
	build(1, academy, 80)
	wait_buildstart(1, academy)
	build(1, control_tower, 80)
	wait_buildstart(1, control_tower)
	build(4, machine_shop, 80)
	wait_buildstart(4, machine_shop)
	upgrade(1, goliath_range, 30)
	stop()


		--vtg_bc_jump--
	multirun(vtg_bc_tech)
	multirun(vtg_attack)
	multirun(vtg_upgrades)

		--vtg_bc_attack_loop--
	defensebuild_gg(1, battlecruiser)
	defenseuse_gg(1, battlecruiser)
	attack_do()
	attack_clear()
	attack_add(8, battlecruiser)
	attack_add(10, siege_tank)
	attack_add(16, goliath)
	attack_prepare()
	wait(600)

	attack_do()
	attack_clear()
	attack_add(8, wraith)
	attack_add(4, battlecruiser)
	attack_add(10, siege_tank)
	attack_add(12, goliath)
	attack_prepare()
	wait(600)

	goto(vtg_bc_attack_loop)


		--vtg_attack--
	wait(100)

	attack_do()
	attack_clear()
	attack_add(14, siege_tank)
	attack_add(14, goliath)
	attack_prepare()
	wait(400)

	stop()


		--vtg_bc_tech--
	build(1, science_facility, 80)
	wait_buildstart(1, science_facility)
	build(4, armory, 80)
	wait_buildstart(4, armory)
	build(5, starport, 80)
	wait_buildstart(5, starport)
	build(1, physics_lab, 80)
	wait_buildstart(1, physics_lab)
	defensebuild_gg(1, battlecruiser)
	defenseuse_gg(1, battlecruiser)
	defensebuild_ag(1, battlecruiser)
	defenseuse_ag(1, battlecruiser)
	defensebuild_aa(1, battlecruiser)
	defenseuse_aa(1, battlecruiser)
	build(5, control_tower, 80)
	wait_buildstart(5, control_tower)
	train(5, battlecruiser)
	tech(yamato_gun, 30)
	train(10, battlecruiser)
	stop()


		--vtg_upgrades--
	wait_build(4, armory)
	upgrade(2, t_vehicle_weapon, 30)
	upgrade(2, t_vehicle_plating, 30)
	upgrade(1, t_ship_weapon, 30)
	upgrade(1, t_ship_plating, 30)
	wait(5000) # up from 4000

	upgrade(3, t_vehicle_weapon, 30)
	upgrade(3, t_vehicle_plating, 30)
	upgrade(2, t_ship_weapon, 30)
	upgrade(2, t_ship_plating, 30)
	wait(5000)

	upgrade(3, t_ship_weapon, 30)
	upgrade(3, t_ship_plating, 30)
	stop()


		--vtg_wraith_build--
	build(16, scv, 80)
	wait_buildstart(16, scv)
	wait_build(1, factory)
	build(1, starport, 80)
	wait_buildstart(1, starport)
	defensebuild_gg(1, vulture)
	defenseuse_gg(1, vulture)
	train(1, vulture)
	build(2, starport, 80)
	wait_buildstart(2, starport)
	build(17, scv, 80)
	wait_buildstart(17, scv)
	train(2, vulture)
	build(2, factory, 80)
	wait_buildstart(2, factory)
	attack_add(3, vulture)
	attack_add(5, marine)
	attack_prepare()
	build(1, control_tower, 80)
	wait_buildstart(1, control_tower)
	define_max(12, wraith)
	defensebuild_gg(1, wraith)
	defenseuse_gg(1, wraith)
	train(1, wraith)
	build(18, scv, 80)
	wait_buildstart(18, scv)
	build(1, machine_shop, 80)
	wait_buildstart(1, machine_shop)
	train(3, wraith)
	tech(cloaking_field, 70)
	multirun(vtg_turrets)
	tech(siege_mode, 70)
	train(1, siege_tank)
	build(19, scv, 80)
	wait_buildstart(19, scv)
	build(20, scv, 80)
	wait_buildstart(20, scv)
	train(2, vulture)
	train(5, wraith)
	train(2, siege_tank)
	attack_do()
	attack_clear()
	attack_add(2, siege_tank)
	attack_add(5, wraith)
	attack_add(2, vulture)
	attack_add(5, marine)
	attack_prepare()
	build(2, machine_shop, 80)
	wait_buildstart(2, machine_shop)
	multirun(vtg_armory_upgrades)
	defensebuild_gg(1, siege_tank)
	defenseuse_gg(1, siege_tank)
	train(4, siege_tank)
	multirun(vtg_expo_loop)
	build(4, factory, 120)
	wait_buildstart(4, factory)
	train(6, siege_tank)
	train(2, goliath)
	build(4, machine_shop, 120)
	wait_buildstart(4, machine_shop)
	multirun(vtg_wraith_attack_loop)
	attack_do()
	attack_clear()
	check_transports()
	defenseclear_gg()
	defenseclear_ga()
	defenseclear_ag()
	stop()


		--vtg_wraith_attack_loop--
	defensebuild_gg(1, siege_tank)
	defensebuild_gg(1, goliath)
	defensebuild_ga(1, goliath)
	defensebuild_aa(1, wraith)
	defensebuild_ag(1, wraith)
	attack_do()
	attack_clear()
	attack_add(12, siege_tank)
	attack_add(6, wraith)
	attack_add(8, goliath)
	attack_prepare()
	wait(500)

	multirun(vtg_wraith_training)
	attack_do()
	attack_clear()
	attack_add(12, siege_tank)
	attack_add(6, wraith)
	attack_add(10, goliath)
	attack_prepare()
	wait(500)

	defenseclear_gg()
	defenseclear_ga()
	defenseclear_ag()
	random_jump(100, vtg_bc_jump)
	goto(vtg_wraith_attack_loop)


		--vtg_wraith_training--
	train(2, siege_tank)
	train(2, goliath)
	train(2, wraith)
	train(4, siege_tank)
	train(4, goliath)
	train(4, wraith)
	train(6, siege_tank)
	train(8, wraith)
	stop()


		--vtg_expo_loop--
	expand(99, vtg_expo)
	wait(7200)

	goto(vtg_expo_loop)


		--vtg_detection--
	build(3, missile_turret, 80)
	wait_buildstart(3, missile_turret)
	build(1, comsat_station, 80)
	wait_buildstart(1, comsat_station)
	check_transports()
	upgrade(1, t_vehicle_weapon, 30)
	wait(4500)

	upgrade(1, t_vehicle_plating, 30)
	stop()


		--vtg_armory_upgrades--
	build(1, armory, 80)
	wait_build(1, armory)
	defensebuild_gg(1, goliath)
	defenseuse_gg(1, goliath)
	defensebuild_ga(1, goliath)
	defenseuse_ga(1, goliath)
	train(8, wraith)
	upgrade(1, goliath_range, 30)
	upgrade(1, t_vehicle_weapon, 30)
	wait(5000)

	upgrade(1, t_vehicle_plating, 30)
	stop()


		--vtg_expo--
	start_town()
	build(1, command_center, 80)
	wait_build(1, command_center)
	build(5, scv, 80)
	wait_build(5, scv)
	build(3, missile_turret, 80)
	wait_buildstart(3, missile_turret)
	build(10, scv, 80)
	wait_build(10, scv)
	build(1, factory, 80)
	wait_buildstart(1, factory)
	build(1, machine_shop, 80)
	wait_buildstart(1, machine_shop)
	build(1, comsat_station, 80)
	wait_buildstart(1, comsat_station)
	build(1, refinery, 80)
	stop() # New
	
		--vtg_useless_loop--
	wait(900)

	goto(vtg_useless_loop)


		--vs_z_air--
	groundmap_jump(vs_z_ground)
	define_max(40, scv)
	define_max(1, marine)
	define_max(15, medic)
	define_max(10, firebat)
	define_max(55, siege_tank)
	define_max(255, wraith)
	define_max(10, science_vessel)
	define_max(5, wraith)
	define_max(35, goliath)
	define_max(10, valkyrie)
	build(1, command_center, 80)
	wait_build(1, command_center)
	build(8, scv, 80)
	wait_buildstart(8, scv)
	build(1, supply_depot, 80)
	wait_buildstart(1, supply_depot)
	build(9, scv, 80)
	wait_buildstart(9, scv)
	build(1, refinery, 80)
	wait_buildstart(1, refinery)
	build(1, barracks, 80)
	wait_buildstart(1, barracks)
	build(12, scv, 80)
	wait_buildstart(12, scv)
	multirun(cheats)
	build(1, factory, 80)
	wait_buildstart(1, factory)
	farms_timing()
	random_jump(128, vza_infantry)
	build(14, scv, 80)
	wait_buildstart(14, scv)
	train(1, marine)
	defensebuild_gg(1, marine)
	defenseuse_gg(1, marine)
	defensebuild_ga(1, marine)
	defenseuse_ga(1, marine)
	build(1, academy, 80)
	wait_buildstart(1, academy)
	build(15, scv, 80)
	wait_buildstart(15, scv)
	define_max(40, marine)
	train(2, marine)
	build(1, academy, 80)
	wait_buildstart(1, academy)
	build(1, armory, 80)
	wait_buildstart(1, armory)
	build(16, scv, 80)
	wait_buildstart(16, scv)
	train(3, marine)
	build(1, machine_shop, 80)
	wait_buildstart(1, machine_shop)
	build(17, scv, 80)
	wait_buildstart(17, scv)
	multirun(vza_starport_stuff)
	wait_build(1, machine_shop)
	upgrade(1, goliath_range, 30)
	train(1, goliath)
	defensebuild_gg(1, goliath)
	defenseuse_gg(1, goliath)
	defensebuild_ag(1, goliath)
	defenseuse_ag(1, goliath)
	train(1, medic)
	tech(optical_flare, 30)
	build(1, comsat_station, 80)
	wait_buildstart(1, comsat_station)
	upgrade(1, t_vehicle_weapon, 30)
	train(2, goliath)
	train(4, marine)
	build(1, engineering_bay, 80)
	wait_buildstart(1, engineering_bay)
	train(6, goliath)
	build(2, factory, 80)
	wait_buildstart(2, factory)
	tech(stim_packs, 30)
	build(2, missile_turret, 80)
	wait_buildstart(2, missile_turret)
	attack_do()
	attack_clear()
	attack_add(8, goliath)
	attack_add(6, marine)
	attack_add(2, medic)
	attack_prepare()
	wait(300)

	train(1, valkyrie)
	defensebuild_aa(1, valkyrie)
	defenseuse_aa(1, valkyrie)
	train(20, marine)
	upgrade(1, marine_range, 30)
	build(2, barracks, 80)
	wait_buildstart(2, barracks)
	upgrade(1, t_infantry_weapon, 30)
	multirun(vza_expo_loop)
	build(2, machine_shop, 80)
	wait_buildstart(2, machine_shop)
	train(10, marine)
	tech(siege_mode, 80)
	train(2, siege_tank)
	build(1, science_facility, 80)
	wait_buildstart(1, science_facility)
	build(4, missile_turret, 80)
	wait_buildstart(4, missile_turret)
	build(3, factory, 80)
	wait_buildstart(3, factory)
	train(1, science_vessel)
	attack_do()
	attack_clear()
	attack_add(6, siege_tank)
	attack_add(5, valkyrie)
	attack_add(12, goliath)
	attack_add(2, science_vessel)
	attack_add(15, marine)
	attack_add(3, medic)
	attack_prepare()
	wait(700)

	multirun(vza_attack_loop)
	tech(irradiate, 30)
	upgrade(1, t_vehicle_plating, 30)
	build(3, starport, 80)
	wait_buildstart(3, starport)
	upgrade(2, t_infantry_weapon, 30)
	build(4, factory, 80)
	wait_buildstart(4, factory)
	upgrade(2, t_vehicle_weapon, 30)
	train(8, siege_tank)
	train(10, goliath)
	train(16, marine)
	build(3, control_tower, 80)
	wait_buildstart(3, control_tower)
	train(2, valkyrie)
	train(5, science_vessel)
	stop()


		--vza_attack_loop--
	attack_do()
	attack_clear()
	attack_add(10, siege_tank)
	attack_add(8, goliath)
	attack_add(6, valkyrie)
	attack_add(4, science_vessel)
	attack_prepare()
	wait(500)

	attack_do()
	attack_clear()
	attack_add(20, marine)
	attack_add(5, firebat)
	attack_add(18, goliath)
	attack_add(7, medic)
	attack_add(4, valkyrie)
	attack_add(4, science_vessel)
	attack_prepare()
	wait(500)

	goto(vza_attack_loop)


		--vza_infantry--
	define_max(255, wraith)
	define_max(10, science_vessel)
	define_max(30, siege_tank)
	build(13, scv, 80)
	wait_buildstart(13, scv)
	define_max(40, marine)
	train(2, marine)
	build(1, academy, 80)
	wait_buildstart(1, academy)
	build(1, starport, 80)
	wait_buildstart(1, starport)
	build(14, scv, 80)
	wait_buildstart(14, scv)
	train(4, marine)
	tech(stim_packs, 30)
	build(15, scv, 80)
	wait_buildstart(15, scv)
	train(1, medic)
	build(1, control_tower, 80)
	wait_buildstart(1, control_tower)
	train(6, marine)
	defenseuse_gg(1, marine)
	defenseuse_ag(1, marine)
	train(1, dropship)
	build(16, scv, 80)
	wait_buildstart(16, scv)
	train(7, marine)
	wait_train(1, dropship)
	wait_train(7, marine)
	send_suicide(0)
	train(8, marine)
	defensebuild_ga(1, marine)
	defenseuse_ga(1, marine)
	defensebuild_gg(1, marine)
	defenseuse_gg(1, marine)
	build(1, comsat_station, 80)
	wait_buildstart(1, comsat_station)
	build(1, science_facility, 80)
	wait_buildstart(1, science_facility)
	train(12, marine)
	build(2, barracks, 80)
	wait_buildstart(2, barracks)
	train(2, medic)
	train(14, marine)
	build(1, machine_shop, 80)
	wait_buildstart(1, machine_shop)
	multirun(vza_training)
	build(17, scv, 80)
	wait_buildstart(17, scv)
	check_transports()
	train(18, marine)
	upgrade(1, marine_range, 30)
	train(1, science_vessel)
	random_jump(128, vza_skip_attack)
	train(24, marine)
	train(3, medic)
	train(24, marine)
	wait_train(24, marine)
	wait_train(3, medic)
	send_suicide(0)

		--vza_skip_attack--
	build(2, engineering_bay, 80)
	wait_buildstart(2, engineering_bay)
	train(1, siege_tank)
	defensebuild_gg(1, siege_tank)
	defenseuse_gg(1, siege_tank)
	tech(siege_mode, 70)
	train(2, siege_tank)
	multirun(vza_expo_loop)
	build(18, scv, 80)
	wait_buildstart(18, scv)
	multirun(vza_infantry_tech)
	tech(optical_flare, 30)
	train(4, siege_tank)
	train(18, marine)
	train(2, science_vessel)
	train(20, marine)
	train(6, medic)
	train(6, siege_tank)
	train(25, marine)
	tech(irradiate, 30)
	train(3, science_vessel)
	train(5, siege_tank)
	wait_train(5, siege_tank)
	wait_train(20, marine)
	attack_add(5, siege_tank)
	attack_add(20, marine)
	attack_add(6, medic)
	attack_add(1, science_vessel)
	attack_prepare()
	build(3, barracks, 80)
	wait_buildstart(3, barracks)
	train(32, marine)
	train(10, medic)
	train(7, siege_tank)
	attack_do()
	attack_clear()

		--vza_infantry_attack_loop--
	train(8, siege_tank)
	train(26, marine)
	train(3, science_vessel)
	train(6, medic)
	wait_train(24, marine)
	wait_train(8, siege_tank)
	wait_train(3, science_vessel)
	wait_train(6, medic)
	attack_add(24, marine)
	attack_add(8, siege_tank)
	attack_add(3, science_vessel)
	attack_add(6, medic)
	attack_prepare()
	train(35, marine)
	train(10, siege_tank)
	train(5, science_vessel)
	attack_do()
	attack_clear()
	wait(900)

	goto(vza_infantry_attack_loop)


		--vza_starport_stuff--
	build(1, starport, 80)
	wait_build(1, starport)
	build(1, control_tower, 80)
	wait_build(1, control_tower)
	check_transports()
	stop()


		--vza_expo_loop--
	expand(99, vza_expo)
	wait(8000)

	goto(vza_expo_loop)


		--vza_expo--
	start_town()
	build(1, command_center, 80)
	wait_buildstart(1, command_center)
	defenseuse_gg(1, siege_tank)
	defenseuse_gg(1, marine)
	build(2, missile_turret, 80)
	wait_buildstart(2, missile_turret)
	build(1, bunker, 80)
	wait_buildstart(1, bunker)
	build(5, scv, 80)
	wait_buildstart(5, scv)
	build(1, comsat_station, 80)
	wait_buildstart(1, comsat_station)
	build(10, scv, 80)
	wait_buildstart(10, scv)
	build(1, barracks, 80)
	wait_buildstart(1, barracks)
	build(1, refinery, 80)
	stop()


		--vza_training--
	train(8, marine)
	train(2, medic)
	train(10, marine)
	train(3, medic)
	train(14, marine)
	train(4, medic)
	train(18, marine)
	train(6, medic)
	train(22, marine)
	stop()


		--vza_infantry_tech--
	upgrade(1, t_infantry_weapon, 30)
	upgrade(1, t_infantry_armor, 30)
	wait(2000)

	build(2, factory, 80)
	wait_buildstart(2, factory)
	build(2, starport, 80)
	wait_buildstart(2, starport)
	build(1, missile_turret, 80)
	wait_buildstart(1, missile_turret)
	build(2, missile_turret, 80)
	wait_buildstart(2, missile_turret)
	build(3, missile_turret, 80)
	wait_buildstart(3, missile_turret)
	build(2, machine_shop, 80)
	wait_buildstart(2, machine_shop)
	build(2, control_tower, 80)
	wait_buildstart(2, control_tower)
	upgrade(2, t_infantry_weapon, 30)
	upgrade(2, t_infantry_armor, 30)
	wait(5000)

	upgrade(3, t_infantry_weapon, 30)
	upgrade(3, t_infantry_armor, 30)
	stop()


		--vs_z_ground--
	build(1, command_center, 80)
	wait_build(1, command_center)
	build(6, scv, 80)
	wait_buildstart(6, scv)
	multirun(cheats)
	build(1, barracks, 80)
	wait_buildstart(1, barracks)
	build(7, scv, 80)
	wait_buildstart(7, scv)
	farms_timing()
	build(2, barracks, 80)
	wait_buildstart(2, barracks)
	build(8, scv, 80)
	wait_buildstart(8, scv)
	defensebuild_gg(1, marine)
	defenseuse_gg(1, marine)
	defensebuild_ga(1, marine)
	defenseuse_ga(1, marine)
	train(1, marine)
	build(9, scv, 80)
	wait_buildstart(9, scv)
	train(3, marine)
	build(10, scv, 80)
	wait_buildstart(10, scv)
	train(5, marine)
	build(11, scv, 80)
	wait_buildstart(11, scv)
	build(1, refinery, 80)
	wait_buildstart(1, refinery)
	train(7, marine)
	build(12, scv, 80)
	wait_buildstart(12, scv)
	build(1, academy, 80)
	wait_buildstart(1, academy)
	random_jump(128, vzg_skip_attack)
	send_suicide(0)

		--vzg_skip_attack--
	train(9, marine)
	build(13, scv, 80)
	wait_buildstart(13, scv)
	train(11, marine)
	build(1, factory, 80)
	wait_buildstart(1, factory)
	build(1, comsat_station, 80)
	wait_buildstart(1, comsat_station)
	tech(stim_packs, 80)
	train(2, medic)
	random_jump(128, vzg_skip_attack2)
	wait_train(2, medic)
	send_suicide(0)

		--vzg_skip_attack2--
	define_max(255, wraith) 
	build(2, engineering_bay, 80)
	wait_buildstart(2, engineering_bay)
	build(1, machine_shop, 80)
	wait_buildstart(1, machine_shop)
	train(2, firebat)
	upgrade(1, t_infantry_weapon, 30)
	upgrade(1, t_infantry_armor, 30)
	build(2, missile_turret, 80)
	wait_buildstart(2, missile_turret)
	upgrade(1, marine_range, 80)
	tech(siege_mode, 70)
	defensebuild_gg(1, siege_tank)
	defenseuse_gg(1, siege_tank)
	train(1, siege_tank)
	build(1, bunker, 80)
	wait_buildstart(1, bunker)
	train(12, marine)
	train(3, medic)
	build(2, missile_turret, 80)
	train(3, siege_tank)
	build(1, starport, 80)
	wait_buildstart(1, starport)
	train(16, marine)
	train(4, medic)
	train(4, siege_tank)
	build(1, science_facility, 80)
	wait_buildstart(1, science_facility)
	build(3, barracks, 80)
	wait_buildstart(3, barracks)
	train(4, siege_tank)
	build(1, control_tower, 80)
	wait_buildstart(1, control_tower)
	build(4, missile_turret, 80)
	wait_buildstart(4, missile_turret)
	train(20, marine)
	upgrade(2, t_infantry_weapon, 30)
	upgrade(2, t_infantry_armor, 30)
	build(3, factory, 80)
	wait_buildstart(3, factory)
	build(3, machine_shop, 80) # Moved up
	multirun(vzg_expo_loop)
	train(1, science_vessel)
	tech(irradiate, 80)
	#train(7, siege_tank)
	multirun(vzg_training) # Moved out of vzg_attack_loop
	multirun(vzg_attack_loop)
	#train(25, marine)
	#build(3, machine_shop, 80)
	#wait_buildstart(3, machine_shop)
	build(4, barracks, 80)
	wait_buildstart(4, barracks)
	#train(30, marine)
	#train(6, medic)
	upgrade(3, t_infantry_weapon, 30)
	upgrade(3, t_infantry_armor, 30)
	#train(12, siege_tank)
	#train(3, science_vessel)
	build(4, barracks, 80)
	wait_buildstart(4, barracks)
	# The commented out things make this script work a _lot_ better than before
	# while it doesn't change the look at all.
	stop()


		--vzg_attack_loop--
	attack_add(1, science_vessel)
	attack_add(6, siege_tank)
	attack_add(20, marine)
	attack_add(3, medic)
	attack_prepare()
	
	wait(400)

	attack_do()
	attack_clear()
	train(1, dropship)
	attack_add(3, science_vessel)
	attack_add(12, siege_tank)
	attack_add(20, marine)
	attack_add(8, medic)
	attack_add(4, firebat)
	attack_prepare()
	wait(400)

	attack_do()
	attack_clear()
	train(2, dropship)
	goto(vzg_attack_loop)


		--vzg_expo_loop--
	expand(99, vzg_expo)
	wait(7000)

	goto(vzg_expo_loop)


		--vzg_training--
	train(4, siege_tank)
	train(16, marine)
	train(4, firebat)
	train(2, science_vessel)
	train(4, medic)
	train(8, siege_tank)
	train(28, marine)
	train(8, medic)
	train(4, science_vessel)
	train(12, siege_tank)
	train(42, marine)
	train(8, science_vessel)
	train(12, medic)
	train(14, siege_tank)
	wait(900)

	goto(vzg_training)


		--vzg_expo--
	start_town()
	build(1, command_center, 80)
	wait_build(1, command_center)
	build(1, bunker, 80)
	wait_buildstart(1, bunker)
	defenseuse_gg(1, marine)
	build(5, scv, 80)
	wait_build(5, scv)
	build(3, missile_turret, 80)
	wait_buildstart(3, missile_turret)
	build(10, scv, 80)
	build(1, comsat_station, 80)
	build(1, refinery, 80)
	build(1, barracks, 80)
	stop()


		--vs_p_air--
	groundmap_jump(vs_p_ground)
	define_max(50, scv)
	define_max(8, marine)
	define_max(50, goliath)
	define_max(50, siege_tank)
	define_max(255, wraith)
	build(1, command_center, 80)
	wait_build(1, command_center)
	build(8, scv, 80)
	wait_buildstart(8, scv)
	build(1, supply_depot, 80)
	wait_buildstart(1, supply_depot)
	build(9, scv, 80)
	wait_buildstart(9, scv)
	build(1, refinery, 80)
	wait_buildstart(1, refinery)
	build(1, barracks, 80)
	wait_buildstart(1, barracks)
	build(12, scv, 80)
	wait_buildstart(12, scv)
	build(1, factory, 80)
	wait_buildstart(1, factory)
	multirun(cheats)
	farms_timing()
	train(1, marine)
	defenseuse_gg(1, marine)
	defenseuse_ag(1, marine)
	build(12, scv, 80)
	wait_buildstart(12, scv)
	build(2, factory, 80)
	wait_buildstart(2, factory)
	build(1, starport, 80)
	wait_buildstart(1, starport)
	build(14, scv, 80)
	wait_buildstart(14, scv)
	build(1, machine_shop, 80)
	wait_buildstart(1, machine_shop)
	multirun(vpa_turrets)
	build(1, armory, 80)
	wait_buildstart(1, armory)
	build(2, machine_shop, 80)
	wait_buildstart(2, machine_shop)
	build(1, control_tower, 80)
	wait_buildstart(1, control_tower)
	tech(siege_mode, 30)
	train(3, siege_tank)
	build(15, scv, 80)
	wait_buildstart(15, scv)
	train(4, marine)
	upgrade(1, t_vehicle_weapon, 30)
	check_transports()
	build(1, academy, 80)
	wait_buildstart(1, academy)
	upgrade(1, goliath_range, 30)
	build(1, comsat_station, 80)
	wait_buildstart(1, comsat_station)
	train(6, siege_tank)
	train(6, goliath)
	wait_train(6, goliath)
	send_suicide(0)
	multirun(vpa_attack_noghosts)
	multirun(vpa_buildings)
	multirun(vpa_tech)
	multirun(vpa_expo_loop)
	stop()


		--vpa_attack_noghosts--
	attack_add(8, siege_tank)
	attack_add(14, goliath)
	attack_prepare()
	wait(500)

	attack_do()
	attack_clear()
	notowns_jump(ghost, vpa_attack_noghosts)
	goto(vpa_attack_wghosts)


		--vpa_attack_wghosts--
	attack_add(10, siege_tank)
	attack_add(18, goliath)
	attack_add(6, ghost)
	attack_prepare()
	wait(500)

	attack_do()
	attack_clear()
	goto(vpa_attack_wghosts)


		--vpa_expo_loop--
	expand(99, vpa_expo)
	wait(7200)

	goto(vpa_expo_loop)


		--vpa_expo--
	start_town()
	build(1, command_center, 80)
	wait_build(1, command_center)
	build(1, scv, 80)
	wait_buildstart(1, scv)
	build(1, missile_turret, 80)
	wait_buildstart(1, missile_turret)
	build(5, scv, 80)
	wait_buildstart(5, scv)
	build(3, missile_turret, 80)
	wait_buildstart(3, missile_turret)
	build(1, refinery, 80)
	build(1, comsat_station, 80)
	wait_buildstart(1, comsat_station)
	build(10, scv, 80)
	wait_build(10, scv)
	build(1, factory, 80)
	wait_build(1, factory)
	build(1, machine_shop, 80)
	stop()


		--vpa_tech--
	build(2, armory, 80)
	wait_build(2, armory)
	upgrade(1, t_vehicle_plating, 30)
	build(1, science_facility, 80)
	wait_buildstart(1, science_facility)
	wait(2500)

	build(1, covert_ops, 80)
	wait_buildstart(1, covert_ops)
	upgrade(2, t_vehicle_plating, 30)
	upgrade(2, t_vehicle_weapon, 30)
	tech(lockdown, 30)
	build(3, barracks, 80)
	wait_buildstart(3, barracks)
	train(10, ghost)
	wait(1000) 

	upgrade(3, t_vehicle_plating, 30)
	upgrade(3, t_vehicle_weapon, 30)
	stop()


		--vpa_buildings--
	build(6, factory, 125)
	wait_build(6, factory)
	build(5, machine_shop, 125)
	stop()


		--vpa_turrets--
	build(1, engineering_bay, 80)
	wait_buildstart(1, engineering_bay)
	build(1, academy, 80)
	wait_buildstart(1, academy)
	build(1, missile_turret, 80)
	wait_build(1, missile_turret)
	build(2, missile_turret, 80)
	wait_build(2, missile_turret)
	build(3, missile_turret, 80)
	wait_build(3, missile_turret)
	build(1, comsat_station, 80)
	wait_buildstart(1, comsat_station)
	build(4, missile_turret, 80)
	wait_build(4, missile_turret)
	build(5, missile_turret, 80)
	wait_build(5, missile_turret)
	stop()


		--vs_p_ground--
	define_max(255, wraith)
	define_max(255, goliath)
	#random_jump(152, vpg_mines_build)
	goto(vpg_mines_build)
# I don't like to make big changes like this, but:
# the other build is so much better!
	build(1, command_center, 150)
	wait_buildstart(1, command_center)
	build(4, scv, 80)
	wait_buildstart(4, scv)
	build(8, scv, 80)
	wait_buildstart(8, scv)
	build(1, supply_depot, 80)
	wait_buildstart(1, supply_depot)
	build(10, scv, 80)
	wait_buildstart(10, scv)
	multirun(cheats)
	build(1, barracks, 80)
	wait_buildstart(1, barracks)
	defensebuild_gg(1, marine)
	defenseuse_gg(1, marine)
	define_max(10, marine)
	build(12, scv, 80)
	wait_buildstart(12, scv)
	build(1, refinery, 80)
	wait_buildstart(1, refinery)
	build(13, scv, 80)
	wait_buildstart(13, scv)
	build(1, factory, 80)
	wait_buildstart(1, factory)
	build(14, scv, 80)
	build(2, factory, 80)
	wait_buildstart(2, factory)
	farms_timing()
	build(15, scv, 80)
	wait_buildstart(15, scv)
	multirun(vtg_turrets)
	build(1, machine_shop, 80)
	wait_buildstart(1, machine_shop)
	defenseuse_gg(1, siege_tank)
	build(16, scv, 80)
	wait_buildstart(16, scv)
	build(2, machine_shop, 80)
	wait_buildstart(2, machine_shop)
	build(18, scv, 80)
	wait_buildstart(18, scv)
	train(2, siege_tank)
	defensebuild_gg(1, siege_tank)
	defenseuse_gg(1, siege_tank)
	panic(vpg_expo2)
	tech(siege_mode, 30)
	build(19, scv, 80)
	wait_buildstart(19, scv)
	train(4, vulture)
	build(20, scv, 80)
	wait_buildstart(20, scv)
	tech(spider_mines, 30)
	multirun(vpg_expo_init)
	multirun(vpa_buildings) # added
	train(4, siege_tank)
	build(21, scv, 80)
	wait_buildstart(21, scv)
	train(6, siege_tank)
	#panic(vpg_attack) # Wtf?
	multirun(vpg_attack)
	multirun(vpg_starport_upgrades)
	wait(1000)
	send_suicide(0)
	#panic(vpg_attack_loop) # Wtf?
	goto(vpg_attack_loop)


		--vpg_attack--
	attack_do()
	attack_clear()
	attack_add(12, siege_tank)
	attack_add(12, vulture)
	attack_prepare()
	wait(300)
	stop()


		--vpg_mines_build--
	define_max(5, dropship)
	define_max(60, siege_tank)
	define_max(40, vulture)
	build(1, command_center, 80)
	wait_build(1, command_center)
	build(8, scv, 80)
	wait_buildstart(8, scv)
	build(1, supply_depot, 80)
	wait_buildstart(1, supply_depot)
	build(10, scv, 80)
	wait_buildstart(10, scv)
	build(1, barracks, 80)
	wait_buildstart(1, barracks)
	build(11, scv, 80)
	wait_buildstart(11, scv)
	build(1, refinery, 70)
	wait_buildstart(1, refinery)
	multirun(cheats)
	build(12, scv, 80)
	wait_buildstart(12, scv)
	train(1, marine)
	defenseuse_gg(1, marine) # new
	build(1, bunker, 80) # new
	build(1, factory, 80)
	wait_buildstart(1, factory)
	train(2, marine)
	build(13, scv, 80)
	wait_buildstart(13, scv)
	farms_timing()
	build(2, factory, 80)
	wait_buildstart(2, factory)
	build(14, scv, 80)
	wait_buildstart(14, scv)
	build(1, machine_shop, 80)
	wait_buildstart(1, machine_shop)
	defenseuse_gg(1, marine)
	train(4, marine)
	build(15, scv, 80)
	wait_buildstart(15, scv)
	tech(spider_mines, 80)
	train(2, vulture)
	defenseuse_gg(1, vulture)
	build(16, scv, 80)
	wait_buildstart(16, scv)
	train(4, vulture)
	build(2, machine_shop, 80)
	wait_buildstart(2, machine_shop)
	multirun(vtg_turrets)
	build(17, scv, 80)
	wait_buildstart(17, scv)
	train(2, siege_tank)
	defenseuse_gg(1, siege_tank)
	tech(siege_mode, 80)
	
	capt_expand() # New
	panic(vpg_expo) # New
	
	multirun(vpg_expo_loop)
	train(6, siege_tank)
	build(20, scv, 80)
	wait_buildstart(20, scv)
	build(1, bunker, 80)
	wait_buildstart(1, bunker)
	train(4, siege_tank)
	tech(spider_mines, 80)
	build(4, factory, 120)
	wait_buildstart(4, factory)
	train(6, siege_tank)
	build(6, factory, 120)
	wait_buildstart(6, factory)
	
	multirun(vpg_mshops)
	multirun(vpg_starport_upgrades)
	#multirun(vpg_normal_attack)
	#wait(2000)
	
	train(10, vulture)
	train(14, siege_tank)
	wait_train(14, siege_tank)
	attack_add(10, vulture)
	attack_add(14, siege_tank)
	
		--vpg_normal_attack_loop--
	attack_do()
	attack_clear()
	defensebuild_gg(1, siege_tank) # New
	defensebuild_gg(1, vulture)	   # New
	attack_add(10, vulture)
	attack_add(12, siege_tank)
	attack_prepare()
	enemyowns_jump(stargate, vpg_nml_goliath_loop)
	attack_do()
	attack_clear()
	attack_add(8, vulture)
	attack_add(14, siege_tank)
	#panic(vpg_normal_attack_loop) No.
	attack_prepare()
	wait(50)
	
	goto(vpg_normal_attack_loop)


		--vpg_nml_goliath_loop--
	define_max(50, goliath)
	define_max(4, vulture)
	attack_do()
	attack_clear()
	attack_add(10, siege_tank)
	attack_add(22, goliath)
	attack_add(6, marine)
	#expand(99, vpg_expansion_attack)
	attack_prepare()
	wait(300)
	
	goto(vpg_nml_goliath_loop)

		# --vpg_expansion_attack--
	# start_town()
	# attack_do()
	# attack_clear()
	# attack_add(26, goliath)
	# attack_add(10, siege_tank)
	# attack_prepare()
	# wait(300)
	# stop()


		--vpg_expo_loop--
	expand(99, vpg_expo)
	wait(8000)

	goto(vpg_expo_loop)


		--vpg_expo--
	start_town()
	defenseuse_gg(1, siege_tank)
	defenseuse_gg(1, vulture)
	build(1, missile_turret, 80)
	wait_build(1, missile_turret)
	build(1, command_center, 80)
	wait_build(1, command_center)
	build(5, scv, 80)
	wait_build(5, scv)
	build(3, missile_turret, 80)
	wait_build(3, missile_turret)
	build(1, comsat_station, 80)
	build(10, scv, 80)
	build(1, factory, 80)
	build(1, refinery, 80)
	build(1, machine_shop, 80)
	stop()


		# --vpg_normal_attack--
	# train(10, vulture)
	# train(14, siege_tank)
	# wait_train(14, siege_tank)
	# attack_add(10, vulture)
	# attack_add(14, siege_tank)
	# attack_do()
	# attack_clear()
	# stop()


		--vpg_mshops--
	wait_build(6, factory)
	build(5, machine_shop, 240)
	stop()


		--vpg_attack_loop--
	attack_do()
	attack_clear()
	attack_add(16, siege_tank)
	attack_add(10, vulture)
	attack_prepare()
	#multirun(vpg_attack2) # Wtf?
	wait(120)

	enemyowns_jump(fleet_beacon, vpg_anticarrier)
	goto(vpg_attack_loop)


	# --vpg_attack2--
	# attack_do()
	# attack_clear()
	# attack_add(10, siege_tank)
	# attack_add(12, vulture)
	# attack_prepare()
	# wait(120)
	# stop()


		--vpg_anticarrier--
	send_suicide(0)
	define_max(255, vulture)
	define_max(60, goliath)

		--vpg_anticarrier_loop--
	attack_do()
	attack_clear()
	attack_add(22, goliath)
	attack_add(10, siege_tank)
	attack_add(6, marine)
	attack_prepare()
	multirun(vpg_attack3) # Wtf?
	wait(120)

	goto(vpg_anticarrier_loop)


		--vpg_attack3--
	attack_do()
	attack_clear()
	attack_add(26, goliath)
	attack_add(8, siege_tank)
	attack_prepare()
	wait(120)

	stop()


		--vpg_starport_upgrades--
	build(1, starport, 80)
	wait_build(1, starport)
	build(1, control_tower, 80)
	wait_build(1, control_tower)
	check_transports()
	build(2, armory, 80)
	wait_build(2, armory)
	upgrade(1, goliath_range, 30)
	upgrade(1, t_vehicle_weapon, 30)
	upgrade(1, t_vehicle_plating, 30)

		--vpg_dropship_loop--
	train(5, dropship)
	wait(300)

	goto(vpg_dropship_loop)


		--vpg_expo_init--
	expand(1, vpg_expo)
	wait(12000)


		--vpg_expo_loop2--
	expand(99, vpg_expo2)
	wait(8000)
	
	goto(vpg_expo_loop2)


		--vpg_expo2--
	start_town()
	defenseuse_gg(1, marine)
	defenseuse_gg(1, siege_tank)
	build(1, command_center, 80)
	wait_build(1, command_center)
	build(3, scv, 80)
	build(1, missile_turret, 80)
	wait_build(1, missile_turret)
	build(3, factory, 80)
	build(1, bunker, 80)
	wait_build(1, bunker)
	build(3, missile_turret, 80)
	wait_build(1, command_center)
	build(1, comsat_station, 80)
	build(3, machine_shop, 80)
	build(1, factory, 80)
	build(1, refinery, 80)
	build(10, scv, 80)
	train(8, siege_tank)
	wait_train(8, siege_tank)
	send_suicide(0)
	stop()


		--vtg_turrets--
	build(1, engineering_bay, 80)
	wait_buildstart(1, engineering_bay)
	build(1, academy, 80)
	wait_buildstart(1, academy)
	wait_build(1, engineering_bay)
	build(2, missile_turret, 150)
	wait_build(1, academy)
	build(1, comsat_station, 80)
	wait_buildstart(1, comsat_station)
	build(3, missile_turret, 80)
	wait_buildstart(3, missile_turret)
	build(5, missile_turret, 80)
	stop()


		--cheats--
	give_money()
	wait(1500)

	goto(cheats)
	
	
	
	
	