# Adun's Fury II Terran v1.1
# bajadulce
# Sept, 2008

TMCx(1342, 101, aiscript):

start_town()
transports_off()
farms_notiming()

#if DEBUG
    debug(debugmsg, Adun's Fury II v1.1 Terran)
    --debugmsg--
#endif


build(1, command_center, 150)
wait_buildstart(1, command_center)
build(4, scv, 130)
wait_buildstart(4, scv)

define_max(25, scv)
define_max(255, wraith)
define_max(40, marine)
define_max(4, dropship)
define_max(12, medic)
define_max(15, firebat)
define_max(10, siege_tank)
define_max(10, goliath)
define_max(12, ghost)

build(7, scv, 80)
wait_buildstart(7, scv)
build(1, barracks, 80)
wait_buildstart(1, barracks)
defenseuse_gg(1, marine)
defenseuse_ag(1, marine)
build(8, scv, 80)
wait_buildstart(8, scv)
multirun(cash)
build(1, supply_depot, 80)
wait_buildstart(1, supply_depot)

enemyowns_jump(hatchery, infantry build)
enemyowns_jump(command_center, metal build)
random_jump(160, metal build)

--infantry build--
build(2, barracks, 80)
wait_buildstart(2, barracks)
wait(100)
build(2, supply_depot, 80)
wait_buildstart(2, supply_depot)
wait(100)
build(3, barracks, 80)
wait_buildstart(3, barracks)
build(9, scv, 80)
wait_buildstart(9, scv)
wait(100)
build(1, engineering_bay, 80)
wait_buildstart(1, engineering_bay)
wait(100)
build(1, academy, 80)
wait_buildstart(1, academy)
defenseuse_gg(1, firebat)
defensebuild_gg(1, firebat)
build(10, scv, 80)
wait_buildstart(10, scv)
wait(100)
build(1, factory, 80)
wait_buildstart(1, factory)
wait(100)
build(4, barracks, 80)
wait_buildstart(4, barracks)
wait(100)
build(3, supply_depot, 80)
wait_buildstart(3, supply_depot)
train(6, marine)
upgrade(1, t_infantry_weapon, 100)
wait(100)
build(4, supply_depot, 80)
wait_buildstart(4, supply_depot)
farms_timing()
train(9, marine)
wait(100)
build(5, barracks, 80)
wait_buildstart(5, barracks)
build(11, scv, 80)
wait_buildstart(11, scv)
upgrade(1, marine_range, 100)
build(1, machine_shop, 80)
wait_buildstart(1, machine_shop)
train(4, firebat)
train(4, medic)
build(5, supply_depot, 80)
wait_buildstart(5, supply_depot)
multirun(comsat)
multirun(mshops)
multirun(turrets)
build(1, starport, 80)
wait_buildstart(1, starport)
wait(100)
build(1, armory, 80)
wait_buildstart(1, armory)
train(6, medic)
wait(100)
build(2, factory, 80)
wait_buildstart(2, factory)
wait(100)
build(1, control_tower, 80)
wait_buildstart(1, control_tower)
build(2, machine_shop, 80)
tech(stim_packs, 100)
multirun(science)
multirun(expo loop)
multirun(ebay upgrades)
multirun(armory upgrades)
define_max(10, wraith)
tech(cloaking_field, 100)
check_transports()
goto(primary attack)

--metal build--
define_max(25, scv)
define_max(10, marine)
define_max(5, dropship)
define_max(3, medic)
define_max(3, firebat)
define_max(25, siege_tank)
define_max(15, vulture)
define_max(25, goliath)
define_max(12, ghost)

build(2, supply_depot, 80)
wait_buildstart(2, supply_depot)
wait(100)
build(3, supply_depot, 80)
wait_buildstart(3, supply_depot)
build(9, scv, 80)
wait_buildstart(9, scv)
wait(100)
build(1, engineering_bay, 80)
wait_buildstart(1, engineering_bay)
build(10, scv, 80)
wait_buildstart(10, scv)
wait(100)
build(1, factory, 80)
wait_buildstart(1, factory)
wait(100)
build(2, factory, 80)
wait_buildstart(2, factory)
wait(100)
build(3, factory, 80)
wait_buildstart(3, factory)
wait(100)
build(4, supply_depot, 80)
wait_buildstart(4, supply_depot)
wait(100)
build(5, supply_depot, 80)
wait_buildstart(5, supply_depot)
farms_timing()
wait(100)
build(11, scv, 80)
wait_buildstart(11, scv)
build(3, machine_shop, 100)
wait_buildstart(1, machine_shop)
multirun(comsat)
multirun(mshops2)
multirun(turrets)
build(1, starport, 80)
wait_buildstart(1, starport)
wait(100)
build(1, armory, 80)
wait_buildstart(1, armory)
wait(100)
build(2, armory, 80)
wait_buildstart(2, armory)
build(1, control_tower, 80)
wait_buildstart(1, control_tower)
build(3, machine_shop, 80)
multirun(science)
multirun(expo loop)
multirun(armory upgrades)
define_max(10, wraith)
tech(cloaking_field, 100)
check_transports()
goto(metal attack)

######### ATTACKS

--primary attack--
multirun(infantry)
multirun(tanks)
multirun(wraiths)
attack_add(5, siege_tank)
attack_add(30, marine)
attack_add(12, firebat)
attack_add(4, goliath)
attack_add(8, medic)
attack_add(4, wraith)
wait(1000)
attack_prepare()
wait(500)
attack_do()
attack_clear()
notowns_jump(physics_lab, primary attack)

--final attack--
multirun(infantry)
multirun(tanks)
multirun(air ships)
attack_add(6, siege_tank)
attack_add(30, marine)
attack_add(15, firebat)
attack_add(5, goliath)
attack_add(8, medic)
attack_add(5, battlecruiser)
attack_add(5, wraith)
attack_add(1, science_vessel)
wait(1000)
attack_prepare()
wait(500)
attack_do()
attack_clear()
goto(final attack)

--metal attack--
multirun(tanks)
multirun(vultures)
multirun(goliaths)
multirun(wraiths)
attack_add(10, siege_tank)
attack_add(10, vulture)
attack_add(10, goliath)
attack_add(5, wraith)
wait(1000)
attack_prepare()
wait(500)
attack_do()
attack_clear()
notowns_jump(physics_lab, metal attack)

--final metal attack--
multirun(tanks)
multirun(vultures)
multirun(goliaths)
multirun(wraiths)
multirun(air ships)
attack_add(10, siege_tank)
attack_add(10, vulture)
attack_add(10, goliath)
attack_add(5, wraith)
attack_add(5, battlecruiser)
attack_add(1, science_vessel)
wait(1000)
attack_prepare()
wait(500)
attack_do()
attack_clear()
goto(final metal attack)

--infantry--
defenseuse_gg(1, marine)
defenseuse_gg(1, firebat)
defensebuild_gg(1, marine)
defensebuild_gg(1, firebat)
train(20, marine)
train(3, medic)
train(5, firebat)
train(30, marine)
train(6, medic)
train(10, firebat)
stop()

--wraiths--
defenseuse_aa(1, wraith)
defensebuild_aa(1, wraith)
train(5, wraith)
stop()

--tanks--
defenseuse_gg(1, siege_tank)
defensebuild_gg(1, siege_tank)
train(2, siege_tank)
wait(400)
train(4, siege_tank)
wait(400)
train(6, siege_tank)
wait(400)
train(8, siege_tank)
stop()

--goliaths--
defenseuse_gg(1, goliath)
defensebuild_gg(1, goliath)
train(4, goliath)
wait(400)
train(8, goliath)
stop()

--vultures--
defenseuse_gg(1, vulture)
defensebuild_gg(1, vulture)
train(4, vulture)
wait(400)
train(8, vulture)
stop()

--air ships--
defenseuse_gg(1, battlecruiser)
defensebuild_gg(1, battlecruiser)
train(4, battlecruiser)
train(1, science_vessel)
stop()

--ghosts--
defenseuse_gg(1, ghost)
defensebuild_gg(1, ghost)
place_guard(ghost, 1)
train(5, ghost)
wait(2500)
goto(ghosts)

######### BUILDS

--science--
wait(6000)
build(1, science_facility, 80)
wait_buildstart(1, science_facility)
wait(1000)
build(1, physics_lab, 80)
tech(yamato_gun, 80)
upgrade(1, science_vessel_mana, 80)
upgrade(1, wraith_mana, 80)
enemyowns_jump(command_center, covert)
goto(turrets2)

--covert--
build(2, science_facility, 80)
wait_buildstart(2, science_facility)
wait(1000)
build(1, covert_ops, 80)
multirun(turrets2)
multirun(covert upgrades)
build(2, command_center, 80)
wait_buildstart(2, command_center)
build(1, nuclear_silo, 80)
goto(ghosts)

--mshops--
tech(siege_mode, 100)
wait_train(1, goliath)
upgrade(1, goliath_range, 30)
stop()

--mshops2--
tech(siege_mode, 100)
tech(spider_mines, 100)
wait_train(1, goliath)
upgrade(1, goliath_range, 30)
stop()

########## EXPANSIONS

--expo loop--
expand(99, expo)
wait(6000)
send_suicide(0)
goto(expo loop)

--expo--
start_town()
build(1, command_center, 80)
wait_build(1, command_center)
multirun(comsat)
multirun(scvs)
multirun(turrets)
enemyowns_jump(command_center, expo4 buildings)
enemyowns_jump(nexus, expo2 buildings)
build(1, barracks, 80)
wait_buildstart(1, barracks)
wait(100)
random_jump(128, expo2 buildings)
build(2, barracks, 80)
goto(expo3 buildings)

--expo2 buildings--
build(1, factory, 80)
wait_build(1, factory)
build(1, machine_shop, 80)
goto(expo3 buildings)

--expo3 buildings--
build(1, starport, 80)
wait_build(1, starport)
build(1, control_tower, 80)
stop()

--expo4 buildings--
build(1, factory, 80)
wait_build(1, factory)
build(1, machine_shop, 80)
wait_buildstart(1, machine_shop)
build(2, factory, 80)
wait_build(2, factory)
build(2, machine_shop, 80)
wait_buildstart(2, machine_shop)
goto(expo3 buildings)

--scvs--
build(1, scv, 80)
wait_build(1, scv)
build(2, scv, 80)
wait_build(2, scv)
build(3, scv, 80)
wait_build(3, scv)
build(4, scv, 80)
wait_build(4, scv)
build(5, scv, 80)
wait_build(5, scv)
build(6, scv, 80)
stop()

########## UPGRADES

--ebay upgrades--
upgrade(1, t_infantry_armor, 30)
wait(4500)
upgrade(2, t_infantry_weapon, 30)
wait(4500)
upgrade(2, t_infantry_armor, 30)
stop()

--armory upgrades--
upgrade(1, t_vehicle_weapon, 30)
upgrade(1, t_ship_weapon, 30)
stop()

--covert upgrades--
tech(lockdown, 100)
wait(3000)
tech(personnel_cloaking, 30)
wait(3000)
upgrade(1, ghost_sight, 30)
wait(3000)
upgrade(1, ghost_mana, 30)
stop()

--turrets--
build(1, missile_turret, 70)
wait_build(1, missile_turret)
build(2, missile_turret, 70)
wait_build(2, missile_turret)
build(3, missile_turret, 70)
wait_build(3, missile_turret)
build(4, missile_turret, 70)
wait_build(4, missile_turret)
stop()

--turrets2--
build(5, missile_turret, 70)
wait_build(5, missile_turret)
build(6, missile_turret, 70)
wait_build(6, missile_turret)
stop()

--comsat--
player_need(1, academy)
build(1, comsat_station, 80)
stop() 

--cash--
give_money()
wait(1500)
goto(cash)