# Adun's Fury II Protoss v1.1
# bajadulce
# latest update: Sept, 2008

PMCx(1343, 101, aiscript):

start_town()
transports_off()
farms_notiming()

build(1, nexus, 150)
wait_buildstart(1, nexus)
build(4, probe, 130)
wait_buildstart(4, probe)

#if DEBUG
    debug(debugmsg, Adun's Fury II v1.1 Protoss)
    --debugmsg--
#endif

define_max(30, high_templar)
define_max(40, dragoon)
define_max(40, zealot)
define_max(3, shuttle)
define_max(2, arbiter)
define_max(5, observer)
define_max(255, scout)
define_max(10, carrier)
define_max(28, probe)

build(8, probe, 80)
wait_buildstart(8, probe)
build(1, pylon, 80)
wait_buildstart(1, pylon)
build(9, probe, 80)
wait_buildstart(9, probe)
build(1, gateway, 80)
wait_buildstart(1, gateway)
defenseuse_gg(2, zealot)
defensebuild_gg(2, zealot)
build(10, probe, 80)
wait_buildstart(10, probe)
build(2, pylon, 80)
wait_buildstart(2, pylon)
build(12, probe, 80)
wait_buildstart(12, probe)
build(1, cybernetics_core, 80)
wait_buildstart(1, cybernetics_core)
defenseuse_gg(1, dragoon)
defenseuse_ag(1, dragoon)
multirun(cash)
build(1, assimilator, 80)
wait_buildstart(1, assimilator)
build(2, gateway, 80)
wait_buildstart(2, gateway)
wait(100)
build(1, forge, 80)
wait_buildstart(1, forge)
wait(100)
build(3, gateway, 80)
wait_buildstart(3, gateway)
wait(100)
build(3, pylon, 80)
wait_buildstart(3, pylon)
farms_timing()
#random_jump(64, dt rush)
build(15, probe, 80)
wait_buildstart(15, probe)
upgrade(1, dragoon_range, 100)
upgrade(1, p_ground_weapon, 100)
build(1, citadel_of_adun, 80)
wait_buildstart(1, citadel_of_adun)
wait(100)
build(1, robotics_facility, 80)
wait_buildstart(1, robotics_facility)
train(5, zealot)
random_jump(156, norush 1)
train(6, zealot)
wait_train(5, zealot)
send_suicide(0)

--norush 1--
defensebuild_gg(1, dragoon)
defensebuild_ag(1, dragoon)
build(4, gateway, 80)
wait_buildstart(4, gateway)
multirun(base cannons)
train(6, zealot)
train(3, dragoon)
build(1, templar_archives, 80)
wait_buildstart(1, templar_archives)
build(1, observatory, 80)
wait_buildstart(1, observatory)
place_guard(observer, 1)
place_guard(observer, 2)
upgrade(1, zealot_speed, 150)
train(12, zealot)
train(7, dragoon)
attack_add(10, zealot)
attack_add(6, dragoon)
attack_prepare()
multirun(expo loop)
tech(psionic_storm, 130)
build(5, gateway, 80)
wait_buildstart(5, gateway)
multirun(air upgrades)
multirun(forge upgrades)
multirun(observer upgrades)
check_transports()
defenseuse_gg(1, dark_templar)
defenseuse_gg(1, archon)

enemyowns_jump(hatchery, zerg attack)

--attack loop--
attack_do()
attack_clear()
multirun(primary units)
wait(50)
multirun(secondary units)
wait(50)
multirun(observers)
wait(1000)
attack_add(25, zealot)
attack_add(20, dragoon)
attack_add(8, dark_templar)
attack_add(4, high_templar)
attack_add(6, archon)
attack_add(2, observer)
attack_prepare()
wait(500)
random_jump(64, random air)
goto(attack loop)

--random air--
build(2, stargate, 80)
wait_buildstart(2, stargate)
build(1, fleet_beacon, 80)
wait_buildstart(1, fleet_beacon)
build(1, arbiter_tribunal, 80)
wait_buildstart(1, arbiter_tribunal)
multirun(tier 3 upgrades)
defenseuse_gg(1, carrier)

--attack loop2--
attack_do()
attack_clear()
multirun(primary units)
wait(50)
multirun(secondary units)
wait(50)
multirun(observers)
wait(50)
multirun(air units)
wait(1000)
attack_add(25, zealot)
attack_add(20, dragoon)
attack_add(8, dark_templar)
attack_add(4, high_templar)
attack_add(6, archon)
attack_add(2, observer)
attack_add(4, carrier)
attack_add(1, arbiter)
attack_prepare()
wait(500)
goto(attack loop2)

--zerg attack--
define_max(4, dragoon)
attack_do()
attack_clear()
multirun(zealots)
wait(50)
multirun(secondary units)
wait(50)
multirun(observers)
wait(1000)
attack_add(25, zealot)
attack_add(8, dark_templar)
attack_add(4, high_templar)
attack_add(6, archon)
attack_add(2, observer)
attack_prepare()
wait(500)
build(2, stargate, 80)
wait_buildstart(2, stargate)
build(1, fleet_beacon, 80)
wait_buildstart(1, fleet_beacon)
build(1, arbiter_tribunal, 80)
wait_buildstart(1, arbiter_tribunal)
multirun(tier 3 upgrades)
defenseuse_gg(1, carrier)

--zerg attack loop--
attack_do()
attack_clear()
multirun(zealots)
wait(50)
multirun(secondary units)
wait(50)
multirun(observers)
wait(50)
multirun(air units)
wait(500)
attack_add(25, zealot)
attack_add(10, dark_templar)
attack_add(5, high_templar)
attack_add(8, archon)
attack_add(2, observer)
attack_add(4, carrier)
attack_add(1, arbiter)
attack_prepare()
wait(500)
goto(zerg attack loop)

--primary units--
defensebuild_gg(1, zealot)
defensebuild_gg(1, dragoon)
train(10, zealot)
train(10, dragoon)
train(20, zealot)
train(15, dragoon)
train(25, zealot)
train(20, dragoon)
stop()

--secondary units--
defensebuild_gg(1, dark_templar)
defensebuild_gg(1, archon)
train(4, dark_templar)
train(4, high_templar)
train(4, archon)
train(8, dark_templar)
train(6, high_templar)
train(6, archon)
train(8, dark_templar)
stop()

--zealots--
defensebuild_gg(1, zealot)
train(10, zealot)
train(20, zealot)
train(25, zealot)
stop()

--observers--
train(4, observer)
stop()

--air units--
defensebuild_gg(1, carrier)
train(2, carrier)
train(1, arbiter)
train(4, carrier)
stop()

--base cannons--
build(1, photon_cannon, 80)
wait_buildstart(1, photon_cannon)
wait(500)
build(2, photon_cannon, 80)
wait_buildstart(2, photon_cannon)
wait(500)
build(3, photon_cannon, 80)
wait_buildstart(3, photon_cannon)
wait(500)
build(4, photon_cannon, 80)
wait_buildstart(4, photon_cannon)
wait(500)
build(5, photon_cannon, 80)
stop()

--observer upgrades--
upgrade(1, observer_sight, 30)
wait(3000)
upgrade(1, observer_speed, 30)
stop()

--air upgrades--
upgrade(1, p_air_weapon, 30)
wait(4500)
upgrade(1, p_plating, 30)
stop()

--forge upgrades--
upgrade(1, p_armor, 30)
wait(4500)
upgrade(2, p_ground_weapon, 30)
stop()

--tier 3 upgrades--
wait_train(1, carrier)
upgrade(1, carrier_capacity, 30)
tech(stasis_field, 100)
stop()

--expo loop--
expand(99, expo)
wait(5000)
send_suicide(0)
goto(expo loop)

--expo--
start_town()
build(1, nexus, 80)
wait_buildstart(1, nexus)
build(1, pylon, 80)
wait_build(1, pylon)
build(2, pylon, 80)
wait_buildstart(2, pylon)
build(2, gateway, 80)
wait_buildstart(2, gateway)
multirun(gas)
build(3, photon_cannon, 80)
place_guard(dark_templar, 1)
place_guard(high_templar, 1)
build(8, probe, 80)
stop()

--gas--
build(1, assimilator, 80)
stop() 

--cash--
give_money()
wait(1500)
goto(cash)