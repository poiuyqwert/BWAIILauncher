# Adun's Fury II Zerg v1.1
# bajadulce
# latest update: Sept, 2008

ZMCx(1344, 101, aiscript):

start_town()
transports_off()
farms_notiming()

#if DEBUG
    debug(debugmsg, Adun's Fury II v1.1 Zerg)
    --debugmsg--
#endif


build(1, drone, 150)
wait_buildstart(1, drone)
build(1, hatchery, 150)
wait_buildstart(1, hatchery)
build(1, overlord, 130)
wait_buildstart(1, overlord)

define_max(20, drone)
define_max(50, zergling)
define_max(255, hydralisk)
define_max(255, queen)
define_max(20, mutalisk)
define_max(10, ultralisk)
define_max(6, guardian)
define_max(6, devourer)
define_max(2, defiler)

build(8, drone, 80)
wait_buildstart(8, drone)
multirun(cash)
build(1, spawning_pool, 80)
wait_buildstart(1, spawning_pool)
defensebuild_gg(1, zergling)
defenseuse_gg(1, zergling)
farms_timing()
multirun(random quick expo)
wait(100)
build(2, hatchery, 80)
wait_buildstart(2, hatchery)
multirun(creep)
build(1, hydralisk_den, 80)
wait_buildstart(1, hydralisk_den)
upgrade(1, zergling_speed, 100)
build(1, lair, 80)
wait_buildstart(1, lair)
random_jump(200, no ling rush)
train(12, zergling)
wait_train(10, zergling)
send_suicide(0)

--no ling rush--
multirun(hydra upgrades)
build(3, hatchery, 80)
wait_buildstart(3, hatchery)
random_jump(80, mutaling)
define_max(65, hydralisk)
defensebuild_gg(1, hydralisk)
defenseuse_gg(1, hydralisk)
attack_do()
attack_clear()
attack_add(14, hydralisk)
attack_add(12, zergling)
attack_prepare()
wait(500)
build(1, queen_nest, 80)
wait_buildstart(1, queen_nest)
multirun(evolution)
build(1, hive, 80)
wait_buildstart(1, hive)
build(2, lair, 80)
multirun(ultralisk cavern)
multirun(defiler mound)
multirun(overlord upgrades)
goto(primary attack1)

--mutaling--
build(1, spire, 100)
wait_buildstart(1, spire)
defenseuse_gg(1, mutalisk)
defensebuild_gg(1, mutalisk)
attack_do()
attack_clear()
attack_add(24, zergling)
attack_add(8, mutalisk)
attack_prepare()
wait(500)
build(1, queen_nest, 100)
wait_buildstart(1, queen_nest)
wait(100)
build(1, hydralisk_den, 80)
wait_buildstart(1, hydralisk_den)
multirun(hydra upgrades)
multirun(evolution)
build(1, hive, 80)
wait_buildstart(1, hive)
build(2, lair, 80)
multirun(overlord upgrades)
multirun(gspire2)
goto(primary attack2)

########### ATTACKS and UNITS

--primary attack1--
attack_do()
attack_clear()
multirun(zerglings)
multirun(hydras)
multirun(lurkers)
notowns_jump(ultralisk_cavern, skip ultras)
multirun(ultras)
multirun(defilers)
wait(800)
attack_add(20, zergling)
attack_add(30, hydralisk)
attack_add(8, ultralisk)
attack_add(1, defiler)
attack_prepare()
wait(500)
random_jump(96, spire)
goto(primary attack1)

--skip ultras--
wait(800)
attack_add(35, zergling)
attack_add(35, hydralisk)
attack_add(8, lurker)
attack_prepare()
wait(500)
goto(primary attack1)

--primary attack2--
attack_do()
attack_clear()
multirun(zerglings)
multirun(mutas)
notowns_jump(ultralisk_cavern, skip ultras2)
define_max(65, hydralisk)
wait(800)
attack_prepare()
wait(500)
goto(main attack)

--skip ultras2--
wait(500)
attack_prepare()
wait(500)
random_jump(128, ultralisk cavern2)
goto(primary attack2)

--main attack--
attack_do()
attack_clear()
multirun(zerglings)
multirun(hydras)
multirun(lurkers)
multirun(ultras)
multirun(mutas)
multirun(guards)
multirun(devourers)
wait(800)
attack_add(25, zergling)
attack_add(30, hydralisk)
attack_add(8, lurker)
attack_add(6, ultralisk)
attack_add(3, guardian)
attack_add(2, devourer)
attack_add(10, mutalisk)
attack_prepare()
wait(500)
goto(main attack)

--zerglings--
defenseuse_gg(1, zergling)
defensebuild_gg(1, zergling)
train(20, zergling)
stop()

--hydras--
defenseuse_gg(1, hydralisk)
defensebuild_gg(1, hydralisk)
train(30, hydralisk)
stop()

--lurkers--
defenseuse_gg(1, lurker)
defensebuild_gg(1, lurker)
train(6, lurker)
stop()

--ultras--
defenseuse_gg(1, ultralisk)
defensebuild_gg(1, ultralisk)
train(8, ultralisk)
stop()

--mutas--
defenseuse_gg(1, mutalisk)
defensebuild_gg(1, mutalisk)
train(10, mutalisk)
stop()

--guards--
defenseuse_gg(1, guardian)
defensebuild_gg(1, guardian)
train(4, guardian)
stop()

--devourers--
defenseuse_aa(1, devourer)
defensebuild_aa(1, devourer)
train(4, devourer)
stop()

--defilers--
train(2, defiler)
stop()

########### EXTRA BUILDS

--evolution--
build(1, evolution_chamber, 70)
wait_buildstart(1, evolution_chamber)
wait(50)
build(2, evolution_chamber, 70)
wait_buildstart(2, evolution_chamber)
wait(50)
build(3, evolution_chamber, 70)
wait_build(3, evolution_chamber)
goto(evo upgrades)

--ultralisk cavern--
wait(100)
notowns_jump(hive, ultralisk cavern)
build(1, ultralisk_cavern, 80)
wait_buildstart(1, ultralisk_cavern)
goto(ultra upgrades)

--ultralisk cavern2--
wait(100)
notowns_jump(hive, ultralisk cavern2)
build(1, ultralisk_cavern, 80)
wait_buildstart(1, ultralisk_cavern)
multirun(ultra upgrades)
define_max(65, hydralisk)
goto(main attack)

--defiler mound--
wait(100)
notowns_jump(hive, defiler mound)
build(1, defiler_mound, 80)
wait_buildstart(1, defiler_mound)
wait(2000)
tech(plague, 70)
stop()

--spire--
attack_do()
attack_clear()
build(1, spire, 80)
wait_buildstart(1, spire)

--gspire--
wait(100)
notowns_jump(spire, gspire)
build(1, greater_spire, 80)
goto(main attack)

--gspire2--
wait(100)
notowns_jump(spire, gspire2)
build(1, greater_spire, 80)
stop()

########### EXPOS

--random quick expo--
random_jump(96, expo loop)
expand(1, expo)

--expo loop--
wait(6500)
expand(99, expo)
send_suicide(0)
goto(expo loop)

--expo--
start_town()
build(1, hatchery, 80)
wait_build(1, hatchery)
build(3, drone, 80)
wait_buildstart(3, drone)
multirun(creep)
wait(2000)
build(2, hatchery, 80)
stop()

########### UPGRADES

--hydra upgrades--
upgrade(1, hydralisk_range, 30)
wait(2000)
upgrade(1, hydralisk_speed, 30)
wait(2000)
tech(lurker_aspect, 70)
defenseuse_gg(1, lurker)
defensebuild_gg(1, lurker)
place_guard(lurker, 0)
place_guard(lurker, 1)
train(2, lurker)
stop()

--evo upgrades--
upgrade(1, z_missile_attack, 30)
upgrade(1, z_melee_attack, 30)
upgrade(1, z_carapace, 30)
wait(4000)
upgrade(2, z_missile_attack, 30)
upgrade(2, z_melee_attack, 30)
upgrade(2, z_carapace, 30)
wait(4000)
upgrade(3, z_missile_attack, 30)
upgrade(3, z_melee_attack, 30)
upgrade(3, z_carapace, 30)
stop()

--ultra upgrades--
wait(100)
notowns_jump(ultralisk_cavern, ultra upgrades)
upgrade(1, ultralisk_speed, 30)
wait(2000)
upgrade(1, ultralisk_armor, 30)
stop()

--overlord upgrades--
wait(100)
notowns_jump(hive, overlord upgrades)
upgrade(1, zergling_attack, 30)
upgrade(1, overlord_capacity, 30)
upgrade(1, overlord_speed, 30)
wait(4000)
upgrade(1, overlord_sight, 30)
tech(burrowing, 30)
stop()

########### MISC

--creep--
build(1, creep_colony, 80)
wait_buildstart(1, creep_colony)
wait(200)
build(2, creep_colony, 80)
wait_buildstart(2, creep_colony)
wait(200)
build(3, creep_colony, 80)
wait_buildstart(3, creep_colony)
build(3, sunken_colony, 80)
wait(2000)
build(4, creep_colony, 80)
wait_buildstart(4, creep_colony)
wait(200)
build(5, creep_colony, 80)
wait_buildstart(5, creep_colony)
wait(200)
build(6, creep_colony, 80)
wait_buildstart(6, creep_colony)
build(3, spore_colony, 80)
stop()

--cash--
give_money()
wait(1500)
goto(cash) 