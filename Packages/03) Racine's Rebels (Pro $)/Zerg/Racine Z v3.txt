# Racine 3.0.1 Zerg 
# March 18, 2013

ZMCx(1344, 101, aiscript):
	start_town()
	transports_off()
	farms_notiming()

#if DEBUG
    debug(debugmsg, Racine 3 Zerg)
    --debugmsg--
#endif

	race_jump(vs_t_air, vs_z_air, vs_p_air)


		--vs_t_air--
	build(1, hatchery, 15)
	wait_build(1, hatchery)
	build(4, drone, 15)
	wait_build(4, drone)
	build(8, drone, 15)
	wait_buildstart(8, drone)
	multirun(cheats)
	build(1, spawning_pool, 15)
	wait_buildstart(1, spawning_pool)
	build(1, extractor, 15)
	wait_buildstart(1, extractor)
	farms_timing()
	build(9, drone, 15)
	wait_buildstart(9, drone)
	multirun(vta_expand_init)
	build(1, lair, 15)
	wait_buildstart(1, lair)
		groundmap_jump(vs_t_ground)
	define_max(8, zergling)
	define_max(24, scourge)
	build(12, drone, 15)
	wait_buildstart(12, drone)
	build(3, overlord, 15)
	wait_buildstart(3, overlord)
	build(1, spire, 15)
	wait_buildstart(1, spire)
	build(2, hatchery, 15)
	wait_buildstart(2, hatchery)
	upgrade(1, overlord_capacity, 15)
	build(1, creep_colony, 15)
	wait_buildstart(1, creep_colony)
	build(1, sunken_colony, 15)
	wait_buildstart(1, sunken_colony)
	defensebuild_gg(1, zergling)
	defensebuild_gg(1, mutalisk)
	defensebuild_ag(1, mutalisk)
	defensebuild_aa(1, mutalisk)
	defenseuse_gg(1, zergling)
	defenseuse_gg(1, mutalisk)
	defenseuse_ag(1, mutalisk)
	defenseuse_aa(1, mutalisk)
	upgrade(1, z_flyer_attack, 15)
	attack_do()
	attack_clear()
	attack_add(12, mutalisk)
	attack_prepare()
	multirun(vta_expo_loop2)
	wait(500)

	upgrade(1, overlord_speed, 15)
	build(1, hydralisk_den, 25)
	wait_buildstart(1, hydralisk_den)
	build(1, evolution_chamber, 20)
	wait_buildstart(1, evolution_chamber)
	defensebuild_aa(1, scourge)
	defenseuse_aa(1, scourge)
	guard_resources(scourge)
	defenseuse_gg(1, hydralisk)
	defenseuse_ga(1, hydralisk)
	tech(lurker_aspect, 20)
	build(2, creep_colony, 15)
	train(8, hydralisk)
	defensebuild_gg(1, lurker)
	defenseuse_gg(1, lurker)
	build(1, spore_colony, 15)
	multirun(vta_lurkermuta_loop)
	build(3, hatchery, 15)
	wait_buildstart(3, hatchery)
	upgrade(1, z_carapace, 15)
	multirun(vta_hive_upgrades)
	upgrade(1, hydralisk_range, 15)

		--vta_end_attack_loop--
	attack_do()
	attack_clear()
	attack_add(20, mutalisk)
	attack_add(4, scourge)
	attack_add(20, hydralisk)
	attack_add(15, lurker)
	attack_prepare()
	wait(500)

	goto(vta_end_attack_loop)


		--vta_lurkermuta_loop--
	attack_do()
	attack_clear()
	attack_add(10, lurker)
	attack_add(4, scourge)
	attack_add(12, mutalisk)
	attack_prepare()
	wait(650)

	goto(vta_lurkermuta_loop)


		--vta_unused_loop--
	train(1, mutalisk)
	train(16, mutalisk)
	train(24, mutalisk)
	train(32, mutalisk)
	train(40, mutalisk)
	wait_train(40, mutalisk)
	wait(1000)

	goto(vta_unused_loop)


		--vta_hive_upgrades--
	build(1, queen_nest, 15)
	wait_build(1, queen_nest)
	attack_do()
	attack_clear()
	attack_add(12, mutalisk)
	attack_add(4, scourge)
	attack_add(10, lurker)
	attack_prepare()
	wait(500)

	build(1, hive, 15)
	wait_buildstart(1, hive)
	upgrade(2, z_carapace, 15)
	upgrade(2, z_flyer_attack, 15)
	wait(4000) # up from 2000

	upgrade(3, z_carapace, 15)
	upgrade(3, z_flyer_attack, 15)
	wait(5000) # up from 2000

	upgrade(3, z_flyer_carapace, 15)
	stop()


		--vta_expo_loop2--
	expand(99, vta_expo3)
	wait(7000)

	goto(vta_expo_loop2)


		--vta_expo3--
	start_town()
	build(1, hatchery, 15)
	wait_build(1, hatchery)
	defensebuild_gg(1, mutalisk)
	defensebuild_aa(1, scourge)
	defensebuild_gg(1, lurker)
	defensebuild_gg(1, zergling)
	defensebuild_gg(1, hydralisk)
	defensebuild_gg(1, lurker)
	build(3, drone, 35)
	wait_build(3, drone)
	build(3, creep_colony, 35)
	wait_build(3, creep_colony)
	build(2, sunken_colony, 35)
	build(1, spore_colony, 35)
	build(9, drone, 15)
	build(1, extractor, 15)
	attack_do()
	attack_clear()
	attack_add(10, mutalisk)
	attack_add(4, scourge)
	attack_add(10, lurker)
	attack_prepare()
	wait(600)
	stop()


		--vs_t_ground--
	upgrade(1, zergling_speed, 15)
	defensebuild_gg(1, zergling)
	defenseuse_gg(1, zergling)
	build(9, drone, 20)
	wait_buildstart(9, drone)
	enemyowns_jump(nexus, vtg_mutalisks)
	random_jump(128, vtg_mutalisks)
	random_jump(128, vtg_hydralurker)
	attack_do()
	attack_clear()
	attack_add(20, zergling)
	attack_prepare()
	wait(100)


		--vtg_hydralurker--
	build(1, hydralisk_den, 15)
	wait_buildstart(1, hydralisk_den)
	build(2, hatchery, 15)
	wait_buildstart(2, hatchery)
	wait_build(1, lair)
	tech(lurker_aspect, 140)
	upgrade(1, overlord_capacity, 15)
	attack_do()
	attack_clear()
	attack_add(20, hydralisk)
	attack_add(6, lurker)
	multirun(vtg_evo_upgrades)
	attack_prepare()
	wait(500)

	upgrade(1, overlord_speed, 15)
	build(1, queen_nest, 15)
	wait_buildstart(1, queen_nest)
	multirun(vtg_3hatch_attack)
	defensebuild_gg(1, hydralisk)
	defenseuse_gg(1, hydralisk)
	defensebuild_gg(1, lurker)
	defenseuse_gg(1, lurker)
	build(1, hive, 20)
	wait_buildstart(1, hive)
	build(1, ultralisk_cavern, 15)
	wait_buildstart(1, ultralisk_cavern)
	defensebuild_gg(1, ultralisk)
	defenseuse_gg(1, ultralisk)
	build(1, defiler_mound, 15)
	wait_buildstart(1, defiler_mound)
	upgrade(1, ultralisk_armor, 15)
	tech(plague, 15)
	multirun(vtg_attack_loop)
	define_max(30, ultralisk)
	define_max(12, defiler)
	train(2, ultralisk)
	train(2, defiler)
	train(8, ultralisk)
	upgrade(1, ultralisk_speed, 15)
	tech(dark_swarm, 15)
	stop()


		--vtg_3hatch_attack--
	build(3, hatchery, 80)
	wait_buildstart(3, hatchery)
	attack_do()
	attack_clear()
	attack_add(30, hydralisk)
	attack_add(10, lurker)
	attack_prepare()
	wait(100)

	attack_do()
	attack_clear()
	attack_add(30, hydralisk)
	attack_add(10, lurker)
	attack_prepare()
	wait(100)

	stop()


		--vtg_attack_loop--
	attack_do()
	attack_clear()
	attack_add(12, ultralisk)
	attack_add(2, defiler)
	attack_add(26, zergling)
	attack_prepare()
	wait(300)

	attack_do()
	attack_clear()
	attack_add(2, defiler)
	attack_add(30, hydralisk)
	attack_add(8, lurker)
	attack_prepare()
	wait(300)
	goto(vtg_attack_loop)



		--vta_expo_loop--
	expand(99, vta_expo2)
	wait(6600)

	goto(vta_expo_loop)


		--vtg_evo_upgrades--
	build(3, evolution_chamber, 15)
	wait_buildstart(3, evolution_chamber)
	upgrade(1, z_missile_attack, 15)
	upgrade(1, z_melee_attack, 15)
	upgrade(1, z_carapace, 15)
	wait(4000)

	upgrade(2, z_missile_attack, 15)
	upgrade(2, z_melee_attack, 15)
	upgrade(2, z_carapace, 15)
	wait(5000)

	upgrade(1, zergling_attack, 15)
	upgrade(3, z_missile_attack, 15)
	upgrade(3, z_melee_attack, 15)
	upgrade(3, z_carapace, 15)
	stop()


		--vtg_mutalisks--
	build(2, hatchery, 20)
	wait_buildstart(2, hatchery)
	build(1, spire, 20)
	wait_buildstart(1, spire)
	upgrade(1, overlord_speed, 15)
	build(1, hydralisk_den, 20)
	define_max(255, hydralisk)
	build(10, drone, 20)
	wait_buildstart(10, drone)
	defenseuse_gg(1, mutalisk)
	defensebuild_gg(1, mutalisk)
	train(1, mutalisk)
	train(1, zergling)
	train(6, mutalisk)
	attack_do()
	attack_clear()
	attack_add(16, mutalisk)
	attack_add(18, zergling)
	attack_prepare()
	tech(lurker_aspect, 5)
	defensebuild_gg(1, hydralisk)
	defenseuse_gg(1, hydralisk)
	defensebuild_gg(1, lurker)
	defenseuse_gg(1, lurker)
	define_max(70, hydralisk)
	train(10, hydralisk)
	upgrade(1, overlord_capacity, 15)
	multirun(vtg_hydralurker_attack)
	upgrade(1, hydralisk_speed, 15)
	build(3, hatchery, 20)
	wait_buildstart(3, hatchery)
	build(1, queen_nest, 20)
	wait_buildstart(1, queen_nest)
	build(10, drone, 20)
	wait_buildstart(10, drone)
	build(3, evolution_chamber, 20)
	wait_buildstart(3, evolution_chamber)
	upgrade(1, hydralisk_range, 15)
	upgrade(1, z_melee_attack, 15)
	upgrade(1, z_missile_attack, 15)
	upgrade(1, z_carapace, 15)
	build(1, hive, 20)
	wait_buildstart(1, hive)
	build(11, drone, 20)
	wait_build(11, drone)
	build(1, ultralisk_cavern, 20)
	wait_buildstart(1, ultralisk_cavern)
	defensebuild_gg(2, ultralisk)
	defenseuse_gg(2, ultralisk)
	train(2, ultralisk)
	upgrade(2, z_melee_attack, 15)
	upgrade(2, z_missile_attack, 15)
	upgrade(2, z_carapace, 15)
	build(1, defiler_mound, 15)
	wait_buildstart(1, defiler_mound)
	multirun(vtg_attack_loop)
	upgrade(1, zergling_attack, 15)
	upgrade(1, ultralisk_armor, 15)
	tech(plague, 15)
	upgrade(1, ultralisk_speed, 15)
	tech(dark_swarm, 15)
	upgrade(3, z_melee_attack, 15)
	upgrade(3, z_missile_attack, 15)
	upgrade(3, z_carapace, 15)
	stop()


		--vtg_hydralurker_attack--
	attack_do()
	attack_clear()
	attack_add(20, hydralisk)
	attack_add(10, lurker)
	attack_prepare()
	wait(300)

	attack_do()
	attack_clear()
	attack_add(30, hydralisk)
	attack_add(10, lurker)
	attack_prepare()
	wait(300)

	notowns_jump(ultralisk_cavern, vtg_hydralurker_attack)
	stop() # vtg_attack_loop picks up the attack loop


		--vta_expand_init--
	expand(1, vta_expo)
	wait(5000)

	goto(vta_expo_loop)


		--vta_expo--
	start_town()
	define_max(12, zergling)
	build(1, hatchery, 15)
	wait_build(1, hatchery)
	defensebuild_gg(1, lurker)
	build(3, drone, 80)
	wait_buildstart(3, drone)
	build(3, creep_colony, 80)
	wait_buildstart(3, creep_colony)
	build(3, sunken_colony, 80)
	train(6, hydralisk)
	wait_train(6, hydralisk)

		--vta_expo_lurkercheck--
	train(6, lurker)
	wait(50)

	notowns_jump(lurker, vta_expo_lurkercheck)
	train(6, lurker)
	wait_train(6, lurker)
	send_suicide(0)
	train(15, hydralisk)
	train(8, lurker)
	train(25, hydralisk)
	train(9, lurker)
	train(35, hydralisk)
	train(10, lurker)
	train(36, hydralisk)
	wait_train(36, hydralisk)
	send_suicide(0)
	stop()


		--vta_expo2--
	start_town()
	build(1, hatchery, 15)
	wait_build(1, hatchery)
	build(3, drone, 15)
	wait_buildstart(3, drone)
	build(3, creep_colony, 15)
	wait_buildstart(3, creep_colony)
	build(3, sunken_colony, 15)
	build(1, extractor, 15)
	train(30, hydralisk)
	train(15, lurker)
	stop()


		--vs_z_air--
	build(1, hatchery, 80)
	wait_build(1, hatchery)
	build(7, drone, 80)
	wait_buildstart(7, drone)
	enemyowns_jump(spawning_pool, vza_rush_defense)
	build(8, drone, 80)
	wait_buildstart(8, drone)
	groundmap_jump(vs_z_ground)
	build(1, spawning_pool, 15)
	wait_buildstart(1, spawning_pool)
	multirun(cheats)
	build(1, extractor, 15)
	wait_buildstart(1, extractor)
	build(10, drone, 15)
	wait_buildstart(10, drone)
	build(1, lair, 15)
	wait_buildstart(1, lair)
	build(12, drone, 15)
	wait_buildstart(12, drone)
	build(1, spire, 15)
	wait_buildstart(1, spire)
	build(2, hatchery, 15)
	wait_buildstart(2, hatchery)
	upgrade(1, overlord_capacity, 20)
	build(13, drone, 15)
	wait_buildstart(13, drone)
	build(1, evolution_chamber, 15)
	wait_buildstart(1, evolution_chamber)
	defensebuild_aa(1, mutalisk)
	defenseuse_aa(1, mutalisk)
	defensebuild_ag(1, mutalisk)
	defenseuse_ag(1, mutalisk)
	defensebuild_aa(1, scourge)
	defenseuse_aa(1, scourge)
	build(1, creep_colony, 15)
	wait_buildstart(1, creep_colony)
	train(1, mutalisk)
	multirun(vza_air_upgrades)
	build(1, spore_colony, 15)
	wait_buildstart(1, spore_colony)
	attack_do()
	attack_clear()
	attack_add(10, mutalisk)
	attack_add(4, scourge)
	attack_prepare()
	wait(500)

	multirun(vza_expo_loop)
	guard_resources(scourge)
	upgrade(1, overlord_speed, 20)
	multirun(vza_hive)
	upgrade(1, zergling_speed, 15)
	attack_do()
	attack_clear()
	attack_add(16, mutalisk)
	attack_add(6, scourge)
	attack_add(20, zergling)
	attack_prepare()
	wait(500)


		--vza_attack_loop--
	attack_do()
	attack_clear()
	attack_add(24, mutalisk)
	attack_add(8, scourge)
	attack_add(20, zergling)
	wait(600)

	goto(vza_attack_loop)


		--vza_hive--
	build(1, queen_nest, 15)
	wait_build(1, queen_nest)
	build(1, hive, 15)
	wait_build(1, hive)
	upgrade(1, zergling_attack, 15)
	stop()


		--vza_expo_loop--
	expand(99, vza_expo)
	wait(7500)

	goto(vza_expo_loop)


		--vza_expo--
	start_town()
	build(1, hatchery, 15)
	wait_build(1, hatchery)
	build(3, drone, 15)
	wait_build(3, drone)
	build(3, creep_colony, 15)
	wait_build(3, creep_colony)
	build(2, spore_colony, 15)
	wait_buildstart(2, spore_colony)
	build(1, sunken_colony, 15)
	wait_buildstart(1, sunken_colony)
	build(8, drone, 15)
	wait_buildstart(8, drone)
	build(1, extractor, 15)
	stop()


		--vza_air_upgrades--
	upgrade(1, z_flyer_attack, 15)
	upgrade(1, z_carapace, 15)
	wait(4000) # up from 3000

	upgrade(1, z_flyer_carapace, 15)
	wait(4000) # up from 3000

	upgrade(2, z_carapace, 15)
	upgrade(2, z_flyer_attack, 15)
	wait(4000) # up from 3000

	upgrade(3, z_carapace, 15)
	upgrade(2, z_flyer_carapace, 15)
	wait(5000) # up from 3000

	upgrade(3, z_flyer_attack, 15)
	wait(5000) # up from 3000

	upgrade(3, z_flyer_carapace, 15)
	stop()


		--vs_z_ground--
	random_jump(100, vzg_quickattack) # hydra zvz
	define_max(1, overlord)
	build(9, drone, 80)
	wait_buildstart(9, drone)
	enemyowns_jump(spawning_pool, vza_rush_defense)
	define_max(30, overlord)
	farms_timing()

		--vzg_mutaling--
	multirun(cheats)
	build(10, drone, 80)
	wait_buildstart(10, drone)
	build(2, hatchery, 80)
	wait_buildstart(2, hatchery)
	wait(300)

	build(1, spawning_pool, 80)
	wait_buildstart(1, spawning_pool)
	build(1, extractor, 80)
	wait_buildstart(1, extractor)
	defensebuild_gg(1, zergling)
	defenseuse_gg(1, zergling)
	upgrade(1, zergling_speed, 80)
	build(1, lair, 150)
	wait_buildstart(1, lair)
	random_jump(128, vzg_mutaling_attack_skip)
	train(12, zergling)
	wait_train(12, zergling)
	send_suicide(0)

		--vzg_mutaling_attack_skip--
	defensebuild_gg(1, zergling)
	defenseuse_gg(1, zergling)
	train(12, zergling)
	build(1, creep_colony, 80)
	wait_buildstart(1, creep_colony)
	build(1, spire, 250)
	wait_buildstart(1, spire)
	build(1, sunken_colony, 80)
	build(1, evolution_chamber, 80)
	upgrade(1, overlord_speed, 80)
	upgrade(1, z_carapace, 80)
	upgrade(1, z_flyer_attack, 80)
	define_max(60, mutalisk)
	defensebuild_gg(1, mutalisk)
	defenseuse_gg(1, mutalisk)
	defensebuild_aa(1, mutalisk)
	defenseuse_aa(1, mutalisk)
	random_jump(128, vzg_scourge_rush)

		--vzg_scourge_rush_return--
	train(10, mutalisk)
	train(16, zergling)
	train(4, scourge)
	#wait_train(10, mutalisk) # Caused a bad pause in testing.
	wait_force(10, mutalisk) # New
	attack_add(16, zergling)
	attack_add(10, mutalisk)
	attack_add(4, scourge)
	attack_prepare()
	upgrade(1, overlord_capacity, 80)
	wait(200)

	attack_do()
	attack_clear()
	multirun(vzg_expo_loop)
	build(3, hatchery, 80)
	wait_buildstart(3, hatchery)
	upgrade(2, z_carapace, 80)
	upgrade(2, z_flyer_attack, 80)
	multirun(vzg_muta_training)

		--vzg_muta_attack_loop--
	attack_add(36, zergling)
	attack_add(8, mutalisk)
	attack_add(10, scourge)
	attack_prepare()
	wait(400)

	attack_do()
	attack_clear()
	attack_add(24, mutalisk)
	attack_add(8, scourge)
	attack_prepare()
	wait(400)

	attack_do()
	attack_clear()
	send_suicide(0)
	goto(vzg_muta_attack_loop)


		--vzg_muta_training--
	train(10, mutalisk)
	train(6, scourge)
	train(14, zergling)
	train(16, mutalisk)
	train(10, scourge)
	train(26, zergling)
	train(28, mutalisk)
	train(30, zergling)
	train(36, mutalisk)
	train(20, scourge)
	wait(900)

	goto(vzg_muta_training)


		--vzg_expo_loop--
	expand(99, vzg_expo)
	wait(6700)

	goto(vzg_expo_loop)


		--vzg_quickattack--
	build(9, drone, 80)
	wait_buildstart(9, drone)
	multirun(cheats)
	farms_timing()
	wait_build(9, drone)
	build(1, spawning_pool, 80)
	wait_buildstart(1, spawning_pool)
	build(10, drone, 80)
	wait_buildstart(10, drone)
	build(2, hatchery, 80)
	wait_buildstart(2, hatchery)
	build(1, extractor, 80)
	wait_buildstart(1, extractor)
	defensebuild_gg(1, zergling)
	defenseuse_gg(1, zergling)
	random_jump(128, vzg_quickattack_skip)
	attack_do()
	attack_clear()
	attack_prepare()
	attack_add(20, zergling)
	wait(300)


		--vzg_quickattack_skip--
	multirun(vzg_3evo_upgrades)
	build(1, hydralisk_den, 80)
	wait_buildstart(1, hydralisk_den)
	build(11, drone, 80)
	wait_buildstart(11, drone)
	#multirun(vzg_3evo_upgrades) # Don't need two of these, see above
	defensebuild_gg(1, hydralisk)
	defenseuse_gg(1, hydralisk)
	defensebuild_ga(1, hydralisk)
	defenseuse_ga(1, hydralisk)
	defensebuild_aa(1, hydralisk)
	defenseuse_aa(1, hydralisk)
	
	upgrade(1, hydralisk_speed, 20) # New
		
	train(8, hydralisk)
	build(2, lair, 80)
	wait_buildstart(2, lair)
	build(3, hatchery, 80)
	wait_buildstart(3, hatchery)
	
	upgrade(1, hydralisk_range, 20) # New
	
	multirun(vzg_expo_loop)
	train(20, hydralisk)
	upgrade(1, overlord_speed, 30)
	upgrade(1, zergling_speed, 30)
	#expand(1, vzg_hydra_suicide) 
	upgrade(1, overlord_capacity, 30)
	define_max(90, hydralisk)
	
		--vzg_hydra_loop--
# Let's simplify this whole mess.
	attack_do()
	attack_clear()
	
	train(20, hydralisk)
	attack_add(40, hydralisk)
	attack_prepare()
	wait(450)
	
	goto(vzg_hydra_loop)


		# --vzg_hydra_suicide--
	# start_town()
	# train(20, hydralisk)
	# wait_train(20, hydralisk)
	# send_suicide(0)
	# stop()


		# --vzg_hive_tech--
	# build(1, queen_nest, 30)
	# wait_build(1, queen_nest)
	# build(1, hive, 30)
	# stop()


		--vzg_3evo_upgrades--
	build(1, creep_colony, 80)
	wait_build(1, creep_colony)
	build(1, sunken_colony, 80)
	wait_force(4, hydralisk) # Changed: from wait_train
	build(2, evolution_chamber, 80)
	wait_buildstart(2, evolution_chamber)
	build(3, creep_colony, 80)
	wait_buildstart(3, creep_colony)
	upgrade(1, z_missile_attack, 30)
	upgrade(1, z_carapace, 30)
	build(2, spore_colony, 80)
	wait_build(2, spore_colony)
	wait(4000) # up from 3000
	wait_build(1, Zerg Lair)
	upgrade(2, z_missile_attack, 30)
	upgrade(2, z_carapace, 30)
	
	build(1, queen_nest, 30)
	wait_build(1, queen_nest)
	build(1, hive, 30)
	wait_build(1, hive)
	wait(2000)
	
	#wait(5000) # up from 3000

	upgrade(3, z_missile_attack, 30)
	upgrade(3, z_carapace, 30)
	stop()

		# --vzg_hydra_attack--
	# define_max(90, hydralisk)
	# attack_do()
	# attack_clear()
	# multirun(vzg_hydra_attack2)
	# attack_add(40, hydralisk)
	# wait(750)

	# attack_prepare()
	# stop()
	#goto(vzg_hydra_attack)


		# --vzg_hydra_attack2--
	# attack_do()
	# attack_clear()
	# attack_add(30, hydralisk)
	# attack_add(15, zergling)
	# wait(300)

	# attack_prepare()
	# stop()


		--vzg_expo--
	start_town()
	build(1, hatchery, 80)
	wait_build(1, hatchery)
	get_oldpeons(3)
	build(1, drone, 80)
	build(3, creep_colony, 100)
	wait_build(3, creep_colony)
	build(2, spore_colony, 100)
	build(1, sunken_colony, 100)
	build(10, drone, 80)
	build(1, extractor, 80)
	stop()


		--vzg_scourge_rush--
	train(8, scourge)
	wait_train(8, scourge)
	wait(400)

	send_suicide(0)
	goto(vzg_scourge_rush_return)


		--vza_rush_defense--
	build(1, spawning_pool, 80)
	wait_buildstart(1, spawning_pool)
	define_max(30, overlord)
	farms_timing()
	build(10, drone, 80)
	wait_buildstart(10, drone)
	defensebuild_gg(1, zergling)
	defenseuse_gg(1, zergling)
	train(6, zergling)
	goto(vzg_mutaling)


		--vs_p_air--
	groundmap_jump(vs_p_ground)
	random_jump(128, vs_z_air) # muta/ling stuff
	define_max(50, mutalisk)
	define_max(255, hydralisk)
	build(1, hatchery, 20)
	wait_build(1, hatchery)
	build(9, drone, 20)
	wait_buildstart(9, drone)
	build(1, spawning_pool, 20)
	wait_buildstart(1, spawning_pool)
	build(1, extractor, 20)
	wait_buildstart(1, extractor)
	build(10, drone, 20)
	wait_buildstart(10, drone)
	farms_timing()
	multirun(cheats)
	build(1, lair, 110)
	build(2, hatchery, 70)
	build(1, hydralisk_den, 60)
	build(1, creep_colony, 20)
	wait_buildstart(1, creep_colony)
	multirun(vpa_overlord_upgrades)
	build(2, lair, 60)
	wait_buildstart(2, lair)
	define_max(60, hydralisk)
	upgrade(1, hydralisk_range, 30)
	train(2, hydralisk)
	build(1, sunken_colony, 20)
	wait_buildstart(1, sunken_colony)
	train(8, hydralisk)
	train(12, hydralisk)
	train(20, hydralisk)
	train(24, hydralisk)
	wait_train(24, hydralisk)
	send_suicide(0)
	multirun(vpa_expo_loop)
	build(1, spire, 20)
	wait_buildstart(1, spire)
	define_max(30, scourge)
	multirun(vpa_evo_upgrades) # extra hatch, too
	multirun(vpa_attack_loop)
	defensebuild_gg(1, mutalisk)
	defenseuse_gg(1, mutalisk)
	defensebuild_aa(1, scourge)
	defenseuse_aa(1, scourge)
	attack_do()
	attack_clear()
	attack_add(16, mutalisk)
	attack_add(4, scourge)
	attack_prepare()
	wait(250)

	build(1, queen_nest, 20)
	wait_build(1, queen_nest)
	build(1, hive, 20)
	stop()


		--vpa_overlord_upgrades--
	upgrade(1, overlord_capacity, 5)
	wait(2000)

	upgrade(1, overlord_speed, 5)
	stop()


		--vpa_attack_loop--
	train(30, hydralisk)
	wait_train(24, hydralisk)
	attack_do()
	attack_clear()
	attack_add(32, hydralisk)
	attack_prepare()
	wait(30)

	train(10, mutalisk)
	train(20, hydralisk)
	train(2, scourge)
	attack_do()
	attack_clear()
	attack_add(15, mutalisk)
	attack_add(4, scourge)
	attack_add(24, hydralisk)
	attack_prepare()
	wait(30)

	goto(vpa_attack_loop)


		--vpa_expo_loop--
	expand(99, vpa_expo)
	wait(7100)

	goto(vpa_expo_loop)


		--vpa_expo--
	start_town()
	defenseuse_gg(1, hydralisk)
	defenseuse_ga(1, hydralisk)
	build(1, hatchery, 20)
	wait_build(1, hatchery)
	build(3, drone, 20)
	wait_build(3, drone)
	build(3, creep_colony, 80)
	wait_build(3, creep_colony)
	build(2, sunken_colony, 80)
	build(1, spore_colony, 80)
	build(8, drone, 20)
	build(1, extractor, 20)
	defensebuild_gg(1, hydralisk)
	defensebuild_ga(1, hydralisk)
	stop()


		--vpa_evo_upgrades--
	build(1, evolution_chamber, 30)
	wait_buildstart(1, evolution_chamber)
	build(2, evolution_chamber, 30)
	wait_buildstart(2, evolution_chamber)
	build(3, hatchery, 20)
	wait_buildstart(3, hatchery)
	upgrade(1, z_missile_attack, 15)
	upgrade(1, z_carapace, 15)
	upgrade(1, z_flyer_carapace, 15)
	wait(2400)

	upgrade(2, z_missile_attack, 15)
	upgrade(2, z_carapace, 15)
	upgrade(2, z_flyer_carapace, 15)
	wait(2400)

	upgrade(3, z_missile_attack, 15)
	upgrade(3, z_carapace, 15)
	upgrade(3, z_flyer_carapace, 15)
	stop()


		--vs_p_ground--
	random_jump(64, vs_t_air)
	random_jump(64, vpg_mutaling)
	define_max(60, zergling)
	build(1, hatchery, 20)
	wait_build(1, hatchery)
	build(9, drone, 20)
	wait_buildstart(9, drone)
	expand(1, vpg_expo)
	multirun(cheats)
	wait(300)

	build(1, spawning_pool, 20)
	wait_buildstart(1, spawning_pool)
	defensebuild_gg(1, zergling)
	defenseuse_gg(1, zergling)
	farms_timing()
	build(10, drone, 20)
	wait_buildstart(10, drone)
	build(1, extractor, 20)
	wait_buildstart(1, extractor)
	build(2, hatchery, 20)
	wait_buildstart(2, hatchery)
	upgrade(1, zergling_speed, 20)
	random_jump(120, vpg_rush_skip)
	train(22, zergling)
	wait_train(22, zergling)
	send_suicide(0)
	
	
		--vpg_rush_skip--
	build(1, hydralisk_den, 20)
	wait_buildstart(1, hydralisk_den)
	train(20, zergling)
	upgrade(1, hydralisk_speed, 20)
	train(2, hydralisk)
	defensebuild_gg(1, hydralisk)
	defenseuse_gg(1, hydralisk)
	defensebuild_ga(1, hydralisk)
	defenseuse_ga(1, hydralisk)
	multirun(vpg_expo_suicide_loop)
	train(6, hydralisk)
	build(1, lair, 30)
	wait_buildstart(1, lair)
	attack_do()
	attack_clear()
	attack_add(20, hydralisk)
	attack_add(20, zergling)
	attack_prepare()
	wait(400)

	upgrade(1, hydralisk_range, 20)
	multirun(vpg_evo_upgrades)
	upgrade(1, overlord_speed, 50)
	send_suicide(0)
	build(5, hatchery, 20)
	wait_buildstart(5, hatchery)
	attack_do()
	attack_clear()
	attack_add(28, hydralisk)
	attack_add(20, zergling)
	attack_prepare()
	wait(500)

	send_suicide(0)
	attack_do()
	attack_clear()
	attack_add(50, hydralisk)
	attack_add(20, zergling)
	attack_prepare()
	wait(500)

	upgrade(1, overlord_capacity, 50)

		--vpg_attack_loop--
	attack_do()
	attack_clear()
	attack_add(55, hydralisk)
	attack_add(20, zergling)
	attack_prepare()
	wait(400)

	goto(vpg_attack_loop)


		--vpg_evo_upgrades--
	build(3, evolution_chamber, 80)
	wait_buildstart(3, evolution_chamber)
	upgrade(1, z_carapace, 15)
	upgrade(1, z_melee_attack, 15)
	upgrade(1, z_missile_attack, 15)
	wait(4000) # up from 3000)

	upgrade(2, z_carapace, 15)
	upgrade(2, z_melee_attack, 15)
	upgrade(2, z_missile_attack, 15)
	build(1, queen_nest, 20)
	wait_buildstart(1, queen_nest)
	build(8, drone, 20)
	wait_buildstart(8, drone)
	build(1, hive, 20)
	wait_buildstart(1, hive)
	wait(3000)

	upgrade(1, zergling_attack, 15)
	upgrade(3, z_carapace, 15)
	upgrade(3, z_melee_attack, 15)
	upgrade(3, z_missile_attack, 15)
	stop()


		--vpg_expo_suicide_loop--
	expand(99, vpg_expo)
	wait(6500)

	send_suicide(0)
	goto(vpg_expo_suicide_loop)


		--vpg_expo--
	start_town()
	build(1, hatchery, 20)
	wait_build(1, hatchery)
	build(2, drone, 80)
	wait_build(2, drone)
	build(2, creep_colony, 80)
	wait_buildstart(2, creep_colony)
	build(2, sunken_colony, 80)
	wait_buildstart(2, sunken_colony)
	build(5, drone, 20)
	wait_build(5, drone)
	build(12, drone, 20)
	build(1, extractor, 20)
	stop()


		--vpg_mutaling--
	define_max(40, drone)
	define_max(100, zergling)
	define_max(30, mutalisk)
	define_max(20, ultralisk)
	build(9, drone, 80)
	wait_buildstart(9, drone)
	build(2, hatchery, 80)
	wait_buildstart(2, hatchery)
	build(1, spawning_pool, 80)
	wait_buildstart(1, spawning_pool)
	build(1, extractor, 80)
	wait_buildstart(1, extractor)
	farms_timing()
	multirun(cheats)
	train(6, zergling)
	defensebuild_gg(1, zergling)
	defenseuse_gg(1, zergling)
	build(1, lair, 80)
	wait_buildstart(1, lair)
	upgrade(1, zergling_speed, 30)
	build(10, drone, 80)
	wait_buildstart(10, drone)
	build(1, creep_colony, 80)
	wait_buildstart(1, creep_colony)
	train(8, zergling)
	build(1, sunken_colony, 80)
	wait_buildstart(1, sunken_colony)
	build(1, spire, 80)
	wait_buildstart(1, spire)
	defenseuse_ga(1, mutalisk)
	defenseuse_gg(1, mutalisk)
	train(10, zergling)
	build(11, drone, 80)
	wait_buildstart(11, drone)
	build(5, overlord, 80)
	wait_build(1, spire)
	upgrade(1, overlord_speed, 30)
	train(8, mutalisk)
	wait_train(8, mutalisk)
	attack_add(14, zergling)
	attack_add(8, mutalisk)
	attack_prepare()
	multirun(vpg_extra_tech)
	multirun(vpg_expo_loop2)
	build(3, hatchery, 80)
	wait_buildstart(3, hatchery)
	train(20, zergling)
	train(4, scourge)
	train(30, zergling)
	train(40, zergling)
	train(50, zergling)
	wait_train(50, zergling)
	send_suicide(0)
	guard_all()
	build(12, drone, 80)
	wait_buildstart(12, drone)
	build(5, hatchery, 80)
	wait_buildstart(5, hatchery)
	wait_build(1, ultralisk_cavern)
	multirun(vpg_attack_loop2)

		--vpg_defense_loop--
	defenseclear_gg()
	defenseclear_ga()
	defensebuild_gg(1, ultralisk)
	defenseuse_gg(1, ultralisk)
	defensebuild_ga(1, mutalisk)
	defenseuse_ga(1, mutalisk)
	defensebuild_gg(1, zergling)
	defenseuse_gg(1, zergling)
	wait(400)

	goto(vpg_defense_loop)


		--vpg_attack_loop2--
	attack_do()
	attack_clear()
	attack_add(2, defiler)
	attack_add(8, ultralisk)
	attack_add(4, scourge)
	attack_add(30, zergling)
	attack_prepare()
	wait(300)

	give_money()
	goto(vpg_attack_loop2)


		--vpg_extra_tech--
	build(1, queen_nest, 80)
	wait_buildstart(1, queen_nest)
	build(9, overlord, 80)
	wait_buildstart(9, overlord)
	build(2, evolution_chamber, 80)
	wait_build(2, evolution_chamber)
	upgrade(1, z_melee_attack, 30)
	upgrade(1, z_carapace, 30)
	build(1, hive, 130)
	wait_build(1, hive)
	build(1, defiler_mound, 120)
	build(1, ultralisk_cavern, 120)
	wait_build(1, ultralisk_cavern)
	upgrade(1, overlord_capacity, 30)
	tech(plague, 30)
	upgrade(1, zergling_attack, 30)
	upgrade(2, z_melee_attack, 30)
	upgrade(2, z_carapace, 30)
	upgrade(1, ultralisk_speed, 30)
	wait(5000) # up from 3800

	tech(dark_swarm, 30)
	upgrade(3, z_melee_attack, 30)
	upgrade(3, z_carapace, 30)
	upgrade(1, ultralisk_armor, 30)
	stop()


		--vpg_expo_loop2--
	expand(99, vpg_expo2)
	wait(5100)

	goto(vpg_expo_loop2)


		--vpg_expo2--
	start_town()
	build(1, hatchery, 80)
	wait_build(1, hatchery)
	build(4, drone, 80)
	wait_buildstart(4, drone)
	build(3, creep_colony, 80)
	wait_build(3, creep_colony)
	build(3, sunken_colony, 150)
	wait_buildstart(3, sunken_colony)
	build(8, drone, 80)
	wait_build(8, drone)
	build(10, drone, 80)
	wait_build(10, drone)
	build(1, extractor, 80)
	stop()


		--cheats--
	give_money()
	wait(1500)

	goto(cheats)


